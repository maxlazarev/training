<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FINFormletterServiceClass_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(classStr(FormletterService))]
public final class FINFormletterServiceClass_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>postSalesOrderInvoice</Name>
				<Source><![CDATA[
    FormletterOutputContract postSalesOrderInvoice(SalesFormLetterInvoiceContract _contract)
    {
        if (_contract.FINParmPostInvoiceProposal())
        {
            FINCustInvoiceJourProForma::restoreMarkupForSalesOrder( _contract.FINParmProformaInvoiceJourRecId(), _contract.parmSalesTable());
        }

        FormletterOutputContract ret = next postSalesOrderInvoice(_contract);

        FINCreditLineDetails creditLineDetails;
        FINCreditLine creditLine;
        
        select firstonly ExternalNotes from creditLineDetails
            join TableId from creditLine
            where creditLine.RecId == creditLineDetails.CreditLineRefRecId
            &&    creditLine.JourRefRecId == _contract.FINParmInvoiceJourRecId()
            &&    creditLine.JourRefTableId == tableNum(CustInvoiceJour);

        if(creditLineDetails.ExternalNotes)
        {
            DNRAdditionalNoteTable additionalNoteTable;
            additionalNoteTable.initFromCommon(ret.parmJournal());
            additionalNoteTable.InvoiceNote = creditLineDetails.ExternalNotes;
            additionalNoteTable.insert();
        }

        this.FINCopyDocuRef(_contract.FINParmProformaInvoiceJourRecId(), ret.parmJournal().RecId);

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>FINCopyDocuRef</Name>
				<Source><![CDATA[
    protected void FINCopyDocuRef(RecId _custInvoiceJourProFormaRecId, RecId _custInvoiceJourRecId)
    {
        DocuRef proposalDocuRef;

        while select proposalDocuRef
            where proposalDocuRef.RefTableId == tableNum(FINCustInvoiceJourProforma)
            && proposalDocuRef.RefRecId == _custInvoiceJourProFormaRecId
        {
            DocuRef::createFromDocuRef(proposalDocuRef, _custInvoiceJourRecId, tableNum(CustInvoiceJour));
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>