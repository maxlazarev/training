<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FINAmendmentSubTypeCtrl</Name>
	<SourceCode>
		<Declaration><![CDATA[
class FINAmendmentSubTypeCtrl
{
    FormComboBoxControl subTypeCtrl;
    Map                 selection2IndexMap;
    Map                 index2SelectionMap;
    Set                 addedElements;
    Map                 textMap;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    public void build()
    {
        DictEnum     dictEnum = new DictEnum(enumnum(FINAmendmentSubType));
        Counter      indexNo, selection;
        str          text;

        subTypeCtrl.clear();

        while (indexNo < dictEnum.values())
        {
            FINAmendmentSubType amendmentSubType = dictEnum.index2Value(indexNo);
            if (!addedElements.in(amendmentSubType))
            {
                indexNo++;
                continue;
            }

            if (textMap.exists(amendmentSubType))
            {
                text = textMap.lookup(amendmentSubType);
            }
            else
            {
                text = dictEnum.index2Label(indexNo);
            }

            subTypeCtrl.item(selection);
            subTypeCtrl.add(text);

            selection2IndexMap.insert(selection, amendmentSubType);
            index2SelectionMap.insert(amendmentSubType, selection);
            indexNo++;
            selection++;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>index2Selection</Name>
				<Source><![CDATA[
    public int index2Selection(FINAmendmentSubType _amendmentSubType)
    {
        int selection = -1;

        if (index2SelectionMap && index2SelectionMap.exists(_amendmentSubType))
        {
            selection = index2SelectionMap.lookup(_amendmentSubType);
        }

        return selection;
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    protected void init()
    {
        selection2IndexMap = new Map(Types::Integer, Types::Enum);
        index2SelectionMap = new Map(Types::Enum, Types::Integer);
        addedElements      = new Set(Types::Enum);
        textMap            = new Map(Types::Integer, Types::String);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        this.init();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmsubTypeCtrl</Name>
				<Source><![CDATA[
    public FormComboBoxControl parmsubTypeCtrl(FormComboBoxControl _subTypeCtrl = subTypeCtrl)
    {
        subTypeCtrl = _subTypeCtrl;
        return subTypeCtrl;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addElements</Name>
				<Source><![CDATA[
    public void addElements(Set _addedElements)
    {
        addedElements = _addedElements;
    }

]]></Source>
			</Method>
			<Method>
				<Name>selection2Index</Name>
				<Source><![CDATA[
    public FINAmendmentSubType selection2Index(int _index)
    {
        FINAmendmentSubType amendmentSubType;

        if (selection2IndexMap && selection2IndexMap.exists(_index))
        {
            amendmentSubType = selection2IndexMap.lookup(_index);
        }

        return amendmentSubType;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setSelection</Name>
				<Source><![CDATA[
    public void setSelection(FINAmendmentSubType _amendmentSubType)
    {
        int selection = 0;

        if (this.index2Selection(_amendmentSubType) != -1)
        {
            selection = this.index2Selection(_amendmentSubType);
        }

        subTypeCtrl.selection(selection);
    }

]]></Source>
			</Method>
			<Method>
				<Name>setText</Name>
				<Source><![CDATA[
    public void setText(FINAmendmentSubType _amendmentSubType, str _text)
    {
        textMap.insert(_amendmentSubType, _text);
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    private static FINAmendmentSubTypeCtrl construct()
    {
        return new FINAmendmentSubTypeCtrl();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newsubTypeCtrl</Name>
				<Source><![CDATA[
    public static FINAmendmentSubTypeCtrl newsubTypeCtrl(FormComboBoxControl _subTypeCtrl)
    {
        FINAmendmentSubTypeCtrl subTypeCtrl = FINAmendmentSubTypeCtrl::construct();
        subTypeCtrl.parmsubTypeCtrl(_subTypeCtrl);
        return subTypeCtrl;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>