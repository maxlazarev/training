<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FINROROrderAmendmentLogTable_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(ROROrderAmendmentLog))]
public final class FINROROrderAmendmentLogTable_Extension
{
    // Chain-of-Command wrapper methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initFromTmpOrderAmendmentDialogTable</Name>
				<Source><![CDATA[
    public void initFromTmpOrderAmendmentDialogTable(RORTmpOrderAmendmentDialogTable _tmpOrderAmendmentDialogTable)
    {
        next initFromTmpOrderAmendmentDialogTable(_tmpOrderAmendmentDialogTable);

        FINInvoicePeriodAmendment invoicePeriodAmendment;

        select firstonly RecId from invoicePeriodAmendment
            where invoicePeriodAmendment.RefRecId   == _tmpOrderAmendmentDialogTable.RefRecId
            && invoicePeriodAmendment.RefTableId    == _tmpOrderAmendmentDialogTable.RefTableId
            && invoicePeriodAmendment.FromDateTime  == _tmpOrderAmendmentDialogTable.EffectiveDateTimeFrom
            && invoicePeriodAmendment.ToDateTime    == _tmpOrderAmendmentDialogTable.EffectiveDateTimeTo;
        
        if (invoicePeriodAmendment.RecId)
        {
            this.FINPeriodAmendmentRefRecId = invoicePeriodAmendment.RecId;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromCommon</Name>
				<Source><![CDATA[
    protected void initFromCommon(Common _buffer, FieldId _fieldId)
    {
        next initFromCommon(_buffer, _fieldId);

        switch (_buffer.TableId)
        {
            case tableNum(FINManualPriceComponent):
                this.FINinitFromManualPriceComponent(_buffer,_fieldId);
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>FINinitFromManualPriceComponent</Name>
				<Source><![CDATA[
    protected void FINinitFromManualPriceComponent(FINManualPriceComponent _manualPriceComponent, FieldId _fieldId)
    {
        this.initFromCommon(_manualPriceComponent.parentRecord(), _fieldId);

        if (_fieldId != 0)
        {
            switch (_fieldId)
            {
                case fieldNum(FINManualPriceComponent, Description):
                    this.StrValue = _manualPriceComponent.Description;
                    this.OldStrValue = _manualPriceComponent.orig().Description;
                    break;

                case fieldNum(FINManualPriceComponent, AmountCur):
                    this.RealValue = _manualPriceComponent.AmountCur;
                    this.OldRealValue = _manualPriceComponent.orig().AmountCur;
                    break;

                case fieldNum(FINManualPriceComponent, Keep):
                    this.StrValue =  _manualPriceComponent.Keep ? "@SYS60867" : '';
                    this.OldStrValue = _manualPriceComponent.orig().Keep ? "@SYS60867" : '';
                    break;
            }
        }
        else
        {
            this.StrValue = (this.AmendmentAction == DNRAmendmentAction::Deleted) ? '' :  _manualPriceComponent.Description;
            this.OldStrValue = (this.AmendmentAction == DNRAmendmentAction::Created) ? '' : _manualPriceComponent.orig().Description;

            if (this.AmendmentAction == DNRAmendmentAction::Created)
            {
                this.RealValue = _manualPriceComponent.AmountCur;
            }
            else if (this.AmendmentAction == DNRAmendmentAction::Deleted)
            {
                this.OldRealValue = _manualPriceComponent.AmountCur;
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>