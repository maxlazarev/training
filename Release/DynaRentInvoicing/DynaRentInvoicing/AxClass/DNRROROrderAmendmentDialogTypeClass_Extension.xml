<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRROROrderAmendmentDialogTypeClass_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(classStr(ROROrderAmendmentDialogType))]
public final class DNRROROrderAmendmentDialogTypeClass_Extension
{
    // event handler methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>runFinancialChanges_post</Name>
				<Source><![CDATA[
    [PostHandlerFor(classStr(ROROrderAmendmentDialogType), staticMethodStr(ROROrderAmendmentDialogType, runFinancialChanges))]
    public static void runFinancialChanges_post(XppPrePostArgs _args)
    {
        INVInvoicePeriodHost invoicePeriodHost = _args.getArg('_invoicePeriodHost');
        Map changesMapWithString = _args.getArg('_changesMap');
        Map changesMap = new Map(Types::Container, Types::Container);
        MapEnumerator enumarator = changesMapWithString.getEnumerator();

        while (enumarator.moveNext())
        {
            TransDateTime fromDateTime;
            TransDateTime toDateTime;
            str priceComponentStr;
            FINInvoicePeriodPriceComponent priceComponent;
            anytype newValue, oldValue;
            RORAmendmentReasonText reasonText;

            [fromDateTime, toDateTime, priceComponentStr] = enumarator.currentKey();
            [newValue, oldValue, reasonText] = enumarator.currentValue();
            priceComponent = ROROrderAmendmentDialogType::FINStr2PriceComponent(priceComponentStr);

            changesMap.insert([fromDateTime, toDateTime, priceComponent], [newValue, oldValue, reasonText]);
        }

        // Considering credit functionality is only linked to Rental, and validations are already added in later steps,
        // _mustSetPendingCredit parameter is set to true for all rental lines
        FINInvoicePeriodManager_FinancialChange::runFromMap(invoicePeriodHost, changesMap, (invoicePeriodHost.TableId == tableNum(SalesLine) || invoicePeriodHost.TableId == tableNum(PurchLine)));
    }

]]></Source>
			</Method>
			<Method>
				<Name>FINStr2PriceComponent</Name>
				<Source><![CDATA[
    // extension methods
    public static FINInvoicePeriodPriceComponent FINStr2PriceComponent(str _priceComponentStr)
    {
        FINInvoicePeriodPriceComponent priceComponent;

        switch (_priceComponentStr)
        {
            case 'CalendarId' :
                priceComponent = FINInvoicePeriodPriceComponent::CalendarId;
                break;
            case 'LinePercent' :
                priceComponent = FINInvoicePeriodPriceComponent::LinePercent;
                break;
            case 'RentalModeId' :
                priceComponent = FINInvoicePeriodPriceComponent::RentalModeId;
                break;
            case 'RentalPrice' :
                priceComponent = FINInvoicePeriodPriceComponent::RentalPrice;
                break;
            case 'InvoiceProfileId' :
                priceComponent = FINInvoicePeriodPriceComponent::InvoiceProfileId;
                break;
        }

        return priceComponent;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>