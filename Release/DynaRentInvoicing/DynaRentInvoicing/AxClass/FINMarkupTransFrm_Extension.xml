<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FINMarkupTransFrm_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(MarkupTrans))]
public final class FINMarkupTransFrm_Extension
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    void  init(FormRun callerForm)
    {
        next init(callerForm);

        FormRun formRun = this.args().caller();

        if (formRun && formRun.name() == formStr(FINRentalCreditWizard))
        {
            this.initFromCreditWizard();
        }

        if (formRun && formRun.name() == formStr(FINCustInvoiceJournalProForma))
        {
            this.changeMarkupTransEdit(false);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>changeMarkupTransEdit</Name>
				<Source><![CDATA[
    public void changeMarkupTransEdit(boolean _enable)
    {
        markupTrans_ds.allowEdit(_enable);
        markupTrans_ds.allowCreate(_enable);
        markupTrans_ds.allowDelete(_enable);
        markupTrans_ds.insertIfEmpty(_enable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromCreditWizard</Name>
				<Source><![CDATA[
    private void initFromCreditWizard()
    {
        QueryBuildDataSource qbdsMarkupTrans = markupTrans_ds.query().dataSourceTable(tableNum(MarkupTrans));

        switch (this.args().menuItemName())
        {
            case menuItemDisplayStr(FINMarkupTransHeadingSalesAmendment):
                FormRun formRun = this.args().caller();
                FINTmpInvoicePeriodAmendment tmpInvoicePeriodAmendment = formRun.dataSource(formDataSourceStr(FINRentalCreditWizard, TmpInvoicePeriodAmendment)).cursor();
                SalesTable salesTable = SalesTable::find(tmpInvoicePeriodAmendment.OrderId);

                qbdsMarkupTrans.addRange(fieldNum(MarkupTrans, FINOrigTableId)).value(queryValue(tableNum(SalesTable)));
                qbdsMarkupTrans.addRange(fieldNum(MarkupTrans, FINOrigRecId)).value(queryValue(salesTable.RecId));
                break;
        }

        qbdsMarkupTrans.addRange(fieldNum(MarkupTrans, FINMarkupAccountType)).value(queryValue(FINMarkupAccountType::None));

        this.changeMarkupTransEdit(false);

        FINDisplayRentalOrderId.visible(true);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>