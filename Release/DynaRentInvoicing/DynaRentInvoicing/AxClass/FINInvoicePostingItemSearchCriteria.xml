<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FINInvoicePostingItemSearchCriteria</Name>
	<SourceCode>
		<Declaration><![CDATA[
class FINInvoicePostingItemSearchCriteria extends FINInvoicePostingSearchCriteria
{
    private FINInvoicePostingSearchParameters   searchParameters;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new (FINInvoicePostingSearchParameters _searchParameters)
    {
        searchParameters    = _searchParameters;
        this.buildSearchValuesList();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static FINInvoicePostingItemSearchCriteria construct(FINInvoicePostingSearchParameters _searchParameters)
    {
        return new FINInvoicePostingItemSearchCriteria(_searchParameters);
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildSearchValuesList</Name>
				<Source><![CDATA[
    protected void buildSearchValuesList()
    {
        this.buildSearchValuesListForTableGroupAll();
    }

]]></Source>
			</Method>
			<Method>
				<Name>populateInvoicePostingSearchState</Name>
				<Source><![CDATA[
    public void populateInvoicePostingSearchState(anytype _searchValue)
    {
        FINInvoicePosting invoicePosting = invoicePostingSearchState.parmInvoicePosting();
        invoicePosting.initFromPostingParameterBase(searchParameters);
        invoicePosting.ItemCode = _searchValue;
        invoicePosting.ItemRelation = FINInvoicePosting::itemRelation(_searchValue, searchParameters.ItemId, searchParameters.ItemGroup);
    }

]]></Source>
			</Method>
			<Method>
				<Name>mustSearchCriteria</Name>
				<Source><![CDATA[
    public boolean mustSearchCriteria(anytype _searchValue)
    {
        ItemRelation itemRelation = FINInvoicePosting::itemRelation(_searchValue, searchParameters.ItemId, searchParameters.ItemGroup);
        return this.itemRelationExists(_searchValue, itemRelation);
    }

]]></Source>
			</Method>
			<Method>
				<Name>itemRelationExists</Name>
				<Source><![CDATA[
    private boolean itemRelationExists(TableGroupAll _itemCode, ItemRelation _itemRelation)
    {
        FINInvoicePosting   localInvoicePosting;
        container           cachedResult;
        boolean             result;

        if (_itemCode == TableGroupAll::All || _itemRelation == '')
        {
            // All always exists
            return true;
        }

        cachedResult = FINInvoicePostingSetupCache::getItemRelationExists(_itemCode, _itemRelation);

        if (cachedResult == conNull())
        {
            select firstonly RecId from localInvoicePosting
            where localInvoicePosting.ItemCode == _itemCode
            &&    localInvoicePosting.ItemRelation == _itemRelation;

            result = (localInvoicePosting.RecId != 0);
            FINInvoicePostingSetupCache::insertItemRelationExists(_itemCode, _itemRelation, result);
        }
        else
        {
            [result] = cachedResult;
        }

        return result;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>