<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRASRORSalesTableForm_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(RORSalesTable))]
public final class DNRASRORSalesTableForm_Extension
{
    // Event-handler methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>DNRASEvent_RORSalesTable_SalesLineDS_OnInitialized</Name>
				<Source><![CDATA[
    [FormDataSourceEventHandler(formDataSourceStr(RORSalesTable, SalesLine), FormDataSourceEventType::Initialized)]
    public static void DNRASEvent_RORSalesTable_SalesLineDS_OnInitialized(FormDataSource _sender, FormDataSourceEventArgs _eventArgs)
    {
        if (isConfigurationkeyEnabled(configurationKeyNum(DNRASAssignSerial)))
        {
            Args args = _sender.formRun().args();

            if (args.caller() && args.callerName() == formStr(DNRASSalesPurchOrderSummaryOverview))
            {
                _sender.queryBuildDataSource().addRange(fieldNum(SalesLine, ItemId)).value(args.parm());
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNRASEvent_RORSalesTable_InventDimDS_inventSerialId_OnValidated</Name>
				<Source><![CDATA[
    [FormDataFieldEventHandler(formDataFieldStr(RORSalesTable, InventDim, inventSerialId), FormDataFieldEventType::Validated)]
    public static void DNRASEvent_RORSalesTable_InventDimDS_inventSerialId_OnValidated(FormDataObject _sender, FormDataFieldEventArgs _eventArgs)
    {
        if (isConfigurationkeyEnabled(configurationKeyNum(DNRASAssignSerial)))
        {
            boolean ret = true;
            SalesLine salesLine = _sender.datasource().formRun().dataSource(formDataSourceStr(RORSalesTable, SalesLine)).cursor();

            if (!RORRentalLine::DNRASValidateInventSerialIdUpdate(salesLine))
            {
                ret = checkFailed("@DNR:AssignSerialBlockManualAssignmentRental");
            }

            if (!ret)
            {
                FormDataFieldCancelEventArgs args = _eventArgs as FormDataFieldCancelEventArgs;
                args.cancel(true);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNRASEvent_RORSalesTable_SalesLineDS_SalesQty_OnValidated</Name>
				<Source><![CDATA[
    [FormDataFieldEventHandler(formDataFieldStr(RORSalesTable, SalesLine, SalesQty), FormDataFieldEventType::Validated)]
    public static void DNRASEvent_RORSalesTable_SalesLineDS_SalesQty_OnValidated(FormDataObject _sender, FormDataFieldEventArgs _eventArgs)
    {
        if (isConfigurationkeyEnabled(configurationKeyNum(DNRASAssignSerial)))
        {
            boolean ret = true;
            SalesLine salesLine = _sender.datasource().cursor();

            if (!RORRentalLine::DNRASValidateRentalQtyUpdate(salesLine))
            {
                ret = checkFailed("@DNR:AssignSerialBlockRentalQtyChange");
            }

            if (!ret)
            {
                FormDataFieldCancelEventArgs args = _eventArgs as FormDataFieldCancelEventArgs;
                args.cancel(true);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNRASEvent_RORSalesTable_DNRASSplitRentalLineButtonCTRL_OnClicked</Name>
				<Source><![CDATA[
    [FormControlEventHandler(formControlStr(RORSalesTable, DNRASSplitRentalLineButton), FormControlEventType::Clicked)]
    public static void DNRASEvent_RORSalesTable_DNRASSplitRentalLineButtonCTRL_OnClicked(FormControl _sender, FormControlEventArgs _e)
    {
        FormRun formRun = _sender.formRun();
        FormDataSource formDSSalesLine = formRun.dataSource(formDataSourceStr(RORSalesTable, SalesLine));
        SalesLine salesLine = formDSSalesLine.cursor();

        if (salesLine.RORTradeType != RORTradeType::SalesPurch)
        {
            ttsbegin;

            SalesTable salesTable = formRun.dataSource(formDataSourceStr(RORSalesTable, SalesTable)).cursor();
            boolean reConfirm = DNRASSplitEngine_Sales::mustReconfirmSplitLines(salesLine);

            DNRASSplitEngine splitEngine = DNRASSplitEngine::construct(salesLine, salesLine.SalesQty - 1);
            splitEngine.run();
            
            if (reConfirm)
            {
                splitEngine.confirmSplitLines();
            }

            ttscommit;
        }

        formDSSalesLine.research(true);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>