<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRASSalesLineTable_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(SalesLine))]
public final class DNRASSalesLineTable_Extension
{
    // Extension methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>DNRASMaySplitLine</Name>
				<Source><![CDATA[
    public boolean DNRASMaySplitLine()
    {
        return (this.SalesQty > 1 && this.inventTable().BOBIsSerializedRentalItem());
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNRASEditSubRentalGrossAmount</Name>
				<Source><![CDATA[
    static edit RORRentalGrossAmount DNRASEditSubRentalGrossAmount(SalesLine _salesLine, boolean _set, Amount _grossAmount)
    {
        Object formRunObj = _salesLine.dataSource().formRun() as Object;
        return formRunObj.DNRASSetSubRentalGrossAmount(_salesLine, _set, _grossAmount);
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNRASSalesQtyModified</Name>
				<Source><![CDATA[
    public void DNRASSalesQtyModified()
    {
        SalesLine salesLineOrig = this.orig();

        SalesOrderedQty salesQtyBeforeUpdate = salesLineOrig.SalesQty;

        // This functionality is only supported until (and including) the 'confirmed' status, so we are only concerned with the below quantities.
        this.QtyOrdered = this.calcQtyOrdered();
        this.RemainSalesPhysical = this.SalesQty;
        this.RemainInventPhysical = this.QtyOrdered;

        this.RORRentalGrossAmount = this.SalesPrice * this.SalesQty;
        this.LineAmount = this.calcLineAmountForced();
        this.RORDeliveryGrossAmount = (this.RORDeliveryGrossAmount / salesQtyBeforeUpdate) * this.SalesQty;
        this.RORDeliveryNetAmount = this.RORDeliveryGrossAmount - ((this.RORDeliveryGrossAmount / 100) * this.RORDeliveryPercentage);
        this.RORReturnGrossAmount = (this.RORReturnGrossAmount / salesQtyBeforeUpdate) * this.SalesQty;
        this.RORReturnNetAmount = this.RORReturnGrossAmount - ((this.RORReturnGrossAmount / 100) * this.RORReturnPercentage);
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNRASEvent_SalesLine_OnUpdating</Name>
				<Source><![CDATA[
    // Event-handler methods

    [SuppressBPWarningAttribute("BPParameterNotUsed", "The parameter '_eventArgs' is mandatory for event-handlers."),
     DataEventHandler(tableStr(SalesLine), DataEventType::Updating)]
    public static void DNRASEvent_SalesLine_OnUpdating(Common _sender, DataEventArgs _eventArgs)
    {
        if (isConfigurationkeyEnabled(configurationKeyNum(DNRASAssignSerial)))
        {
            SalesLine salesLine = _sender as SalesLine;
            SalesLine salesLineOrig = salesLine.orig();

            if (salesLine.SalesQty != salesLineOrig.SalesQty)
            {
                DNASRentalLineQtyManager qtyManager = DNASRentalLineQtyManager::construct(salesLine);
                qtyManager.run();
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>