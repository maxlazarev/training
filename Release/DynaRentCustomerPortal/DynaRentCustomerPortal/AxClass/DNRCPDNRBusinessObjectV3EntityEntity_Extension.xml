<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRCPDNRBusinessObjectV3EntityEntity_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(dataEntityViewStr(DNRBusinessObjectV3Entity))]
public final class DNRCPDNRBusinessObjectV3EntityEntity_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>postLoad</Name>
				<Source><![CDATA[
    public void postLoad()
    {
        next postLoad();

        if (isConfigurationkeyEnabled(configurationKeyNum(DNRCustomerPortal)))
        {
            BOBObjectView   objectView;
            EcoResProduct   ecoResProduct;
            InventTable     inventTable;
            InventSerial    inventSerial;

            DataAreaId dataAreaId = curExt();

            select firstonly Name, Address from objectView
            where objectView.ObjectId == this.ObjectId
                join TableId from ecoResProduct
                where ecoResProduct.DisplayProductNumber == objectView.RentalTypeId
                    outer join TableId from InventTable
                    where inventTable.Product       == ecoResProduct.RecId
                    &&    inventTable.DataAreaId    == dataAreaId
                        outer join ProdDate from inventSerial
                        where inventSerial.ItemId           == inventTable.ItemId
                        &&    inventSerial.InventSerialId   == this.InventSerialId;

            this.Name           = objectView.Name;
            this.ProductionDate = inventSerial.ProdDate;
            this.Address        = objectView.Address;

            BOBObjectMaintenancePlan    objectMaintenancePlan;
            BOBDefaultMaintenancePlan   defaultMaintenancePlan;

            select firstonly NextDate from objectMaintenancePlan
            order by NextDate asc
            where objectMaintenancePlan.ObjectId      == this.ObjectId
                join TableId from defaultMaintenancePlan
                where defaultMaintenancePlan.RecId    == objectMaintenancePlan.DefaultPlanRecId
                &&   (defaultMaintenancePlan.PlanType == BOBMaintenancePlanType::TimeBased
                ||    defaultMaintenancePlan.PlanType == BOBMaintenancePlanType::TimeAndConsumptionBased);

            this.NextMaintenanceDate = objectMaintenancePlan.NextDate;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>