<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNREventDNRRentalOrderHeaderEntity</Name>
	<SourceCode>
		<Declaration><![CDATA[
class DNREventDNRRentalOrderHeaderEntity
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initializeEntityDataSource_pre</Name>
				<Source><![CDATA[
    [PreHandlerFor(tableStr(DNRRentalOrderHeaderEntity), tableMethodStr(DNRRentalOrderHeaderEntity, initializeEntityDataSource))]
    public static void initializeEntityDataSource_pre(XppPrePostArgs _eventArgs)
    {
        DNRRentalOrderHeaderEntity          hostObject          = _eventArgs.getThis() as DNRRentalOrderHeaderEntity;
        DataEntityDataSourceRuntimeContext  parmDataSourceCtx   = _eventArgs.getArg('_dataSourceCtx') as DataEntityDataSourceRuntimeContext;

        switch(parmDataSourceCtx.name())
        {
            case dataEntityDataSourceStr(DNRRentalOrderHeaderEntity, RORRentalDateTimes):
                RORRentalDateTimes rentalDateTimes = parmDataSourceCtx.getBuffer();
                
                rentalDateTimes.ExpectedOffRentDateTime = hostObject.DNRExpectedOffRentDateTime;
                rentalDateTimes.OffRentDateTime         = hostObject.DNROffRentDateTime;
                rentalDateTimes.OnRentDateTime          = hostObject.DNROnRentDateTime;
                
                parmDataSourceCtx.setBuffer(rentalDateTimes);
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertEntityDataSource_pre</Name>
				<Source><![CDATA[
    //In the duplicate entity
    [PreHandlerFor(tableStr(DNRRentalOrderHeaderEntity), tableMethodStr(DNRRentalOrderHeaderEntity, insertEntityDataSource))]
    public static void insertEntityDataSource_pre(XppPrePostArgs _eventArgs)
    {
        DNRRentalOrderHeaderEntity          hostObject          = _eventArgs.getThis() as DNRRentalOrderHeaderEntity;
        DataEntityRuntimeContext            parmEntityCtx       = _eventArgs.getArg('_entityCtx') as DataEntityRuntimeContext;
        DataEntityDataSourceRuntimeContext  parmDataSourceCtx   = _eventArgs.getArg('_dataSourceCtx') as DataEntityDataSourceRuntimeContext;

        switch(parmDataSourceCtx.name())
        {
            case dataEntityDataSourceStr(DNRRentalOrderHeaderEntity, SalesTable):
                if (!hostObject.SalesOrderNumber)
                {
                    SalesTable  rentalOrderHeader = parmDataSourceCtx.getBuffer();
                    rentalOrderHeader.SalesId = NumberSeq::newGetNumFromId(SalesParameters::numRefSalesId().NumberSequenceId).num();
                    parmDataSourceCtx.setBuffer(rentalOrderHeader);
                }
                break;

            case dataEntityDataSourceStr(DNRRentalOrderHeaderEntity, RORRentalDateTimes):
                RORRentalDateTimes rentalDateTimes = parmDataSourceCtx.getBuffer();
                SalesTable  rentalOrderHeader = parmEntityCtx.getRuntimeContextByName(dataEntityDataSourceStr(DNRRentalOrderHeaderEntity, SalesTable)).getBuffer();

                rentalDateTimes.ExpectedOffRentDateTime = hostObject.DNRExpectedOffRentDateTime;
                rentalDateTimes.OffRentDateTime         = hostObject.DNROffRentDateTime;
                rentalDateTimes.OnRentDateTime          = hostObject.DNROnRentDateTime;

                rentalDateTimes.RefRecId                = rentalOrderHeader.RecId;
                rentalDateTimes.RefTableId              = rentalOrderHeader.TableId;

                rentalDateTimes.insert();
                
                parmDataSourceCtx.setBuffer(rentalDateTimes);
                parmDataSourceCtx.setDatabaseOperation(DataEntityDatabaseOperation::None);
                break;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>