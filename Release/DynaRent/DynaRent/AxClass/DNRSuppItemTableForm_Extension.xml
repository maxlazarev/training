<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRSuppItemTableForm_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(SuppItemTable))]
public final class DNRSuppItemTableForm_Extension
{
    private RORTradeType tradeType;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    // chain-of-command wrapper methods
    public void  init()
    {
        str     caption;
        boolean isRental;

        if (this.args())
        {
            tradeType   = str2int(this.args().parm());
            isRental    = (tradeType != RORTradeType::SalesPurch);

            switch (tradeType)
            {
                case RORTradeType::Rental :
                    caption = "@DNR6418";
                    break;
    
                case RORTradeType::SubRental :
                    caption = "@DNR6416";
                    break;
    
                default:
                    caption = "@SYS58282";
            }
        }

        next init();

        this.design().caption(strfmt('%1 - %2', caption, module));
    }

]]></Source>
			</Method>
			<Method>
				<Name>initParameters</Name>
				<Source><![CDATA[
    void initParameters()
    {
        next initParameters();

        if (this.args().dataset() == tableNum(smmBusRelTable))
        {
            smmBusRelTable  = this.args().record();
            fromModule      = module;
            this.RORinitsmmBusRelTable();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>RORinitsmmBusRelTable</Name>
				<Source><![CDATA[
    // extension methods

    public void RORinitsmmBusRelTable()
    {
        if (module == ModuleInventCustVend::Cust)
        {
            custTable       = CustTable::findByPartyRecId(smmBusRelTable.Party);
            accountRelation = custTable.AccountNum;
        }
        else
        {
            vendTable       = VendTable::findByPartyRecId(smmBusRelTable.Party);
            accountRelation = vendTable.AccountNum;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>RORparmTradeType</Name>
				<Source><![CDATA[
    public RORTradeType RORparmTradeType(RORTradeType _tradeType = tradeType)
    {
        tradeType = _tradeType;
        return tradeType;
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_SuppItemTableDS_OnInitValue</Name>
				<Source><![CDATA[
    // event handler methods

    [FormDataSourceEventHandler(formDataSourceStr(SuppItemTable, SuppItemTable), FormDataSourceEventType::InitValue)]
    public static void DNREvent_SuppItemTableDS_OnInitValue(FormDataSource _sender, FormDataSourceEventArgs _eventArgs)
    {
        FormRun         formRun         = _sender.formRun() as FormRun;
        Object          formRunObject   = formRun as Object;
        SuppItemTable   suppItemTable   = _sender.cursor() as SuppItemTable;

        suppItemTable.RORTradeType     = formRunObject.RORparmTradeType();
        suppItemTable.SuppItemOptional = NoYes::Yes;
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_SuppItemTableForm_RORKitRequiredLevelCtrl_OnModified</Name>
				<Source><![CDATA[
    [FormDataFieldEventHandler(formDataFieldStr(SuppItemTable, SuppItemTable, RORKitRequiredLevel), FormDataFieldEventType::Modified)]
    public static void DNREvent_SuppItemTableForm_RORKitRequiredLevelCtrl_OnModified(FormDataObject _sender, FormDataFieldEventArgs _e)
    {
        FormDataSource      suppItemTableDS = _sender.datasource();
        SuppItemTable       suppItemTable   = suppItemTableDS.cursor();

        suppItemTableDS.object(fieldNum(SuppItemTable, SuppItemOptional)).enabled(suppItemTable.RORKitRequiredLevel == RORKitRequiredLevel::Recommended);
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_SuppItemTableForm_OnActivated</Name>
				<Source><![CDATA[
    [FormDataSourceEventHandler(formDataSourceStr(SuppItemTable, SuppItemTable), FormDataSourceEventType::Activated)]
    public static void DNREvent_SuppItemTableForm_OnActivated(FormDataSource _sender, FormDataSourceEventArgs e)
    {
        SuppItemTable   suppItemTable   = _sender.cursor();

        _sender.object(fieldNum(SuppItemTable, SuppItemOptional)).enabled(suppItemTable.RORKitRequiredLevel == RORKitRequiredLevel::Recommended);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>