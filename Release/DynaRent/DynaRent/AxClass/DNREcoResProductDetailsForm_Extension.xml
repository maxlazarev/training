<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNREcoResProductDetailsForm_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(EcoResProductDetails))]
public final class DNREcoResProductDetailsForm_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>DNREvent_BOBItemMaintenancePlanDS_OnInitialized</Name>
				<Source><![CDATA[
    // event handler methods
    [FormDataSourceEventHandler(formDataSourceStr(EcoResProductDetails, BOBItemMaintenancePlan), FormDataSourceEventType::Initialized)]
    public static void DNREvent_BOBItemMaintenancePlanDS_OnInitialized(FormDataSource _sender, FormDataSourceEventArgs _eventArgs)
    {
        var overrides = DNREcoResProductDetailsFormExtensionOverrides::construct();
        _sender.object(fieldNum(BOBItemMaintenancePlan, PlanCode)).registerOverrideMethod(methodStr(FormDataObject, lookup),
            methodStr(DNREcoResProductDetailsFormExtensionOverrides, BOBItemMaintenancePlanDS_PlanCode_onLookup), overrides);
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_EcoResProduct_OnActivated</Name>
				<Source><![CDATA[
    [FormDataSourceEventHandler(formDataSourceStr(EcoResProductDetails, EcoResProduct), FormDataSourceEventType::Activated)]
    public static void DNREvent_EcoResProduct_OnActivated(FormDataSource _sender, FormDataSourceEventArgs _eventArgs)
    {
        EcoResProduct   ecoResProduct = _sender.cursor();
        InventTable     inventTable;
        BOBRentalType   rentalType;
        FormRun         formRun = _sender.formRun();
        FormDataObject  rentalTypeId; 
        FormFunctionButtonControl productNumberRenameButton;
        
        rentalTypeId = _sender.object(fieldNum(EcoResProduct, BOBRentalTypeId));
        productNumberRenameButton = formRun.design().controlName(formControlStr(EcoResProductDetails, EcoResProductNumberRename));

        rentalTypeId.allowEdit(ecoResProduct.BOBMayRentalTypeBeModified());
        productNumberRenameButton.enabled(true);
        
        if(ecoResProduct.BOBRentalTypeId && BOBRentalType::exist(ecoResProduct.DisplayProductNumber))
        {
            rentalTypeId.allowEdit(false);
            productNumberRenameButton.enabled(false);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_AddPlan_OnClicked</Name>
				<Source><![CDATA[
    [FormControlEventHandler(formControlStr(EcoResProductDetails, AddPlan), FormControlEventType::Clicked)]
    public static void DNREvent_AddPlan_OnClicked(FormControl _sender, FormControlEventArgs _eventArgs)
    {
        FormRun formRun = _sender.formRun();
        
        FormDataSource BOBItemMaintenancePlanDs = formRun.dataSource(formDataSourceStr(EcoResProductDetails, BOBItemMaintenancePlan));
        BOBItemMaintenancePlanDs.create();

        formRun.design().controlName(formControlStr(EcoResProductDetails, GridSelectedPlans_PlanCode)).setFocus();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>