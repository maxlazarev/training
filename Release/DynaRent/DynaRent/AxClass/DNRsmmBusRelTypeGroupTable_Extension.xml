<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRsmmBusRelTypeGroupTable_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(smmBusRelTypeGroup))]
public final class DNRsmmBusRelTypeGroupTable_Extension
{
    // chain-of-command wrapper methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insert</Name>
				<Source><![CDATA[
    public void insert()
    {
        // Only one record of type IRMCustVend is allowed, after the example of standard Ax.
        if(this.TableType == smmBusRelTypeSourceTable::IRMCustVend && smmBusRelTypeGroup::IRMexistCustomerVendor())
        {
            // Cannot create additional records of type customer/vendor.
            error("@DNR5920");
        }
        next insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateDelete</Name>
				<Source><![CDATA[
    public boolean validateDelete()
    {
        boolean ret = next validateDelete();

        if(this.TableType == smmBusRelTypeSourceTable::IRMCustVend)
        {
            error("@DNR5919");
            ret = false;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertCustomerVendor</Name>
				<Source><![CDATA[
    public static void insertCustomerVendor()
    {
        smmBusRelTypeGroup smmBusRelTypeGroup = null;

        ttsbegin;

        next insertCustomerVendor();

        // If type for customer/vendor does not exist, it needs to be inserted.
        if(!smmBusRelTypeGroup::IRMexistCustomerVendor())
        {
            smmBusRelTypeGroup.clear();
            smmBusRelTypeGroup.TypeId = "@SYS22898"; // Customer/Vendor
            smmBusRelTypeGroup.TypeDescription = "@SYS22898"; // Customer/Vendor
            smmBusRelTypeGroup.TableType = smmBusRelTypeSourceTable::IRMCustVend;
            smmBusRelTypeGroup.insert();
        }

        ttscommit;

    }

]]></Source>
			</Method>
			<Method>
				<Name>IRMexistCustomerVendor</Name>
				<Source><![CDATA[
    // extension methods

    public static boolean IRMexistCustomerVendor()
    {
        return (select firstonly RecId from smmBusRelTypeGroup
                where smmBusRelTypeGroup.TableType == smmBusRelTypeSourceTable::IRMCustVend).RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>IRMGetCustVendType</Name>
				<Source><![CDATA[
    public static SmmBusRelTypeId IRMGetCustVendType(boolean _throwError = true)
    {
        SmmBusRelTypeGroup smmBusRelTypeGroup;
    
        // It is only allowed to create one record of a certain TableType
        // therefore it is not a problem to select the firstonly in this case
        select firstonly @TypeId from smmBusRelTypeGroup
        where smmBusRelTypeGroup.TableType == SmmBusRelTypeSourceTable::IRMCustVend;
    
        if (_throwError && !smmBusRelTypeGroup.TypeId)
        {
            throw error("@DNR5918");
        }
    
        return smmBusRelTypeGroup.TypeId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>