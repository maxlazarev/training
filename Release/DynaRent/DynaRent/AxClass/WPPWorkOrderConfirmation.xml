<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WPPWorkOrderConfirmation</Name>
	<SourceCode>
		<Declaration><![CDATA[
class WPPWorkOrderConfirmation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        WPPWorkOrderTable   workOrderTable;
        WPPWorkOrderLine    workOrderLine;

        if (_args)
        {
            switch (_args.dataset())
            {
                case tableNum(WPPWorkOrderView):
                    WPPWorkOrderView workOrderView = _args.record();
                    workOrderTable = WPPWorkOrderTable::find(workOrderView.WorkOrderId);
                    break;

                case tableNum(WPPWorkOrderLineListPageView):
                    WPPWorkOrderLineListPageView workOrderLineListPageView = _args.record();
                    workOrderTable = WPPWorkOrderTable::find(workOrderLineListPageView.WorkOrderId);
                    break;

                case tableNum(WPPWorkOrderTable):
                    workOrderTable = _args.record();
                    break;

                case tableNum(WPPWorkOrderLine):
                    workOrderLine = _args.record();
                    workOrderTable = workOrderLine.workOrderTable();
                    break;
            }
        }

        if (!workOrderTable)
        {
            throw error(strFmt("@SYS134996", tableStr(WPPWorkOrderTable)));
        }

        SRSReportRun reportRun = new SRSReportRun();

        reportRun.reportName(ssrsReportStr(WPPWorkOrderConfirmation, WorkOrderConfirmation));
        reportRun.readPrintDestinationSettings();

        SRSPrintDestinationSettings printDestinationSettings = reportRun.printDestinationSettings();

        if (SrsReportRunUtil::showSettingsDialog(printDestinationSettings))
        {
            reportRun.printDestinationSettings(printDestinationSettings);
            reportRun.savePrintDestinationSettings();

            DataArea dataArea;

            select firstOnly Id from dataArea
            where dataArea.id == workOrderTable.CompanyId
            &&    dataArea.isVirtual == NoYes::No;

            changeCompany(dataArea ? dataArea.id : curext())
            {
                SrsReportRunController controller = new SrsReportRunController();
                controller.parmArgs(_args);
                controller.parmReportName(reportRun.reportName());
                controller.parmShowDialog(false);

                SrsReportDataContract contract = controller.parmReportContract();
                contract.parmReportCaption("@DNR6789");
                contract.parmPrintSettings(printDestinationSettings);

                WPPWorkOrderHeaderContract dataContract = contract.parmRdpContract();
                dataContract.parmWorkOrderId(workOrderTable.WorkOrderId);

                if (workOrderLine)
                {
                    dataContract.parmLineNumber(workOrderLine.LineNumber);
                }

                controller.runReport();
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>