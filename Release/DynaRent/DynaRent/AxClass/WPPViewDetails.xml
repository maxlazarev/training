<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WPPViewDetails</Name>
	<SourceCode>
		<Declaration><![CDATA[
class WPPViewDetails
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        WPPWorkOrderView                workOrderView;
        WPPWorkOrderTable               workOrderTable;
        WPPWorkOrderLineListPageView    workOrderLineListPageView;
        Args                            args;
        FormName                        formName;
        FormRun                         formRun;
        PurchTable                      purchTable;
        SalesTable                      salesTable;
        PurchLine                       purchLine;
        SalesLine                       salesLine;
        SalesQuotationTable             salesQuotationTable;
        WMSShipment                     shipment;
        WMSOrderTrans                   wmsOrderTrans;
        boolean                         linkedToTransaction;
    
        switch (_args.dataset())
        {
            case tableNum(WPPWorkOrderTable):
                workOrderTable = _args.record();
                break;
    
            case tableNum(WPPWorkOrderView):
                workOrderView = _args.record();
                workOrderTable = WPPWorkOrderTable::find(workOrderView.WorkOrderId);
                break;
    
            case tableNum(WPPWorkOrderLineListPageView):
                workOrderLineListPageView = _args.record();
                workOrderTable = WPPWorkOrderTable::find(workOrderLineListPageView.WorkOrderId);
                break;
    
            case tableNum(SalesQuotationTable):
                args = new args();
                salesQuotationTable = _args.record();
                linkedToTransaction = true;
                formName = formStr(RORSalesQuotationTable);
                args.record(salesQuotationTable);
                break;
    
            default:
                throw error("@DNR6504");
        }
    
        if (!linkedToTransaction)
        {
            args = new args();

            changecompany(workOrderTable.CompanyId)
            {
                switch (workOrderTable.OrderTableId)
                {
                    case tableNum(PurchTable):
                        purchTable = PurchTable::findRecId(workOrderTable.OrderRecId);
                        formName   = purchTable.PurchaseType == PurchaseType::RORSubRental ? formStr(RORPurchTable) : formStr(PurchTable);
    
                        args.record(purchTable);
                        break;
    
                    case tableNum(SalesTable):
                        salesTable = SalesTable::findRecId(workOrderTable.OrderRecId);
                        formName   = salesTable.SalesType == SalesType::RORRental ? formStr(RORSalesTable) : formStr(salesTable);
    
                        args.record(salesTable);
                        break;
    
                    case tableNum(PurchLine):
                        purchLine = PurchLine::findRecId(workOrderTable.OrderRecId);
                        formName  = purchLine.purchTable().PurchaseType == PurchaseType::RORSubRental ? formStr(RORPurchTable) : formStr(PurchTable);
    
                        args.record(purchLine);
                        break;
    
                    case tableNum(SalesLine):
                        salesLine = SalesLine::findRecId(workOrderTable.OrderRecId);
                        formName  = salesLine.salesTable().SalesType == SalesType::RORRental ? formStr(RORSalesTable) : formStr(salesTable);
    
                        args.record(salesLine);
                        break;

                    case tableNum(WHSShipmentTable):
                        formName = WPPViewDetails::getFormNameForShipment(workOrderTable, _args.menuItemName(), args);
                        if (!formName)
                        {
                            throw error(strFmt("@SYS31187", tableId2PName(workOrderTable.OrderTableId)));
                        }
                        break;

                    default:
                        throw error(strFmt("@SYS31187", tableId2PName(workOrderTable.OrderTableId)));
                }
            }
        }
    
        args.name(formName);
    
        formRun = ClassFactory::formRunClassOnClient(args);
        formRun.init();
        formRun.run();
        formRun.wait();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFormNameForShipment</Name>
				<Source><![CDATA[
    public static FormName getFormNameForShipment(WPPWorkOrderTable _workOrderTable, MenuItemName _callerMenuItemName, Args _args)
    {
        FormName ret;
        // Implemented in other model if needed
        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>