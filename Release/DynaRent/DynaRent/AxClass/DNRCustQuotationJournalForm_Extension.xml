<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRCustQuotationJournalForm_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(CustQuotationJournal))]
public final class DNRCustQuotationJournalForm_Extension
{
    private BOBRentalSubStatus rentalSubStatus;

    // chain-of-command wrapper methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createRecordSortedList</Name>
				<Source><![CDATA[
    protected RecordSortedList createRecordSortedList(QuotationType _quotationType)
    {
        RecordSortedList ret = next createRecordSortedList(_quotationType);
        QuotationType additionalQuotationType = (_quotationType == QuotationType::Project ? QuotationType::RORProjRental : QuotationType::RORRental);
        
        // iterate datasource selection again, add addition quotations
        for(CustQuotationJour cursor = getFirstSelection(custQuotationJour_ds); cursor; cursor = custQuotationJour_ds.getNext())
        {
            if(cursor.QuotationType == additionalQuotationType)
            {
                ret.ins(cursor);
            }
        }
               
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>RORUpdateDesign</Name>
				<Source><![CDATA[
    // extension methods

    public void RORUpdateDesign()
    {
        QuotationType quotationType = CustQuotationJour.salesQuotationTable().QuotationType;
        boolean isRentalQuotation = (quotationType == QuotationType::RORRental || quotationType == QuotationType::RORProjRental);
        boolean hasRentalDates = (isConfigurationkeyEnabled(ConfigurationKeyNum(RORRentalDateTimes)) && isRentalQuotation);
            
        TabHeaderRentalDates.visible(hasRentalDates);
        TabLineRentalDates.visible(hasRentalDates);

        if (isRentalQuotation)
        {
            if (salesQuotationTableRef.QuotationId)
            {
                header.text(strFmt("@SYS76498",
                                   (quotationType == QuotationType::RORRental) ? "@DNR5855" : "@DNR1999",
                                   salesQuotationTableRef.QuotationId));
            }

            FormDesign formDesign = this.design();

            if (formDesign)
            {
                FormStringControl CustQuotationTrans_origSalesId = formDesign.controlName(formControlStr(CustQuotationJournal, CustQuotationTrans_origSalesId)) as FormStringControl;
                CustQuotationTrans_origSalesId.label("@DNR10");
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_element_OnPostRun</Name>
				<Source><![CDATA[
    // event handler methods

    [FormEventHandler(formStr(CustQuotationJournal), FormEventType::PostRun)]
    public static void DNREvent_element_OnPostRun(xFormRun _sender, FormEventArgs _eventArgs)
    {
        FormRun formRun         = _sender as FormRun;
        Object  formRunObject   = formRun as Object;

        formRunObject.RORUpdateDesign();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>