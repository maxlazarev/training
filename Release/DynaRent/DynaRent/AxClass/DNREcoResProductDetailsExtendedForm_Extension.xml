<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNREcoResProductDetailsExtendedForm_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(EcoResProductDetailsExtended))]
public final class DNREcoResProductDetailsExtendedForm_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    public void init()
    {
        next init();

        FINDimValueController::setEditableDimensionSet(tableNum(InventTable), this.design().controlName(formControlStr(EcoResProductDetailsExtended, DimensionEntryControl)));
    }

]]></Source>
			</Method>
			<Method>
				<Name>BOBInitDesign</Name>
				<Source><![CDATA[
    public void BOBInitDesign()
    {
        boolean allowEditAssetFields = AssetParameters::find().AssetAllowAcqPostingPO;
    
        InventTable_ds.object(fieldNum(InventTable, BOBAutoAssetConvertPurch)).allowEdit(allowEditAssetFields);
        InventTable_ds.object(fieldNum(InventTable, BOBAssetGroupId)).allowEdit(allowEditAssetFields);
    }

]]></Source>
			</Method>
			<Method>
				<Name>WPPsetUseAssociatedDeliveryEnabled</Name>
				<Source><![CDATA[
    /// <summary>
    /// Enables or disables the button based upon the Rental agreement items.
    /// </summary>
    public void WPPsetUseAssociatedDeliveryEnabled()
    {
        RAGParameters   parameters;
        boolean         enableAssociatedDelivery;
    
        parameters = RAGParameters::find();
        enableAssociatedDelivery = (InventTable.ItemId != parameters.ItemIdInsurance        &&
                                    InventTable.ItemId != parameters.ItemIdTransport        &&
                                    InventTable.ItemId != parameters.ItemIdReturnTransport  &&
                                    InventTable.ItemId != parameters.ItemIdWarranty);
    
        WPPAssociatedDelivery.enabled(enableAssociatedDelivery);
    }

]]></Source>
			</Method>
			<Method>
				<Name>inventTableDS_RORProjCategoryIdRental_OnLookup</Name>
				<Source><![CDATA[
    public void inventTableDS_RORProjCategoryIdRental_OnLookup(FormControl _formControl)
    {
        ProjCategory::lookupProjCategoryType(_formControl, ProjCategoryType::Hour);
    }

]]></Source>
			</Method>
			<Method>
				<Name>inventTableDS_RORProjCategoryIdSubRental_OnLookup</Name>
				<Source><![CDATA[
    public void inventTableDS_RORProjCategoryIdSubRental_OnLookup(FormControl _formControl)
    {
        ProjCategory::lookupProjCategoryType(_formControl, ProjCategoryType::Hour);
    }

]]></Source>
			</Method>
			<Method>
				<Name>inventTableDS_RORProjCategoryIdRevenueSubRental_OnLookup</Name>
				<Source><![CDATA[
    [SysObsolete("Field RORProjCategoryIdRevenueSubRental has been made obsolete", false)]
    public void inventTableDS_RORProjCategoryIdRevenueSubRental_OnLookup(FormControl _formControl)
    {
        ProjCategory::lookupProjCategoryType(_formControl, ProjCategoryType::Cost);
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_element_OnInitialized</Name>
				<Source><![CDATA[
    [FormEventHandler(formStr(EcoResProductDetailsExtended), FormEventType::Initialized)]
    public static void DNREvent_element_OnInitialized(xFormRun _sender, FormEventArgs _eventArgs)
    {
        Object formRunObject = _sender as Object;

        formRunObject.BOBInitDesign();
        formRunObject.WPPsetUseAssociatedDeliveryEnabled();
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_InventTableDS_OnActivated</Name>
				<Source><![CDATA[
    [FormDataSourceEventHandler(formDataSourceStr(EcoResProductDetailsExtended, InventTable), FormDataSourceEventType::Activated)]
    public static void DNREvent_InventTableDS_OnActivated(FormDataSource _sender, FormDataSourceEventArgs _eventArgs)
    {
        Object formRunObject = _sender.formRun() as Object;
        InventTable inventTable = _sender.cursor();
        BOBRentalType rentalType = inventTable.BOBRentalType();

        _sender.object(fieldNum(InventTable, RORBasePriceOnDistance)).allowEdit(!rentalType);
        _sender.object(fieldNum(InventTable, RORProjCategoryIdRental)).allowEdit(inventTable.projCategoryId != '');
        _sender.object(fieldNum(InventTable, RORProjCategoryIdSubRental)).allowEdit(inventTable.projCategoryId != '');
        
        _sender.object(fieldNum(InventTable, BOBAssetGroupId)).mandatory(rentalType && rentalType.Classification == BOBRentalTypeClassification::Bulk);
            
        formRunObject.WPPsetUseAssociatedDeliveryEnabled();
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_InventTableDS_OnInitialized</Name>
				<Source><![CDATA[
    [FormDataSourceEventHandler(formDataSourceStr(EcoResProductDetailsExtended, InventTable), FormDataSourceEventType::Initialized)]
    public static void DNREvent_InventTableDS_OnInitialized(FormDataSource _sender, FormDataSourceEventArgs _eventArgs)
    {
        var overrides = _sender.formRun() as FormRun;

        _sender.object(fieldNum(InventTable, RORProjCategoryIdRental)).registerOverrideMethod(methodStr(FormDataObject, lookup),
            methodStr(DNREcoResProductDetailsExtendedForm_Extension, inventTableDS_RORProjCategoryIdRental_OnLookup), overrides);

        _sender.object(fieldNum(InventTable, RORProjCategoryIdSubRental)).registerOverrideMethod(methodStr(FormDataObject, lookup),
            methodStr(DNREcoResProductDetailsExtendedForm_Extension, inventTableDS_RORProjCategoryIdSubRental_OnLookup), overrides);

    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>