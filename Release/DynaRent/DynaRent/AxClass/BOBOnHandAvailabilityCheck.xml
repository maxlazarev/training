<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BOBOnHandAvailabilityCheck</Name>
	<SourceCode>
		<Declaration><![CDATA[
class BOBOnHandAvailabilityCheck
{
    SalesTable salesTable;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new(salesTable _salesTable)
    {
        salesTable = _salesTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run()
    {
        SalesLine               salesLine;
        SysInfoAction_Formrun   sysInfoAction;
        RORRentalDateTimes      rentalDateTimes;
        InventDim               inventDim;
        ToDateTime              untilDateTime;
        SysInfoLogStr           message;
        Timezone                userPreferredTimeZone;
        ;
    
        sysInfoAction = SysInfoAction_Formrun::newFormname(formStr(BOBTmpRentalSum));
        userPreferredTimeZone = DateTimeUtil::getUserPreferredTimeZone();
    
        setPrefix("@DNR7126");
    
        while select salesLine
        where salesLine.SalesId == salesTable.SalesId
            join  rentalDateTimes
            where rentalDateTimes.RefTableId == salesLine.TableId
            &&    rentalDateTimes.RefRecId   == salesLine.RecId
                join InventLocationId from inventDim
                where inventDim.inventDimId == salesLine.InventDimId
        {
            sysInfoAction.parmCallerBuffer(salesLine);
    
            untilDateTime = (rentalDateTimes.OffRentDateTime >= 1900-01-02T00:00:00 ?
                             DateTimeUtil::applyTimeZoneOffset(rentalDateTimes.OffRentDateTime, userPreferredTimeZone) :
                             DateTimeUtil::applyTimeZoneOffset(rentalDateTimes.ExpectedOffRentDateTime, userPreferredTimeZone));
    
            if(new BOBRentalInventOnHand().displayIssueCheck(salesLine, salesLine.QtyOrdered, rentalDateTimes))
            {
                message = strFmt("@DNR7127",
                                 salesLine.ItemId,
                                 salesLine.LineNum,
                                 inventDim.InventLocationId,
                                 strLwr("@SYS82323"),
                                 DateTimeUtil::applyTimeZoneOffset(rentalDateTimes.OnRentDateTime, userPreferredTimeZone),
                                 untilDateTime);
    
                info(message, '', sysInfoAction);
            }
            else
            {
                message = strFmt("@DNR7127",
                                 salesLine.ItemId,
                                 salesLine.LineNum,
                                 inventDim.InventLocationId,
                                 strLwr("@SYS82324"),
                                 DateTimeUtil::applyTimeZoneOffset(rentalDateTimes.OnRentDateTime, userPreferredTimeZone),
                                 untilDateTime);
    
                error(message, '', sysInfoAction);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        if(_args.dataset() == tableNum(SalesTable))
        {
            new BOBOnHandAvailabilityCheck(_args.record()).run();
        }
        else
        {
            throw error(Error::missingRecord(tableId2pname(tableNum(SalesTable))));
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>