<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PersonSearch_DNRJumpRefHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class <c>PersonSearch_DNRJumpRefHelper</c> provides modified reference functionality for DynaRent records in a person search.
/// </summary>
public class PersonSearch_DNRJumpRefHelper
{
    protected FormRun callingForm;
    protected boolean shouldDisplayDialog;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static PersonSearch_DNRJumpRefHelper construct(FormRun _callingForm, boolean _shouldDisplayDialog = false)
    {
        PersonSearch_DNRJumpRefHelper jumpRefHelper = new PersonSearch_DNRJumpRefHelper();

        jumpRefHelper.parmCallingForm(_callingForm);
        jumpRefHelper.parmShouldDisplayDialog(_shouldDisplayDialog);

        return jumpRefHelper;
    }

]]></Source>
			</Method>
			<Method>
				<Name>jumpRef</Name>
				<Source><![CDATA[
    public void jumpRef(FormDataObject _formDataObject)
    {
        FormDataSource formDataSource = _formDataObject.datasource();

        if (formDataSource && formDataSource.cursor())
        {
            Common formDataSourceRecord = formDataSource.cursor();

            switch (formDataSourceRecord.TableId)
            {
                case tableNum(PersonSearchResultWPPResourceView):

                    this.jumpRef_WPPResource(formDataSourceRecord);

                    break;

                case tableNum(PersonSearchResultRORVenueView):

                    this.jumpRef_RORVenue(formDataSourceRecord);

                    break;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>jumpRef_WPPResource</Name>
				<Source><![CDATA[
    private void jumpRef_WPPResource(Common _formDataSourceRecord)
    {
        WPPResourceTable resourceTable;

        PersonSearchResultWPPResourceView resourceView = _formDataSourceRecord as PersonSearchResultWPPResourceView;

        select firstonly resourceTable
        where resourceTable.RecId == resourceView.ResultRecId;

        this.executeJumpRef(menuItemDisplayStr(WPPResource), MenuItemType::Display, '', resourceTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>jumpRef_RORVenue</Name>
				<Source><![CDATA[
    private void jumpRef_RORVenue(Common _formDataSourceRecord)
    {
        RORVenueTable venueTable;

        PersonSearchResultRORVenueView venueView = _formDataSourceRecord as PersonSearchResultRORVenueView;

        select firstonly venueTable
        where venueTable.RecId == venueView.ResultRecId;

        this.executeJumpRef(menuItemDisplayStr(RORVenueTable), MenuItemType::Display, venueTable.DataAreaId, venueTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>executeJumpRef</Name>
				<Source><![CDATA[
    private void executeJumpRef(str _menuItemName, MenuItemType _menuItemType, DataAreaId _dataAreaId, Common _lookupRecord)
    {
        if (_dataAreaId && xDataArea::exist(_dataAreaId) && _dataAreaId != curExt())
        {
            appl.setDefaultCompany(_dataAreaId, shouldDisplayDialog);
        }

        if (_menuItemName && _lookupRecord)
        {
            MenuFunction menuFunction = new MenuFunction(_menuItemName, _menuItemType);

            Args args = new Args();

            args.lookupRecord(_lookupRecord);
            args.caller(callingForm);

            if (menuFunction)
            {
                menuFunction.run(args);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCallingForm</Name>
				<Source><![CDATA[
    public FormRun parmCallingForm(FormRun _callingForm = callingForm)
    {
        callingForm = _callingForm;

        return callingForm;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmShouldDisplayDialog</Name>
				<Source><![CDATA[
    public boolean parmShouldDisplayDialog(boolean _shouldDisplayDialog = shouldDisplayDialog)
    {
        shouldDisplayDialog = _shouldDisplayDialog;

        return shouldDisplayDialog;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>