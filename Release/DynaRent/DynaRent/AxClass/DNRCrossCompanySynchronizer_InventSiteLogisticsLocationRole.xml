<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRCrossCompanySynchronizer_InventSiteLogisticsLocationRole</Name>
	<SourceCode>
		<Declaration><![CDATA[
class DNRCrossCompanySynchronizer_InventSiteLogisticsLocationRole extends DNRCrossCompanySynchronizer
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getFieldIds2Identify</Name>
				<Source><![CDATA[
    protected Set getFieldIds2Identify()
    {
        return null; 
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFieldIds2Sync</Name>
				<Source><![CDATA[
    protected Set getFieldIds2Sync()
    {
        return null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>eventInserted</Name>
				<Source><![CDATA[
    protected void eventInserted()
    {
        DataAreaId                      dataAreaId;
        InventSiteId                    origSiteId;
        RefRecId                        origLocation;
        InventSiteLogisticsLocationRole locationRole = callerRecord;
        SetEnumerator                   enumerator = dataAreaIds2Sync.getEnumerator();

        changecompany(transDataAreaId)
        {
            InventSite                      inventSiteOrig;
            InventSiteLogisticsLocation     origLogisticsLocation;
            
            select origLogisticsLocation
            where origLogisticsLocation.RecId == locationRole.SiteLogisticsLocation
                join inventSiteOrig
                where inventSiteOrig.RecId == origLogisticsLocation.Site;

            origSiteId = inventSiteOrig.SiteId;
            origLocation = origLogisticsLocation.Location;

        }
        
        while(enumerator.moveNext())
        {
            dataAreaId = enumerator.current();
            changecompany(dataAreaId)
            {
                InventSite                      inventSite;
                InventSiteLogisticsLocation     logisticsLocation;
                InventSiteLogisticsLocationRole newRecord;
                InventSiteLogisticsLocationRole locationRoleNotExists;

                inventSite = InventSite::find(origSiteId);
                
                select logisticsLocation
                where logisticsLocation.Location == origLocation
                &&    logisticsLocation.Site == inventSite.RecId
                    notexists join locationRoleNotExists            //Find Location only if the related Role is already not available
                    where locationRoleNotExists.LocationRole == locationRole.LocationRole
                    &&    locationRoleNotExists.SiteLogisticsLocation == logisticsLocation.RecId;

                newRecord.clear();
                newRecord.LocationRole = locationRole.LocationRole;
                newRecord.SiteLogisticsLocation = logisticsLocation.RecId;
                
                if(logisticsLocation && this.validateWrite(newRecord))
                {
                    newRecord.insert();
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>eventDeleted</Name>
				<Source><![CDATA[
    protected void eventDeleted()
    {
        DataAreaId                      dataAreaId;
        InventSiteId                    origSiteId;
        RefRecId                        origLocation;
        InventSiteLogisticsLocationRole locationRole = callerRecord;
        SetEnumerator                   enumerator = dataAreaIds2Sync.getEnumerator();

        changecompany(transDataAreaId)
        {
            InventSite                      inventSiteOrig;
            InventSiteLogisticsLocation     origLogisticsLocation;
            
            select origLogisticsLocation
            where origLogisticsLocation.RecId == locationRole.SiteLogisticsLocation
                join inventSiteOrig
                where inventSiteOrig.RecId == origLogisticsLocation.Site;

            origSiteId = inventSiteOrig.SiteId;
            origLocation = origLogisticsLocation.Location;

        }
        
        ttsbegin;
        while(enumerator.moveNext())
        {
            dataAreaId = enumerator.current();
            changecompany(dataAreaId)
            {
                InventSiteLogisticsLocation     logisticsLocation;
                InventSite                      inventSite = InventSite::find(origSiteId);
                
                if(inventSite)
                {
                    select logisticsLocation
                    where logisticsLocation.Location == origLocation
                    &&    logisticsLocation.Site == inventSite.RecId;

                    if(logisticsLocation)
                    {
                        InventSiteLogisticsLocationRole locationRoleDel;
                        
                        select forupdate locationRoleDel
                        where locationRoleDel.LocationRole == locationRole.LocationRole
                        &&    locationRoleDel.SiteLogisticsLocation == logisticsLocation.RecId;
                
                        if(locationRoleDel && locationRoleDel.validateDelete())
                        {
                            locationRoleDel.delete();
                        }
                    }
                }
            }
        }
        ttscommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>