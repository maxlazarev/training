<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRExtCodeTableForm_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(ExtCodeTable))]
public final class DNRExtCodeTableForm_Extension
{
    private Common callerCommon;

    // extension methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>DNRparmCallerCommon</Name>
				<Source><![CDATA[
    public Common DNRparmCallerCommon(Common _callerCommon = callerCommon)
    {
        callerCommon = _callerCommon;
        return callerCommon;
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_element_OnInitializing</Name>
				<Source><![CDATA[
    // event handler methods

    [FormEventHandler(formStr(ExtCodeTable), FormEventType::Initializing)]
    public static void DNREvent_element_OnInitializing(xFormRun _sender, FormEventArgs _eventArgs)
    {
        FormRun formRun         = _sender as FormRun;
        Object  formRunObject   = formRun as Object;

        if (formRun.args().dataset())
        {
            formRunObject.DNRparmCallerCommon(formRun.args().record());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_ExtCodeTableDS_OnInitValue</Name>
				<Source><![CDATA[
    [FormDataSourceEventHandler(formDataSourceStr(ExtCodeTable, ExtCodeTable), FormDataSourceEventType::InitValue)]
    public static void DNREvent_ExtCodeTableDS_OnInitValue(FormDataSource _sender, FormDataSourceEventArgs _eventArgs)
    {
        FormRun         formRun         = _sender.formRun() as FormRun;
        ExtCodeTable    extCodeTable    = _sender.cursor() as ExtCodeTable;

        extCodeTable.RORinitFromCommon(formRun.args().record(), formRun.args().menuItemName() == menuItemDisplayStr(ExtCodeValueTable) ? ExtCodeSubModule::Cust : ExtCodeSubModule::Vend);
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_ExtCodeTableDS_OnInitialized</Name>
				<Source><![CDATA[
    [FormDataSourceEventHandler(formDataSourceStr(ExtCodeTable, ExtCodeTable), FormDataSourceEventType::Initialized)]
    public static void DNREvent_ExtCodeTableDS_OnInitialized(FormDataSource _sender, FormDataSourceEventArgs _eventArgs)
    {
        FormRun                 formRun         = _sender.formRun() as FormRun;
        Object                  formRunObject   = formRun as Object;
        Common                  callerCommon    = formRunObject.DNRparmCallerCommon();
        QueryBuildDataSource    qbdsExtCodeTable;
        QueryBuildDataSource    qbdsLink;

        
        if (callerCommon.TableId == tableNum(smmBusRelTable))
        {
            qbdsExtCodeTable = _sender.query().dataSourceTable(tableNum(ExtCodeTable));
            qbdsExtCodeTable.clearDynalinks();
        
            switch (formRun.args().menuItemName())
            {
                case menuItemDisplayStr(ExtCodeValueTable):
                    qbdsLink = qbdsExtCodeTable.addDataSource(tableNum(CustTable));
                    qbdsLink.addLink(fieldNum(ExtCodeTable, ExtCodeTableId), fieldNum(CustTable, TableId));
                    qbdsLink.addDynalink(fieldNum(CustTable, Party),
                                         formRun.args().record(),
                                         fieldNum(smmBusRelTable, Party));
                    break;
        
                case menuItemDisplayStr(ExtCodeValueTableAction):
                    qbdsLink = qbdsExtCodeTable.addDataSource(tableNum(VendTable));
                    qbdsLink.addLink(fieldNum(ExtCodeTable, ExtCodeTableId), fieldNum(VendTable, TableId));
                    qbdsLink.addDynalink(fieldNum(VendTable, Party),
                                                  formRun.args().record(),
                                                  fieldNum(smmBusRelTable, Party));
                    break;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_ExtCodeValueTableDS_OnInitValue</Name>
				<Source><![CDATA[
    [FormDataSourceEventHandler(formDataSourceStr(ExtCodeTable, ExtCodeValueTable), FormDataSourceEventType::InitValue)]
    public static void DNREvent_ExtCodeValueTableDS_OnInitValue(FormDataSource _sender, FormDataSourceEventArgs _eventArgs)
    {
        FormRun             formRun             = _sender.formRun() as FormRun;
        ExtCodeValueTable   extCodeValueTable   = _sender.cursor() as ExtCodeValueTable;

        if (formRun.args().record().TableId == tableNum(smmBusRelTable))
        {
            extCodeValueTable.ExtCodeSubModule = formRun.args().menuItemName() == menuItemDisplayStr(ExtCodeValueTable) ? ExtCodeSubModule::Cust : ExtCodeSubModule::Vend;
        }

        extCodeValueTable.initFromCommon(formRun.args().record());
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>