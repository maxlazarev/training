<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BOBDelayedMaintenanceForm</Name>
	<SourceCode>
		<Declaration><![CDATA[
class BOBDelayedMaintenanceForm
{
    BOBObjectTableRecId objectTableRecId;
    ProjGroupId         projGroupId;
    WPPWorkOrderId      workOrderId;
    InventSerialId      inventSerialId;
    AssetId             assetId;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>deleteObsoleteRecords</Name>
				<Source><![CDATA[
    [SuppressBPWarningAttribute("BPCheckSkipStatementValidation", "Cannot use skip method without altering functional behavior")]
    private void deleteObsoleteRecords(WPPWorkOrderId _workOrderId, Line _lineNumber, ProjId _projId)
    {
        WPPWorkOrderTable   workOrderTable;
        WPPWorkOrderLine    workOrderLine;
        InventJournalTable  inventJournalTable;
        InventJournalTrans  inventJournalTrans;
        ProjTable           projTable;
    
        // When multiple lines exist then only delete the line that is moved
        if (WPPWorkOrderTable::numberOfLines(_workOrderId) > 1)
        {
            delete_from workOrderLine
            where workOrderLine.WorkOrderId == _workOrderId
            &&    workOrderLine.LineNumber  == _lineNumber;
        }
        // Otherwise delete the whole work order
        else
        {
            delete_from workOrderTable
            where workOrderTable.WorkOrderId == _workOrderId;
        }
    
        if (_projId)
        {
            delete_from inventJournalTrans
            where inventJournalTrans.ProjId == _projId;
    
            delete_from inventJournalTable
            where inventJournalTable.WPPProjId == _projId;
    
            delete_from projTable
            where projTable.ProjId == _projId;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillTmpWorkOrderLine</Name>
				<Source><![CDATA[
    public WPPWorkOrderLine fillTmpWorkOrderLine()
    {
        WPPWorkOrderLine        workOrderLineSource, tmpWorkOrderLine;
        WPPWorkOrderTable       workOrderTable;
        WPPWorkOrderTaskTable   workOrderTaskTable;
    
        // BP Deviation documented
        tmpWorkOrderLine.setTmp();
    
        while select workOrderLineSource
        where workOrderLineSource.Status == WPPWorkOrderLineStatus::Created
        &&    workOrderLineSource.RequestedStartDateTime < DateTimeUtil::getSystemDateTime()
            exists join workOrderTable
            where workOrderTable.WorkOrderId == workOrderLineSource.WorkOrderId
            &&    (!projGroupId || workOrderTable.ProjGroupId == projGroupId)
                join TableId from workOrderTaskTable
                where workOrderTaskTable.TaskId == workOrderLineSource.TaskId
                &&    workOrderTaskTable.TaskCategory == WPPTaskCategory::MaintenanceTask
        {
            if (!workOrderLineSource.transactionsExist() || !workOrderLineSource.postedItemTransactionExists())
            {
                tmpWorkOrderLine.data(workOrderLineSource);
                // doInsert; in the insert method the work order is adjusted
                // when a work order line is inserted
                tmpWorkOrderLine.doInsert();
            }
        }
    
        return tmpWorkOrderLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillTmpWorkOrderLine_ObjectTableRecId</Name>
				<Source><![CDATA[
    public WPPWorkOrderLine fillTmpWorkOrderLine_ObjectTableRecId()
    {
        WPPWorkOrderLine        workOrderLineSource, tmpWorkOrderLine;
        WPPWorkOrderTable       workOrderTable;
        WPPWorkOrderTaskTable   workOrderTaskTable;
    
        // BP Deviation documented
        tmpWorkOrderLine.setTmp();
    
        if (objectTableRecId)
        {
            while select workOrderLineSource
            where workOrderLineSource.Status == WPPWorkOrderLineStatus::Created
            &&    workOrderLineSource.RequestedStartDateTime < DateTimeUtil::getSystemDateTime()
                exists join workOrderTable
                where workOrderTable.WorkOrderId == workOrderLineSource.WorkOrderId
                &&    (!objectTableRecId ||  workOrderTable.BOBObjectTableRecId == objectTableRecId)
                &&    (!projGroupId || workOrderTable.ProjGroupId == projGroupId)
                    join TableId from workOrderTaskTable
                    where workOrderTaskTable.TaskId == workOrderLineSource.TaskId
                    &&    workOrderTaskTable.TaskCategory == WPPTaskCategory::MaintenanceTask
            {
                if (!workOrderLineSource.transactionsExist() || !workOrderLineSource.postedItemTransactionExists())
                {
                    tmpWorkOrderLine.data(workOrderLineSource);
                    // doInsert; in the insert method the work order is adjusted
                    // when a work order line is inserted
                    tmpWorkOrderLine.doInsert();
                }
            }
        }
    
        return tmpWorkOrderLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillTmpWorkOrderLine_WorkOrderId</Name>
				<Source><![CDATA[
    public WPPWorkOrderLine fillTmpWorkOrderLine_WorkOrderId(WPPWorkOrderId _workOrderId)
    {
        WPPWorkOrderLine        workOrderLineSource, tmpWorkOrderLine;
        WPPWorkOrderTable       workOrderTable;
        WPPWorkOrderTaskTable   workOrderTaskTable;
    
        // BP Deviation documented
        tmpWorkOrderLine.setTmp();
    
        if (objectTableRecId)
        {
            while select workOrderLineSource
            where workOrderLineSource.Status      == WPPWorkOrderLineStatus::Created
            &&    workOrderLineSource.WorkOrderId != _workOrderId
            &&    workOrderLineSource.RequestedStartDateTime < DateTimeUtil::getSystemDateTime()
                exists join workOrderTable
                where workOrderTable.WorkOrderId         == workOrderLineSource.WorkOrderId
                &&    workOrderTable.BOBObjectTableRecId == objectTableRecId
                &&    (!projGroupId || workOrderTable.ProjGroupId == projGroupId)
                    join TableId from workOrderTaskTable
                    where workOrderTaskTable.TaskId == workOrderLineSource.TaskId
                    &&    workOrderTaskTable.TaskCategory == WPPTaskCategory::MaintenanceTask
            {
                if (!workOrderLineSource.transactionsExist())
                //||  !workOrderLineSource.postedItemTransactionExists())
                {
                    tmpWorkOrderLine.data(workOrderLineSource);
                    // doInsert; in the insert method the work order is adjusted
                    // when a work order line is inserted
                    tmpWorkOrderLine.doInsert();
                }
            }
        }
    
        return tmpWorkOrderLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>mergeAdditionalNotes_Header</Name>
				<Source><![CDATA[
    private void mergeAdditionalNotes_Header(WPPWorkOrderId _workOrderId, RefRecId _refRecId)
    {
        DNRAdditionalNoteTable  additionalNoteTableSource;
        WPPWorkOrderTable       workOrderTable;
    
        select firstOnly OrderNote, InternalNote, InvoiceNote from additionalNoteTableSource
            exists join workOrderTable
            where workOrderTable.WorkOrderId == _workOrderId
            &&    workOrderTable.TableId     == additionalNoteTableSource.RefTableId
            &&    workOrderTable.RecId       == additionalNoteTableSource.RefRecId;
    
        DNRAdditionalNoteTable additionalNoteTableTarget = DNRAdditionalNoteTable::find(tableNum(WPPWorkOrderTable), _refRecId, true);
    
        additionalNoteTableTarget.RefTableId   = tableNum(WPPWorkOrderTable);
        additionalNoteTableTarget.RefRecId     = _refRecId;
        additionalNoteTableTarget.OrderNote    += additionalNoteTableTarget.OrderNote    ? '\n' + additionalNoteTableSource.OrderNote    : additionalNoteTableSource.OrderNote;
        additionalNoteTableTarget.InternalNote += additionalNoteTableTarget.InternalNote ? '\n' + additionalNoteTableSource.InternalNote : additionalNoteTableSource.InternalNote;
        additionalNoteTableTarget.InvoiceNote  += additionalNoteTableTarget.InvoiceNote  ? '\n' + additionalNoteTableSource.InvoiceNote  : additionalNoteTableSource.InvoiceNote;
    
        if (additionalNoteTableTarget)
        {
            additionalNoteTableTarget.update();
        }
        else
        {
            additionalNoteTableTarget.insert();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>mergeAdditionalNotes_Lines</Name>
				<Source><![CDATA[
    private void mergeAdditionalNotes_Lines(WPPWorkOrderId _workOrderId, Line _lineNumber, RefRecId _refRecId)
    {
        DNRAdditionalNoteTable  additionalNoteTableSource, additionalNoteTableTarget;
        WPPWorkOrderLine        workOrderLine;
    
        select firstOnly OrderNote, InternalNote, InvoiceNote from additionalNoteTableSource
            exists join workOrderLine
            where workOrderLine.WorkOrderId == _workOrderId
            &&    workOrderLine.LineNumber  == _lineNumber
            &&    workOrderLine.TableId     == additionalNoteTableSource.RefTableId
            &&    workOrderLine.RecId       == additionalNoteTableSource.RefRecId;
    
        additionalNoteTableTarget = DNRAdditionalNoteTable::find(tableNum(WPPWorkOrderLine), _refRecId, true);
    
        additionalNoteTableTarget.RefTableId   = tableNum(WPPWorkOrderLine);
        additionalNoteTableTarget.RefRecId     = _refRecId;
        additionalNoteTableTarget.OrderNote    += additionalNoteTableTarget.OrderNote    ? '\n' + additionalNoteTableSource.OrderNote    : additionalNoteTableSource.OrderNote;
        additionalNoteTableTarget.InternalNote += additionalNoteTableTarget.InternalNote ? '\n' + additionalNoteTableSource.InternalNote : additionalNoteTableSource.InternalNote;
        additionalNoteTableTarget.InvoiceNote  += additionalNoteTableTarget.InvoiceNote  ? '\n' + additionalNoteTableSource.InvoiceNote  : additionalNoteTableSource.InvoiceNote;
    
        if (additionalNoteTableTarget)
        {
            additionalNoteTableTarget.update();
        }
        else
        {
            additionalNoteTableTarget.insert();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>mergeDelayedMaintenance</Name>
				<Source><![CDATA[
    [SuppressBPWarningAttribute("BPCheckInsertMethodInLoop", "workOrderLineTmp.setTmp(); has been set as a Tmp table" )]

    public void mergeDelayedMaintenance(FormDataSource _formDataSource)
    {
        WPPWorkOrderLine    workOrderLine, workOrderLineTmp, workOrderLineTarget;
        WPPWorkOrderTable   workOrderTable;
        ProjTable           projTable;
        ProjGroup           projGroup;
        WPPWorkOrderTaskId  taskIdPrev;
        WPPWorkOrderId      workOrderIdPrev;
        Line                newLineNumber;
        TransDateTime       dateTime;
        boolean             mergeAdditionalNotesHeader;
        TransDateTime       nullDateTime = DateTimeUtil::addDays(utcDateTimeNull(), 1);
    
        workOrderTable = WPPWorkOrderTable::find(workOrderId);
        newLineNumber = WPPWorkOrderLine::lastLineNumber(workOrderId);
    
        mergeAdditionalNotesHeader = true;
    
        ttsBegin;
        // BP Deviation documented
        workOrderLineTmp.setTmp();
    
        // Put all the selected records in a temporary buffer
        for (workOrderLine = _formDataSource.getFirst(true) ? _formDataSource.getFirst(true) : _formDataSource.cursor();
             workOrderLine;
             workOrderLine = _formDataSource.getNext())
        {
            workOrderLineTmp.data(workOrderLine);
            workOrderLineTmp.doInsert(); // We don't want to execute the code in the insert method
        }
    
        // Loop through the temporary buffer and sort by Task en Start date/time
        while select workOrderLineTmp
        order by TaskId, RequestedStartDateTime asc
        {
            mergeAdditionalNotesHeader = workOrderIdPrev != workOrderLineTmp.WorkOrderId;
    
            if (workOrderLineTmp.TaskId != taskIdPrev)
            {
                workOrderLineTarget = WPPWorkOrderLine::findTaskId(workOrderTable.WorkOrderId, workOrderLineTmp.TaskId, true);
    
                if (!workOrderLineTarget)
                {
                    projGroup = workOrderTable.projGroup();
                    projTable = smmProjectCreate::createProjectGroup(projGroup.projType, projGroup.ProjGroupId, '', false, workOrderTable.ProjInvoiceProjId, '', workOrderTable.custVendAccount(), '', '', true);
    
                    newLineNumber++;
    
                    workOrderLineTarget.data(workOrderLineTmp);
    
                    dateTime = DateTimeUtil::applyTimeZoneOffset(workOrderLineTmp.RequestedStartDateTime, DateTimeUtil::getOriginatingTimeZone(workOrderLineTmp.RequestedStartDateTime));
                    workOrderLineTarget.RequestedStartDateTime = DateTimeUtil::newDateTime(workOrderTable.StartDate, DateTimeUtil::time(dateTime), DateTimeUtil::getUserPreferredTimeZone());
    
                    dateTime = DateTimeUtil::applyTimeZoneOffset(workOrderLineTmp.RequestedEndDateTime, DateTimeUtil::getOriginatingTimeZone(workOrderLineTmp.RequestedEndDateTime));
                    workOrderLineTarget.RequestedEndDateTime = DateTimeUtil::newDateTime(workOrderTable.EndDate, DateTimeUtil::time(dateTime), DateTimeUtil::getUserPreferredTimeZone());
    
                    if (workOrderLineTarget.RequestedEndDateTime >= nullDateTime && workOrderLineTarget.RequestedEndDateTime < workOrderLineTarget.RequestedStartDateTime)
                    {
                        workOrderLineTarget.RequestedEndDateTime = DateTimeUtil::minValue();
                    }
    
                    workOrderLineTarget.WorkOrderId = workOrderId;
                    workOrderLineTarget.LineNumber  = newLineNumber;
                    workOrderLineTarget.initFromProjTable(projTable);
                    workOrderLineTarget.insert();
                }
            }
    
            if (mergeAdditionalNotesHeader)
            {
                this.mergeAdditionalNotes_Header(workOrderLineTmp.WorkOrderId, workOrderTable.RecId);
            }
            this.mergeAdditionalNotes_Lines(workOrderLineTmp.WorkOrderId, workOrderLineTmp.LineNumber, workOrderLineTarget.RecId);
            this.mergeInventJournalTrans(workOrderLineTmp.ProjId, workOrderLineTarget.ProjId);
            this.mergeItemRequirements(workOrderLineTmp.ProjId, workOrderLineTarget.ProjId);
            this.deleteObsoleteRecords(workOrderLineTmp.WorkOrderId, workOrderLineTmp.LineNumber, workOrderLineTmp.ProjId);
    
            taskIdPrev = workOrderLineTmp.TaskId;
            workOrderIdPrev = workOrderLineTmp.WorkOrderId;
        }
    
        ttsCommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>mergeInventJournalTrans</Name>
				<Source><![CDATA[
    private void mergeInventJournalTrans(ProjId _projIdOrig, ProjId _projIdNew)
    {
        InventJournalTable      inventJournalTableSource, inventJournalTableTarget;
        InventJournalTrans      inventJournalTransSource, inventJournalTransTarget;
        RecordInsertList        inventJournalTableInsertList, inventJournalTransInsertList;
        InventJournalId         inventJournalIdPrev;
        InventJournalTableData  inventJournalTableData;
        InventJournalTransData  inventJournalTransData;
        Voucher                 voucher_last;
        boolean                 insertInsertList;
    
        inventJournalTableInsertList = new RecordInsertList(tableNum(InventJournalTable));
        inventJournalTransInsertList = new RecordInsertList(tableNum(InventJournalTrans));
    
        if (_projIdOrig && _projIdNew)
        {
            while select inventJournalTableSource
            where inventJournalTableSource.WPPProjId == _projIdOrig
                join inventJournalTransSource
                where inventJournalTransSource.JournalId == inventJournalTableSource.JournalId
            {
                if (inventJournalTableSource.JournalId != inventJournalIdPrev)
                {
                    inventJournalTableTarget.data(inventJournalTableSource);
                    inventJournalTableTarget.JournalId = JournalTableData::newTable(inventJournalTableTarget).nextJournalId();
                    inventJournalTableTarget.WPPProjId = _projIdNew;
    
                    inventJournalTableData = JournalTableData::newTable(inventJournalTableTarget);
    
                    inventJournalTableInsertList.add(inventJournalTableTarget);
                }
    
                inventJournalTransData = inventJournalTableData.journalStatic().newJournalTransData(inventJournalTransTarget, inventJournalTableData);
    
                inventJournalTransTarget.initFromInventJournalTable(inventJournalTableTarget);
                inventJournalTransTarget.initFromInventJournalTrans(inventJournalTransSource);
                inventJournalTransTarget.initFromProjTable(ProjTable::find(_projIdNew));
                inventJournalTransTarget.JournalId           = inventJournalTableTarget.JournalId;
                inventJournalTransTarget.ProjId              = _projIdNew;
                inventJournalTransTarget.Voucher             = '';
    
                if (inventJournalTableTarget.VoucherDraw == JournalVoucherDraw::Entering)
                {
                    inventJournalTransData.initVoucher(voucher_last, false);               // Voucher is assigned
                }
    
                inventJournalTransData.addJournalTransVoucher();
    
                inventJournalTransInsertList.add(inventJournalTransTarget);
    
                voucher_last = inventJournalTransTarget.Voucher;
                inventJournalIdPrev = inventJournalTableSource.JournalId;
    
                insertInsertList = true;
            }
    
            if (insertInsertList)
            {
                inventJournalTableInsertList.insertDatabase();
                inventJournalTransInsertList.insertDatabase();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>mergeItemRequirements</Name>
				<Source><![CDATA[
    private void mergeItemRequirements(ProjId _projIdOrig, ProjId _projIdNew)
    {
        if (_projIdOrig && _projIdNew)
        {
            SalesLine salesLineSource, salesLineTarget;
            ProjTable projTable = ProjTable::find(_projIdNew);
            SalesTableForm_ProjectSalesItem salesTableForm = SalesTableForm::construct(SalesTableFormId::ProjectSalesItem, projTable);
    
            while select salesLineSource
            where salesLineSource.ProjId    == _projIdOrig
            &&    salesLineSource.SalesType == SalesType::ItemReq
            {
                salesLineTarget.data(salesLineSource);
                salesLineTarget.SalesId       = '';
                salesLineTarget.RecId         = 0;
                salesLineTarget.InventTransId = '';
                salesLineTarget.LineNum       = 0;
                salesLineTarget.RORIsModified = true;
    
                salesTableForm.salesLine_initValue(salesLineTarget);
    
                salesLineTarget.initFromProjTable(projTable);
    
                salesTableForm.salesLine_Write(salesLineTarget);
    
                if (salesLineTarget.validateWrite())
                {
                    salesLineTarget.write();
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmObjectTableRecId</Name>
				<Source><![CDATA[
    public BOBObjectTableRecId parmObjectTableRecId(BOBObjectTableRecId _objectTableRecId = objectTableRecId)
    {
        objectTableRecId = _objectTableRecId;
    
        return objectTableRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmProjGroupId</Name>
				<Source><![CDATA[
    public ProjGroupId parmProjGroupId(ProjGroupId _projGroupId = projGroupId)
    {
        projGroupId = _projGroupId;
    
        return projGroupId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWorkOrderId</Name>
				<Source><![CDATA[
    public WPPWorkOrderId parmWorkOrderId(WPPWorkOrderId _workOrderId = workOrderId)
    {
        workOrderId = _workOrderId;
    
        return workOrderId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static BOBDelayedMaintenanceForm construct()
    {
        return new BOBDelayedMaintenanceForm();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>