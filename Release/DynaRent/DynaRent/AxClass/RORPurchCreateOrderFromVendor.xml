<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RORPurchCreateOrderFromVendor</Name>
	<SourceCode>
		<Declaration><![CDATA[
class RORPurchCreateOrderFromVendor
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        VendTable       vendTable;
        PurchTable      purchTable;
        FormDataSource  vendTableDatasource;
        
        if (!_args || !_args.record())
        {
            throw error(strFmt("@SYS29104", classStr(PurchCreateOrderFromVendor)));
        }

        // check datasource parameter
        if (_args.dataset() != tableNum(VendTable)
        &&  _args.dataset() != tableNum(smmBusRelTable))
        {
            throw error(strFmt("@SYS117974", classStr(PurchCreateOrderFromVendor)));
        }

        // If vendTable record is passed from a list page, some fields are not fetched.
        // In that case, vendTable record should be selected again.
        vendTableDatasource = FormDataUtil::getFormDataSource(_args.record());
        if (vendTableDatasource && vendTableDatasource.onlyFetchActive())
        {
            // reselect vendor record
            vendTable = VendTable::findRecId(_args.record().RecId);
        }
        else
        if (_args.dataset() == tableNum(smmBusRelTable))
        {
            smmBusRelTable smmBusRelTable = _args.record();
            vendTable = VendTable::findByPartyRecId(smmBusRelTable.Party);
        }
        else
        {
            vendTable = _args.record();
        }
                
        PurchTableForm  purchTableForm;
        
        purchTableForm = PurchTableForm::construct(_args.parmEnum(), purchTable);

        purchTableForm.vendAccount(vendTable.AccountNum);
        purchTableForm.invoiceAccount(vendTable.InvoiceAccount ? vendTable.InvoiceAccount : vendTable.AccountNum);

        if (purchTableForm.create())
        {
            purchTable = purchTableForm.purchTable();
        }
        
        if (purchTable == null)
        {
            return; // Customer creation was canceled
        }
    
        new MenuFunction(menuitemdisplaystr(RORPurchTableForEdit_Action), MenuItemType::Display).run(
                    RORPurchCreateOrderFromVendor::purchTableFormArgs(purchTable, _args.caller()));                
    }

]]></Source>
			</Method>
			<Method>
				<Name>purchTableFormArgs</Name>
				<Source><![CDATA[
    static protected Args purchTableFormArgs(PurchTable _purchTable, Object _caller,
        VendTable _vendTable = _purchTable.vendTable_OrderAccount())
    {
        Args purchOrderCreateArgs = new Args();

        purchOrderCreateArgs.record(_purchTable);       
        purchOrderCreateArgs.caller(_caller);
        purchOrderCreateArgs.parmObject(_caller);
        purchOrderCreateArgs.openMode(OpenMode::Edit);

        return purchOrderCreateArgs;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>