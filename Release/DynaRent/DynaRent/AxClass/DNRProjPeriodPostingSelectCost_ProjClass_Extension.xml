<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRProjPeriodPostingSelectCost_ProjClass_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(classStr(ProjPeriodPostingSelectCost_Proj))]
public final class DNRProjPeriodPostingSelectCost_ProjClass_Extension
{
    private DialogField workOrderLineStatusField;
    
    // chain-of-command wrapper methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    public Object dialog()
    {
        DialogRunbase dialogObj = next dialog();
        
        dialogObj.addGroup("@DNR6467");
        workOrderLineStatusField = dialogObj.addFieldValue(identifierStr(WPPWorkOrderLineStatus), WPPParameters::find().PostCostStatus, "@DNR6703", "@DNR6704");

        return dialogObj;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    public boolean getFromDialog()
    {
        boolean ret = next getFromDialog();
        this.DNRParmWorkOrderLineStatus(workOrderLineStatusField.value());
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryBuild</Name>
				<Source><![CDATA[
    // *** Repeated code
    // RLAM - This is almost an exact copy of the original 'queryBuild()' method.
    public QueryRun queryBuild()
    {
        next queryBuild();
            
        Query                   query = new Query();
        QueryBuildDataSource    dsProjTable, dsProjEmplTrans, dsProjCostTrans, dsProjItemTrans, dsProjItemTransCost;
        QueryBuildDataSource    dsProjEmplTransCost, dsProjCostTransCost;
    
        dsProjTable         = query.addDataSource(tableNum(ProjTable));
    
        QueryBuildDataSource qbdsWorkOrderLine = dsProjTable.addDataSource(tableNum(WPPWorkOrderLine));
        qbdsWorkOrderLine.addLink(fieldNum(ProjTable, ProjId), fieldNum(WPPWorkOrderLine, ProjId));
        qbdsWorkOrderLine.joinMode(JoinMode::OuterJoin);
            
        dsProjEmplTrans     = qbdsWorkOrderLine.addDataSource(tableNum(ProjEmplTrans));
        dsProjEmplTrans.addLink(fieldNum(WPPWorkOrderLine, ProjId), fieldNum(ProjEmplTrans, ProjId));
    
        dsProjEmplTransCost = dsProjEmplTrans.addDataSource(tableNum(ProjEmplTransCost));
        dsProjEmplTransCost.relations(true);
        dsProjEmplTransCost.joinMode(JoinMode::ExistsJoin);
    
        dsProjCostTrans     = qbdsWorkOrderLine.addDataSource(tableNum(ProjCostTrans));
        dsProjCostTrans.addLink(fieldNum(WPPWorkOrderLine, ProjId), fieldNum(ProjCostTrans, ProjId));
    
        dsProjCostTransCost = dsProjCostTrans.addDataSource(tableNum(ProjCostTransCost));
        dsProjCostTransCost.relations(true);
        dsProjCostTransCost.joinMode(JoinMode::ExistsJoin);
    
        dsProjItemTrans     = qbdsWorkOrderLine.addDataSource(tableNum(ProjItemTrans));
        dsProjItemTrans.addLink(fieldNum(WPPWorkOrderLine, ProjId), fieldNum(ProjItemTrans, ProjId));
    
        dsProjItemTransCost = dsProjItemTrans.addDataSource(tableNum(ProjItemTransCost));
        dsProjItemTransCost.relations(true);
        dsProjItemTransCost.joinMode(JoinMode::ExistsJoin);
    
        queryRun = new SysQueryRun(query);
    
        return queryRun;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>