<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRVendTransInvoicePoolForm_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(VendTransInvoicePool))]
public final class DNRVendTransInvoicePoolForm_Extension
{
    // chain-of-command wrapper methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    void init()
    {
        next init();
        this.DNRSetAllowEdit();
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNRSetAllowEdit</Name>
				<Source><![CDATA[
    // extension methods
    
    public void DNRSetAllowEdit()
    {
        DictTable dictTable = new DictTable(tableNum(LedgerJournalTrans));
        DictField dictField;
        int i;
        
        ledgerJournalTrans_ds.allowEdit(true);

        for (i = 1; i <= dictTable.fieldCnt(); i++)
        {
            dictField  = new DictField(tableNum(LedgerJournalTrans), dictTable.fieldCnt2Id(i));

            if (!dictField.isSystem() && dictField.id() != fieldNum(LedgerJournalTrans, Approver))
            {
                LedgerJournalTrans_ds.object(dictField.id()).allowEdit(false);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_ledgerJournalTransDS_OnActivated</Name>
				<Source><![CDATA[
    // event handler methods

    [FormDataSourceEventHandler(formDataSourceStr(VendTransInvoicePool, LedgerJournalTrans), FormDataSourceEventType::Activated)]
    public static void DNREvent_ledgerJournalTransDS_OnActivated(FormDataSource _sender, FormDataSourceEventArgs _eventArgs)
    {
        LedgerJournalTrans          ledgerJournalTrans      = _sender.cursor() as LedgerJournalTrans;
        FormFunctionButtonControl   btnEditApproveJournal   = _sender.formRun().design().controlName(formControlStr(VendTransInvoicePool, btnEditApproveJournal)) as FormFunctionButtonControl;

        btnEditApproveJournal.enabled(ledgerJournalTrans.INVHasPendingInvoice());
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>