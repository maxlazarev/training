<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RORFuelAdd</Name>
	<SourceCode>
		<Declaration><![CDATA[
class RORFuelAdd
{
    ItemId      itemIdFuel;
    SalesLine   salesLineFuelHost;
    SalesLine   salesLineFuel;
    Common      callerRecord;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createFuelLine</Name>
				<Source><![CDATA[
    private void createFuelLine()
    {
        InventDim   inventDim, inventDimHost;
    
        salesLineFuel.initValue();
        salesLineFuel.initFromSalesTable(salesLineFuelHost.salesTable());
        salesLineFuel.SalesType                 = salesLineFuelHost.SalesType;
        salesLineFuel.RORModifiedRORTradeType();
        salesLineFuel.ItemId                    = itemIdFuel;
        salesLineFuel.itemIdChanged();
    
        inventDimHost               = salesLineFuelHost.inventDim();
        inventDim                   = salesLineFuel.inventDim();
        inventDim.InventLocationId  = inventDimHost.InventLocationId;
        inventDim.InventSiteId      = inventDimHost.InventSiteId;
        inventDim                   = inventDim::findOrCreate(inventDim);
    
        salesLineFuel.InventDimId               = inventDim.inventDimId;
        salesLineFuel.RORReturnInventLocationId = salesLineFuelHost.RORReturnInventLocationId ?
                                                  salesLineFuelHost.RORReturnInventLocationId :
                                                  inventDim.InventLocationId;
        salesLineFuel.RORParentLineRecId        = salesLineFuelHost.RecId;
        salesLineFuel.RORKitComponentType       = RORKitComponentType::Standalone;
        salesLineFuel.RORAllowQtyEdit           = salesLineFuelHost.RORAllowQtyEdit;
        salesLineFuel.RORRentalLine::initFromParent(salesLineFuelHost);
        salesLineFuel.RORIsPriceFixed           = salesLineFuelHost.RORIsPriceFixed;
        salesLineFuel.RORIsPriceNet             = salesLineFuelHost.RORIsPriceNet;
        salesLineFuel.INVRateMultiplierTier     = 0;
        salesLineFuel.LineNum                   = salesLineFuel.RORRentalLine::getNextLineNum(SalesLine::lastLineNum(salesLineFuel.SalesId));
        salesLineFuel.createLine(true, false, false, true, false, false, false, true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new(SalesLine _salesLineFuelHost, Common _callerRecord)
    {
        itemIdFuel        = RORParameters::find().ItemIdFuel;
        callerRecord      = _callerRecord;
        salesLineFuelHost = _salesLineFuelHost;
    
    }

]]></Source>
			</Method>
			<Method>
				<Name>prompt</Name>
				<Source><![CDATA[
    public boolean prompt()
    {
        boolean         ret;
        DialogButton    dialogResult;
    
        dialogResult = Box::yesNo("@DNR8203", DialogButton::No, "@SYS24848");
        ret = (dialogResult == DialogButton::Yes);
    
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run()
    {
        if (this.validate())
        {
            ttsbegin;
            this.createFuelLine();
            ttscommit;
    
            info("@DNR8204");
        }
        else
        {
            throw error("@SYS93289");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate()
    {
        boolean ret = true;
    
        if (!itemIdFuel)
        {
            ret = checkFailed(strFmt("@DNR6654", "@DNR2433"));
        }
    
        if (!salesLineFuelHost)
        {
            ret = checkFailed(strFmt("@DNR6655", tablePName(SalesLine)));
        }
    
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static RORFuelAdd construct(Common _callerRecord)
    {
        RORRentalFuelMgtView    callerRentalFuelMgt;
        SalesLine               callerSalesLine;
    
        switch (_callerRecord.TableId)
        {
            case tableNum(RORRentalFuelMgtView):
                callerRentalFuelMgt = _callerRecord;
                callerSalesLine = callerRentalFuelMgt.salesLine();
                return new RORFuelAdd(callerSalesLine, callerRentalFuelMgt);
    
            default:
                throw error(strFmt("@SYS31187", tableId2name(_callerRecord.TableId)));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        RORFuelAdd  fuelAdd;
    
        if (_args.dataset() != tableNum(RORRentalFuelMgtView))
        {
            throw error(strFmt("@SYS31187", tableId2name(_args.dataset())));
        }
    
        fuelAdd = RORFuelAdd::construct(_args.record());
    
        if (fuelAdd.prompt())
        {
            fuelAdd.run();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>