<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRLedgerJournalTransCostForm_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(LedgerJournalTransCost))]
public final class DNRLedgerJournalTransCostForm_Extension
{
    private WPPWorkOrderLine workOrderLine;

    // chain-of-command wrapper methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    void init()
    {
        next init();

        // Check if the caller is the form 'LedgerJournalTable'. If so, obtain the work order line.
        if (this.args().caller() is FormRun)
        {
            FormRun callerForm = this.args().caller() as FormRun;

            if (callerForm.name() == formStr(LedgerJournalTable))
            {
                Object formRunObject = callerForm as Object;

                workOrderLine = formRunObject.DNRparmWorkOrderLine();
            }
        }

        FINDimValueController::setEditableDimensionSet(tableNum(LedgerJournalTrans), this.design().controlName(formControlStr(LedgerJournalTransCost, DimensionEntryControl)));
    }

]]></Source>
			</Method>
			<Method>
				<Name>WPPParmWorkOrderLine</Name>
				<Source><![CDATA[
    // extension methods

    public WPPWorkOrderLine WPPParmWorkOrderLine(WPPWorkOrderLine _workOrderLine = workOrderLine)
    {
        workOrderLine = _workOrderLine;
        return workOrderLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_LedgerJournalTransDS_OnCreated</Name>
				<Source><![CDATA[
    // event handler methods

    [FormDataSourceEventHandler(formDataSourceStr(LedgerJournalTransCost, LedgerJournalTrans), FormDataSourceEventType::Created)]
    public static void DNREvent_LedgerJournalTransDS_OnCreated(FormDataSource _sender, FormDataSourceEventArgs _eventArgs)
    {
        FormRun             formRun             = _sender.formRun() as FormRun;
        Object              formRunObject       = formRun as Object;
        WPPWorkOrderLine    workOrderLine       = formRunObject.WPPParmWorkOrderLine();

        // If there is a work order line and the work order line has a project attached to it, set the project id.
        if (workOrderLine && workOrderLine.ProjId)
        {
            DimensionAttributeValueCombination  ledgerDimension;
            LedgerJournalTrans                  ledgerJournalTrans  = formRun.dataSource().cursor() as LedgerJournalTrans;

            select RecId from ledgerDimension
            where ledgerDimension.DisplayValue == workOrderLine.ProjId;

            ledgerJournalTrans.LedgerDimension = ledgerDimension.RecId;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>