<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRLedgerJournalEngineClass_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(classStr(LedgerJournalEngine))]
public final class DNRLedgerJournalEngineClass_Extension
{
    // chain-of-command wrapper methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initDefaultDimension</Name>
				<Source><![CDATA[
    public void initDefaultDimension(
        LedgerJournalTrans              _ledgerJournalTrans,
        LedgerJournalTrans_Asset        _ledgerJournalTrans_Asset,
        LedgerJournalTrans_RAsset       _ledgerJournalTrans_RAsset, // not used anymore
    LedgerJournalTrans_RDeferrals   _ledgerJournalTrans_RDeferrals)
    {
        LedgerDimensionValueSet defaultDimensionLocal = _ledgerJournalTrans.DefaultDimension;

        next initDefaultDimension(_ledgerJournalTrans, _ledgerJournalTrans_Asset, _ledgerJournalTrans_RAsset, _ledgerJournalTrans_RDeferrals);

        // In case the LedgerJournalTrans table is configured to use the financial dimensions extension
        // framework, we don't want to use standard AX logic to populate the financial dimensions.
        if(FINDimAttributeSetup::existTableId(tableNum(LedgerJournalTrans)))
        {
            _ledgerJournalTrans.DefaultDimension = defaultDimensionLocal;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromOffsetAccount</Name>
				<Source><![CDATA[
    public void initFromOffsetAccount(
        LedgerJournalTrans              _ledgerJournalTrans,
        LedgerJournalTrans_Project      _ledgerJournalTrans_Project,
        LedgerJournalTrans_Asset        _ledgerJournalTrans_Asset,
        LedgerJournalTrans_RAsset       _ledgerJournalTrans_RAsset, 
        LedgerJournalTrans_RDeferrals   _ledgerJournalTrans_RDeferrals)
    {
        LedgerDefaultDimensionValueSet offsetDefaultDimensionLocal = _ledgerJournalTrans.OffsetDefaultDimension;

        next initFromOffsetAccount(_ledgerJournalTrans, _ledgerJournalTrans_Project, _ledgerJournalTrans_Asset, _ledgerJournalTrans_RAsset, _ledgerJournalTrans_RDeferrals);

        if(ledgerJournalTable.ledgerJournalName().FINSuppressDimensionControl)
        {
            _ledgerJournalTrans.OffsetDefaultDimension = offsetDefaultDimensionLocal;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>mustInitializeLedgerJournalTransDefaultDimensions</Name>
				<Source><![CDATA[
    protected boolean mustInitializeLedgerJournalTransDefaultDimensions()
    {
        boolean ret = next mustInitializeLedgerJournalTransDefaultDimensions();

        ret = !ledgerJournalName.FINSuppressDimensionControl;

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>offsetAccountModified</Name>
				<Source><![CDATA[
    public void offsetAccountModified(
        LedgerJournalTrans              _ledgerJournalTrans,
        LedgerJournalTrans_Project      _ledgerJournalTrans_Project,
        LedgerJournalTrans_Asset        _ledgerJournalTrans_Asset,
        LedgerJournalTrans_RAsset       _ledgerJournalTrans_RAsset,
        LedgerJournalTrans_RDeferrals   _ledgerJournalTrans_RDeferrals)
    {
        LedgerDefaultDimensionValueSet offsetDefaultDimensionLocal = _ledgerJournalTrans.OffsetDefaultDimension;

        next offsetAccountModified(_ledgerJournalTrans, _ledgerJournalTrans_Project, _ledgerJournalTrans_Asset, _ledgerJournalTrans_RAsset, _ledgerJournalTrans_RDeferrals);

        if(_ledgerJournalTrans.ledgerJournalTable().ledgerJournalName().FINSuppressDimensionControl)
        {
            _ledgerJournalTrans.OffsetDefaultDimension = offsetDefaultDimensionLocal;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>