<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRDirPartyPostalAddressFormHandlerClass_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(classStr(DirPartyPostalAddressFormHandler))]
public final class DNRDirPartyPostalAddressFormHandlerClass_Extension
{
    // event handler methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>DNREvent_isTransactionCommon_delegate</Name>
				<Source><![CDATA[
    [SubscribesTo(classStr(DirPartyPostalAddressFormHandler), delegateStr(DirPartyPostalAddressFormHandler, isTransactionCommon_delegate))]
    public static void DNREvent_isTransactionCommon_delegate(TableId _tableId, EventHandlerResult _eventHandlerResult)
    {
        switch (_tableId)
        {
            case tableNum(InventJournalTable):
            case tableNum(WPPWorkOrderTable):
                _eventHandlerResult.result(true);
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_newWithCallerRecord_delegate</Name>
				<Source><![CDATA[
    [SubscribesTo(classStr(DirPartyPostalAddressFormHandler), delegateStr(DirPartyPostalAddressFormHandler, newWithCallerRecord_delegate))]
    public static void DNREvent_newWithCallerRecord_delegate(TableId _tableId, EventHandlerResult _eventHandlerResult)
    {
        if (!_eventHandlerResult.hasResult())
        {
            DirPartyPostalAddressFormHandler postalAddressForm;

            switch (_tableId)
            {
                case tableNum(RORVenueTable):
                    postalAddressForm = new DirPartyPostalAddressFormHnd_RORVenue();
                    break;

                case tableNum(BOBObjectTable):
                case tableNum(BOBObjectView):
                    postalAddressForm = new DirPartyPostalAddressFormHnd_BOBObject();
                    break;

                case tableNum(WPPWorkOrderTable):
                    postalAddressForm = new DirPartyPostalAddressFrmHnd_WPPWorkOrder();
                    break;

                case tableNum(WPPTransportRouteAddress):
                    postalAddressForm = new DirPartyPostalAddressFrmHnd_WPPRoute();
                    break;
            }

            if (postalAddressForm)
            {
                _eventHandlerResult.result(postalAddressForm);
            }

        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_getTransactionEntity_Delegate_handler</Name>
				<Source><![CDATA[
    [SubscribesTo(classStr(DirPartyPostalAddressFormHandler), delegateStr(DirPartyPostalAddressFormHandler, getTransactionEntity_Delegate))]
    public static void DNREvent_getTransactionEntity_Delegate_handler(EventHandlerResult _result, Common _common, LogisticsLocationRoleType _roleType)
    {
        if(_common.TableId == tableNum(WPPWorkOrderTable))
        {
            WPPWorkOrderTable   workOrderTable = _common;
            
            if(workOrderTable.CustVendAccountType == CustVendACType::Cust)
            {
                _result.result(CustTable::findByPartyRecId(workOrderTable.OrganizationParty));
            }
            else if(workOrderTable.CustVendAccountType == CustVendACType::Vend)
            {
                _result.result(VendTable::findByPartyRecId(workOrderTable.OrganizationParty));
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>