<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WPPWorkOrderOverviewItemsDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
[
    SRSReportQueryAttribute(querystr(WPPWorkOrderOverviewItems)),
    SRSReportParameterAttribute(classstr(WPPWorkOrderReportsContract))
]
public class WPPWorkOrderOverviewItemsDP extends SRSReportDataProviderBase
{
    WPPWorkOrderTable       workOrderTable;
    WPPWorkOrderOverviewTmp workOrderOverviewTmp;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calculateCostPrice</Name>
				<Source><![CDATA[
    private AmountMST calculateCostPrice(InventTransId  _inventTransId)
    {
        AmountMST       costPrice;
        ProjItemTrans   projItemTrans, projItemTransRef;
        RecId           prevRecId;
    
        while select TotalCostAmountCur, RecId from projItemTrans
        where projItemTrans.InventTransId == _inventTransId
            outer join TotalCostAmountCur from projItemTransRef
            where projItemTransRef.AdjRefTransId == projItemTrans.ProjTransId
        {
            if (prevRecId != projItemTrans.RecId)
            {
                costPrice += projItemTrans.TotalCostAmountCur;
            }
    
            costPrice += projItemTransRef.TotalCostAmountCur;
    
            prevRecId = projItemTrans.RecId;
        }
    
        return costPrice;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getWorkOrderItems</Name>
				<Source><![CDATA[
    private void getWorkOrderItems()
    {
        Query query;
        QueryRun queryRun;
        QueryBuildDataSource qbds;
    
        WPPWorkOrderLine    workOrderLine;
        InventJournalTable  journalTable;
        InventJournalTrans  journalTrans;
    
        query = this.parmQuery();
        qbds = query.dataSourceTable(tableNum(WPPWorkOrderLine));
        qbds.addDynalink(fieldNum(WPPWorkOrderLine, WorkOrderId), workOrderTable, fieldNum(WPPWorkOrderTable, WorkOrderId));
        qbds.addRange(fieldNum(WPPWorkOrderLine, ProjId)).value(SysQuery::valueNotEmptyString());
    
        queryRun = new QueryRun(query);
    
        while (queryRun.next())
        {
            workOrderLine  = queryRun.get(tablenum(WPPWorkOrderLine));
            journalTable   = queryRun.get(tableNum(InventJournalTable));
            journalTrans   = queryRun.get(tableNum(InventJournalTrans));
    
            workOrderOverviewTmp.WorkOrderId        = workOrderLine.WorkOrderId;
            workOrderOverviewTmp.WorkOrderLine      = workOrderLine.LineNumber;
            workOrderOverviewTmp.TaskId             = workOrderLine.TaskId;
            workOrderOverviewTmp.Description        = journalTrans.itemName();
            workOrderOverviewTmp.TransDate          = journalTrans.TransDate;
            workOrderOverviewTmp.Qty                = journalTrans.Qty;
            workOrderOverviewTmp.CostPrice          = journalTrans.CostPrice;
            workOrderOverviewTmp.CostAmount         = workOrderOverviewTmp.CostPrice * journalTrans.Qty;
            workOrderOverviewTmp.SalesPrice         = journalTrans.ProjSalesPrice;
            workOrderOverviewTmp.SalesAmount        = journalTrans.ProjSalesPrice * journalTrans.Qty;
            workOrderOverviewTmp.LinePropertyId     = journalTrans.ProjLinePropertyId;
            workOrderOverviewTmp.Posted             = journalTable.Posted;
            workOrderOverviewTmp.insert();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getWorkOrderItemsTmp</Name>
				<Source><![CDATA[
    [
        SRSReportDataSetAttribute(tablestr(WPPWorkOrderOverviewTmp))
    ]
    public WPPWorkOrderOverviewTmp getWorkOrderItemsTmp()
    {
        select workOrderOverviewTmp;
        return workOrderOverviewTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    public void processReport()
    {
        WPPWorkOrderReportsContract contract;
    
        contract = this.parmDataContract() as WPPWorkOrderReportsContract;
        workOrderTable = WPPWorkOrderTable::find(contract.parmWorkOrderId());
    
        this.getWorkOrderItems();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>