<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRGlobalCls_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(classStr(Global))]
public static final class DNRGlobalCls_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>DNRIsUserInteractionSession</Name>
				<Source><![CDATA[
    public static boolean DNRIsUserInteractionSession()
    {
        SysClientSessions clientSession;
        SessionType       sessionType = SessionType::Worker;
        String255         sessionId   = getcurrentsessionid();
        int               strLength   = strLen(sessionId);

        sessionId = subStr(sessionId, strFind(sessionId, "S", 0 , strLength), strLength);

        select firstonly sessionType from clientSession
        where clientSession.sid == sessionId;

        if (clientSession)
        {
            sessionType = clientSession.SessionType;
        }

        return (sessionType == SessionType::WebUser || sessionType == SessionType::Worker);
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNRbuf2bufByFieldName</Name>
				<Source><![CDATA[
    public static Common  DNRbuf2bufByFieldName(Common _from, Common _to)
    {
        DictTable   dictTableFrom   = new DictTable(_from.TableId);
        DictTable   dictTableTo     = new DictTable(_to.TableId);
        DictField   dictFieldFrom;
        FieldId     fieldIdFrom     = dictTableFrom.fieldNext(0);
        FieldId     fieldIdTo;
    
        while (fieldIdFrom && !isSysId(fieldIdFrom))
        {
            dictFieldFrom = new DictField(_from.TableId, fieldIdFrom);

            if(dictFieldFrom)
            {
                fieldIdTo = dictTableTo.fieldName2Id(dictFieldFrom.name());

                if(fieldIdTo)
                _to.(fieldIdTo) = _from.(fieldIdFrom);
            }

            fieldIdFrom = dictTableFrom.fieldNext(fieldIdFrom);
        }

        return _to;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>