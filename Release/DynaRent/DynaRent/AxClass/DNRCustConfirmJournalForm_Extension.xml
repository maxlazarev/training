<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRCustConfirmJournalForm_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(CustConfirmJournal))]
public final class DNRCustConfirmJournalForm_Extension
{
    // extension methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>RORUpdateDesign</Name>
				<Source><![CDATA[
    public void RORUpdateDesign()
    {
        CustConfirmJour     custConfirmJour = this.dataSource(formDataSourceStr(CustConfirmJournal, CustConfirmJour)).cursor() as CustConfirmJour;
        boolean             hasRentalDates  = (isConfigurationkeyEnabled(ConfigurationKeyNum(RORRentalDateTimes)) && custConfirmJour.salesTable().SalesType == SalesType::RORRental);
        SalesType           salesType       = custConfirmJour.salesTable().SalesType;
    
        TabHeaderRentalDates.visible(hasRentalDates);
        TabLineRentalDates.visible(hasRentalDates);
    
        FormDesign formDesign = this.design();

        if (salesType == SalesType::RORRental)
        {
            formDesign.caption("@DNR6193");

            FormStringControl CustCustConfirmTrans_origSalesId = formDesign.controlName(formControlStr(CustConfirmJournal, CustCustConfirmTrans_origSalesId)) as FormStringControl;
            FormStringControl CustConfirmJour_SalesId = formDesign.controlName(formControlStr(CustConfirmJournal, CustConfirmJour_SalesId)) as FormStringControl;

            CustCustConfirmTrans_origSalesId.label("@DNR10");
            CustConfirmJour_SalesId.label("@DNR10");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_element_OnPostRun</Name>
				<Source><![CDATA[
    // event handler methods

    [FormEventHandler(formStr(CustConfirmJournal), FormEventType::PostRun)]
    public static void DNREvent_element_OnPostRun(xFormRun _sender, FormEventArgs _eventArgs)
    {
        Object formRun = _sender as Object;

        formRun.RORUpdateDesign();
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_CustConfirmJourDS_OnInitialized</Name>
				<Source><![CDATA[
    [FormDataSourceEventHandler(formDataSourceStr(CustConfirmJournal, CustConfirmJour), FormDataSourceEventType::Initialized)]
    public static void DNREvent_CustConfirmJourDS_OnInitialized(FormDataSource _sender, FormDataSourceEventArgs _eventArgs)
    {
        FormRun                 formRun = _sender.formRun() as FormRun;
        QueryBuildDataSource    queryDataSourceLink;

        if (formRun.args().dataset() == tableNum(smmBusRelTable))
        {
            formRun.query().dataSourceTable(tableNum(CustConfirmJour)).clearDynalinks();
            queryDataSourceLink = formRun.query().dataSourceTable(tableNum(CustConfirmJour)).addDataSource(tableNum(CustTable));
            queryDataSourceLink.addLink(fieldNum(CustConfirmJour, OrderAccount), fieldNum(CustTable, AccountNum));
            queryDataSourceLink.addDynalink(fieldNum(CustTable, Party), formRun.args().record(), fieldNum(smmBusRelTable, Party));
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>