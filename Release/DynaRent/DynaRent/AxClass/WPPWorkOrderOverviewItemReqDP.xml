<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WPPWorkOrderOverviewItemReqDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
[
    SRSReportQueryAttribute(querystr(WPPWorkOrderOverviewItemRequirements)),
    SRSReportParameterAttribute(classstr(WPPWorkOrderReportsContract))
]
public class WPPWorkOrderOverviewItemReqDP extends SRSReportDataProviderBase
{
    WPPWorkOrderTable       workOrderTable;
    WPPWorkOrderOverviewTmp workOrderOverviewTmp;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getWorkOrderItems</Name>
				<Source><![CDATA[
    private void getWorkOrderItems()
    {
        Query                   query;
        QueryBuildDataSource    qbds;
        WPPWorkOrderLine        workOrderLine;
        SalesLine               salesLine;
    
        query = this.parmQuery();
        qbds = query.dataSourceTable(tableNum(WPPWorkOrderLine));
        qbds.addDynalink(fieldNum(WPPWorkOrderLine, WorkOrderId), workOrderTable, fieldNum(WPPWorkOrderTable, WorkOrderId));
    
        while select salesLine
        where salesLine.SalesType == SalesType::ItemReq
        &&   !salesLine.InventRefId
            join WorkOrderId, LineNumber, TaskId from workOrderLine
            where workOrderLine.ProjId == salesLine.ProjId
            &&    workOrderLine.WorkOrderId == workOrderTable.WorkOrderId
    
        {
            workOrderOverviewTmp.WorkOrderId        = workOrderLine.WorkOrderId;
            workOrderOverviewTmp.WorkOrderLine      = workOrderLine.LineNumber;
            workOrderOverviewTmp.TaskId             = workOrderLine.TaskId;
    
            workOrderOverviewTmp.ItemId             = salesLine.ItemId;
            workOrderOverviewTmp.Description        = salesLine.inventTable().itemName();
            workOrderOverviewTmp.TransDate          = salesLine.ReceiptDateRequested;
            workOrderOverviewTmp.Qty                = salesLine.SalesQty;
            workOrderOverviewTmp.CostPrice          = salesLine.inventTable().costPcsPrice(salesLine.InventDimId, salesLine.inventDim());
            workOrderOverviewTmp.CostAmount         = salesLine.inventTable().costPcsPrice(salesLine.InventDimId, salesLine.inventDim()) * salesLine.SalesQty;
            workOrderOverviewTmp.SalesPrice         = salesLine.SalesPrice;
            workOrderOverviewTmp.SalesAmount        = salesLine.SalesQty * salesLine.SalesPrice;
            workOrderOverviewTmp.LinePropertyId     = salesLine.ProjLinePropertyId;
            workOrderOverviewTmp.Posted             = NoYes::No;
            workOrderOverviewTmp.insert();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getWorkOrderItemsTmp</Name>
				<Source><![CDATA[
    [
        SRSReportDataSetAttribute(tablestr(WPPWorkOrderOverviewTmp))
    ]
    public WPPWorkOrderOverviewTmp getWorkOrderItemsTmp()
    {
        select workOrderOverviewTmp;
        return workOrderOverviewTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    public void processReport()
    {
        WPPWorkOrderReportsContract contract;
    
        contract = this.parmDataContract() as WPPWorkOrderReportsContract;
        workOrderTable = WPPWorkOrderTable::find(contract.parmWorkOrderId());
    
        this.getWorkOrderItems();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>