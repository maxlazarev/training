<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRSalesTableType_RORRentalStrategy</Name>
	<SourceCode>
		<Declaration><![CDATA[
[DNRExtensionStrategyFactoryAttribute(classStr(SalesTableType_RORRental))]
class DNRSalesTableType_RORRentalStrategy extends DNRSalesTableType_SalesStrategy
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>RORmayReturnNoteBeUpdated</Name>
				<Source><![CDATA[
    public boolean RORmayReturnNoteBeUpdated()
    {
        SalesTable      salesTable      = this.parmSalesTableType_RORRental().parmSalesTable();
        boolean         salesCycle      = this.parmSalesTableType_RORRental().DNRParmSalesCycle();
        SalesPurchCycle salesPurchCycle = this.parmSalesTableType_RORRental().DNRParmSalesPurchCycle();

        boolean ok = true;
    
        if (ok && salesCycle)
        {
            salesPurchCycle = SalesPurchCycle::findBySalesPurchaseCycle(salesPurchCycleAction::RORReturnNote, SalesPurchCycleModuleParameter::Cust, salesTable.CustAccount);
    
            if (salesPurchCycle)
            {
                if (!salesPurchCycle.Active)
                {
                    ok = false;
                }
            }
            else
            if (!SalesPurchCycle::findBySalesPurchaseCycle(salesPurchCycleAction::RORReturnNote, SalesPurchCycleModuleParameter::CustParameter, '').Active)
            {
                ok = false;
            }
        }
        return ok;
    }

]]></Source>
			</Method>
			<Method>
				<Name>RORupdateAmendmentLog</Name>
				<Source><![CDATA[
    public void RORupdateAmendmentLog(ConfirmId _confirmId)
    {
        SalesTable salesTable = this.parmSalesTableType_RORRental().parmSalesTable();
        ROROrderAmendmentLog orderAmendmentLog;
    
        update_recordSet orderAmendmentLog
        setting ConfirmId = _confirmId
        where orderAmendmentLog.RefTableId == salesTable.TableId
        &&    orderAmendmentLog.RefRecId   == salesTable.RecId
        &&    !orderAmendmentLog.ConfirmId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>RORexistUnConfirmedChanges</Name>
				<Source><![CDATA[
    public boolean RORexistUnConfirmedChanges()
    {
        SalesTableType_RORRental    salesTableType_RORRental    = this.parmSalesTableType_RORRental();
        SalesTable                  salesTable                  = this.parmSalesTableType_RORRental().parmSalesTable();
    
        if(salesTableType_RORRental.DNRParmExistUnconfirmedChanges() == UnknownNoYes::Unknown)
        {
            if(salesTable.RORIsModified)
            {
                salesTableType_RORRental.DNRParmExistUnconfirmedChanges(UnknownNoYes::Yes);
            }
            else
            {
                SalesLine salesLine;

                // This line has been added for the following reason: 
                // There was an issue where the 'generate' functions under the 'POST-RENTAL' tab were disabled after exchanging an item (even for lines 
                // with status 'Delivered'). Sub status postings are only enabled if there exist no unconfirmed changes (and amendments). Even though the 
                // relevant sales order line(s) had no unconfirmed changes (i.e., 'RORIsModified' is equal to 0), the below select-statement still found
                // (a) sales order line(s) where 'RORIsModified' is not equal to 0 (while the value[s] in the database is/are 0). This is likely due to 
                // caching. Therefore, force the below select-statement to not use the cache.
                salesLine.disableCache(true);

                select firstOnly RecId from salesLine
                where salesLine.SalesId == salesTable.SalesId
                &&    salesLine.RORIsModified;
                
                salesLine.disableCache(false);

                salesTableType_RORRental.DNRParmExistUnconfirmedChanges((salesLine.RecId ? UnknownNoYes::Yes : UnknownNoYes::No));
            }
        }
    
        return (salesTableType_RORRental.DNRParmExistUnconfirmedChanges() == UnknownNoYes::Yes);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmSalesTableType_RORRental</Name>
				<Source><![CDATA[
    private SalesTableType_RORRental parmSalesTableType_RORRental()
    {
        return this.parmSalesTableType();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>