<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesRORReturnNoteJournalPrint</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>SalesRORReturnNoteJournalPrint</c> class handles the printing of the Rental order return note.
/// </summary>
class SalesRORReturnNoteJournalPrint
extends FormletterJournalPrint
{
    CustRORReturnNoteJour   returnNoteJour;
    NoYes                   printShippingLabel;
    container               printerSettingsShippingLabel;
    RecordSortedList        recordListShippingLine;
    SalesTable              salesTable;

    TaxUnrealizedDocuId                 docuSetCode;
    TaxUnrealizedDocuPrintHeader        printHeader;
    TaxUnrealizedDocuProdType           prodType;
    TaxUnrealizedDocuPrintLocation      location;
    TaxUnrealizedDocuText               miscText;

    #define.currentVersion(1)
    #LOCALMACRO.CurrentList
        printShippingLabel,
        printerSettingsShippingLabel,
        salesTable,
        docuSetCode
    #ENDMACRO
    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>deleteProforma</Name>
				<Source><![CDATA[
    private void deleteProforma()
    {
        if (returnNoteJour.isProforma())
        {
            returnNoteJour.delete();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>doPrint</Name>
				<Source><![CDATA[
    /// <summary>
    /// Prints the document.
    /// </summary>
    protected void doPrint()
    {
        CustRORReturnNoteJour journal;
    
        journal.printJournal(this, journalList);
    }

]]></Source>
			</Method>
			<Method>
				<Name>newJournalList</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs a record sorted list.
    /// </summary>
    /// <returns>
    /// The constructed record sorted list.
    /// </returns>
    public RecordSortedList  newJournalList()
    {
        RecordSortedList  recordSortedList;
    
        recordSortedList = new RecordSortedList(tableNum(CustRORReturnNoteJour));
        recordSortedList.sortOrderFromContainer(this.formLetterSorting(tableNum(CustRORReturnNoteJour), fieldNum(CustRORReturnNoteJour, RecId)));
    
        return recordSortedList;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [#CurrentVersion, #CurrentList] + [super()];
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmSalesTable</Name>
				<Source><![CDATA[
    public SalesTable parmSalesTable(SalesTable _salesTable = salesTable)
    {
        salesTable = _salesTable;
        return salesTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>sendAsXML</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sends the document in XML format.
    /// </summary>
    /// <remarks>
    /// The method must be overridden in specialized classes if sending XML is supported by the document.
    /// </remarks>
    protected void sendAsXML()
    {
        boolean newLine;
    
        if (!this.parmProforma()
        &&  new SRSPrintDestinationSettings(this.parmPrinterSettingsFormLetter()).printMediumType() != SRSPrintMediumType::Screen)
        {
            newLine = journalList.first(returnNoteJour);
            while (newLine)
            {
                returnNoteJour.sendElectronically(PrintCopyOriginal::Original);
                newLine = journalList.next(returnNoteJour);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container _packedClass)
    {
        int         version = RunBase::getVersion(_packedClass);
        container   packedSuper;
    
        switch (version)
        {
            case #CurrentVersion:
                [version, #CurrentList, packedSuper] = _packedClass;
                super(packedSuper);
                break;
    
            default :
                return false;
        }
    
        return true;
    
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static SalesRORReturnNoteJournalPrint construct()
    {
        return new SalesRORReturnNoteJournalPrint();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>