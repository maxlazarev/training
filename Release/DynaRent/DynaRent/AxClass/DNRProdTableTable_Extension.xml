<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRProdTableTable_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(ProdTable))]
public final class DNRProdTableTable_Extension
{
    // chain-of-command wrapper methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initFromCaller</Name>
				<Source><![CDATA[
    public void initFromCaller(Common callerBuffer)
    {
        ProjTable         projTable;
        SalesShippingDate localDlvDate;

        localDlvDate = this.DlvDate;

        next initFromCaller(callerBuffer);

        this.DlvDate = localDlvDate;

        switch (callerBuffer.TableId)
        {
            case tableNum(WPPWorkOrderLine):
                WPPWorkOrderLine workOrderLine = callerBuffer as WPPWorkOrderLine;
                projTable = workOrderLine.projTable();
                this.ProjId = workOrderLine.ProjId;
                this.initProjPostingType();
                this.initFromProjTable(projTable);
                break;
               
            default:
                this.DlvDate = DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone());               
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setInventDimId</Name>
				<Source><![CDATA[
    public void setInventDimId(InventDimId _inventDimId, InventDim _inventDim)
    {
        next setInventDimId(_inventDimId, _inventDim);

        this.BOBCreateSerialNum(_inventDimId, _inventDim);
    }

]]></Source>
			</Method>
			<Method>
				<Name>BOBCreateSerialNum</Name>
				<Source><![CDATA[
    // extensoin methods
    public void BOBCreateSerialNum(InventDimId _inventDimId = this.InventDimId, InventDim _inventDim = InventDim::find(_inventDimId))
    {
        // When serial number is set on a production order check if corresponding record exists in InvetSerialTable.
        InventDim inventDim = (_inventDim ? _inventDim : InventDim::find(_inventDimId));

        if (this.ItemId && inventDim.inventSerialId && !InventSerial::exist(inventDim.inventSerialId, this.ItemId))
        {
            InventSerial inventSerial;
            
            inventSerial.ItemId                 = this.ItemId;
            inventSerial.InventSerialId         = inventDim.inventSerialId;
            inventSerial.BOBSkipObjectCreation  = BOBObjectTable::findInventSerialId(inventDim.inventSerialId).RecId;

            if (inventSerial.validateWrite())
            {
                inventSerial.insert();
            }
            // Throw a stopping error, if validation fails
            else
            {
                throw error("@DNR10167");
            }            
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>