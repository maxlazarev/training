<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRProjTransItemForm_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(ProjTransItem))]
public final class DNRProjTransItemForm_Extension
{
    // chain-of-command wrapper methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildQuery</Name>
				<Source><![CDATA[
    Query buildQuery()
    {
        Query ret = next buildQuery();

        if (this.args().dataset() == tableNum(BOBObjectTable))
        {
            QueryBuildDataSource qbdsWorkOrderLine = qb_projItemTrans.addDataSource(tableNum(WPPWorkOrderLine));
            qbdsWorkOrderLine.addLink(fieldNum(ProjItemTrans, ProjId), fieldNum(WPPWorkOrderLine, ProjId));
            qbdsWorkOrderLine.joinMode(JoinMode::ExistsJoin);
    
            QueryBuildDataSource qbdsWorkOrderTable = qbdsWorkOrderLine.addDataSource(tableNum(WPPWorkOrderTable));
            qbdsWorkOrderTable.relations(true);
            qbdsWorkOrderTable.addDynalink(fieldNum(WPPWorkOrderTable, BOBObjectTableRecId), this.args().record(), fieldNum(BOBObjectTable, RecId));
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setButtonAccess</Name>
				<Source><![CDATA[
    void setButtonAccess()
    {
        next setButtonAccess();

        ProjTable           projTable;
        WPPWorkOrderTable   workOrderTable;

        select firstOnly TableId from projTable
        where projTable.ProjId == projItemTrans.ProjId
            join firstOnly TableId from workOrderTable
            where workOrderTable.OrderTableId == projTable.TableId
            &&    workOrderTable.OrderRecId   == projTable.RecId;
    
        if (workOrderTable)
        {
            WPPWorkOrders.enabled(true);
        }
        else
        {
            WPPWorkOrderLine workOrderLine;

            select firstOnly RecId from workOrderLine
            where workOrderLine.ProjId == projItemTrans.ProjId;
    
            WPPWorkOrders.enabled(workOrderLine.RecId > 0);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNRParmQb_projItemTrans</Name>
				<Source><![CDATA[
    // extension methods

    public QueryBuildDataSource DNRParmQb_projItemTrans(QueryBuildDataSource _qb_projItemTrans = qb_projItemTrans)
    {
        qb_projItemTrans = _qb_projItemTrans;
        return qb_projItemTrans;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>