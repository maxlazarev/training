<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRSalesInvoiceJournalCreateClass_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(classStr(SalesInvoiceJournalCreate))]
public final class DNRSalesInvoiceJournalCreateClass_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createJournalLine</Name>
				<Source><![CDATA[
    // chain-of-command wrapper methods
    protected void createJournalLine(Common _salesParmLine)
    {
        next createJournalLine(_salesParmLine);

        SalesParmUpdate salesParmUpdate = this.parmParmUpdate();

        if (!salesParmUpdate.Proforma)
        {
            this.BOBUpdateObjects(_salesParmLine);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>BOBUpdateObjects</Name>
				<Source><![CDATA[
    // extension methods

    private void BOBUpdateObjects(SalesParmLine _salesParmLine)
    {
        if (_salesParmLine.RORTradeType == RORTradeType::SalesPurch
        &&  _salesParmLine.salesParmTable().ParmJobStatus == ParmJobStatus::Waiting)
        {
            InventDim       inventDim;
            BOBObjectTable  objectTable;
            InventTable     inventTable;
            EcoResProduct   ecoResProduct;

            select firstOnly objectTable
            where objectTable.InventSerialId
                exists join inventDim
                where inventDim.inventDimId == _salesParmLine.InventDimId
                &&    inventDim.inventSerialId == objectTable.InventSerialId
                    join TableId from ecoResProduct
                    where ecoResProduct.BOBRentalTypeId == objectTable.RentalTypeId
                        join TableId from inventTable
                        where inventTable.ItemId == _salesParmLine.ItemId
                        &&    inventTable.Product == ecoResProduct.RecId;

            if (objectTable)
            {
                if (_salesParmLine.DeliverNow > 0)
                {
                    BOBObjectManager::invoiceSalesLine(objectTable, _salesParmLine);
                }
                else
                {
                    BOBObjectManager::creditSalesLine(objectTable, _salesParmLine);
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>