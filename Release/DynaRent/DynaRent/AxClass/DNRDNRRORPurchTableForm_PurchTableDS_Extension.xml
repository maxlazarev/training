<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRDNRRORPurchTableForm_PurchTableDS_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formDataSourceStr(DNRRORPurchTable, PurchTable))]
public final class DNRDNRRORPurchTableForm_PurchTableDS_Extension
{
    // Chain-of-Command wrapper methods

}

]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    public void init()
    {
        switch(element.args().dataset())
        {
            case tableNum(SalesTable) :
                if (TradeFormHelper::isCalledFromForm(element.args(), formStr(RORSalesTable)))
                {
                    Query query = new Query();
                    QueryBuildDataSource qbds = query.addDataSource(tableNum(PurchTable));
                    qbds.addRange(fieldNum(PurchTable, PurchaseType)).value(SysQuery::value(PurchaseType::RORSubRental));
        
                    qbds = qbds.addDataSource(tableNum(InterCompanyPurchSalesReference));
                    qbds.relations(false);
                    qbds.joinMode(JoinMode::ExistsJoin);
                    qbds.addLink(fieldNum(PurchTable, PurchId), fieldNum(InterCompanyPurchSalesReference, PurchId));
                    qbds.addRange(fieldNum(InterCompanyPurchSalesReference, SalesId)).value(SysQuery::value(element.args().record().(fieldNum(SalesTable, SalesId))));
        
                    this.query(query);
                }
                break;
                
            case tableNum(smmBusRelTable) :
                this.RORmodifyQueryBusRel();
                break;
        }

        next init();
    }

]]></Source>
			</Method>
			<Method>
				<Name>RORmodifyQueryBusRel</Name>
				<Source><![CDATA[
    // Extension methods

    void RORmodifyQueryBusRel()
    {
        smmBusRelTable busRelTable = element.args().record();
        VendAccount vendAccount = busRelTable.vendAccount();
        
        if (vendAccount)
        {
            QueryBuildDataSource qbds = this.query().dataSourceTable(tableNum(PurchTable));
            qbds.clearDynalinks();
            qbds.addRange(fieldNum(PurchTable, OrderAccount)).value(vendAccount);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>