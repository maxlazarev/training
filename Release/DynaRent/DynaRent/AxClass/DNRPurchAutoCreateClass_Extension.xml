<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRPurchAutoCreateClass_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(classStr(PurchAutoCreate))]
public final class DNRPurchAutoCreateClass_Extension
{
    private boolean                     addInfoAction;
    private boolean                     openPurchOrderForm;
    private boolean                     forcePurchTypePurchase;
    private DNRPurchAutoCreateStrategy  strategy;
    private container                   back2BackRentalDateTimes;
    private container                   conInvoiceProfile;

    // chain-of-command wrapper methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static PurchAutoCreate construct(Common buffer, Object object, Common buffer2)
    {
        PurchAutoCreate ret = next construct(buffer, object, buffer2);

        if (buffer is TmpPurchLinePrice)
        {
            TmpPurchLinePrice tmpPurchLinePrice = buffer as TmpPurchLinePrice;

            ret.RORParmAddInfoAction(tmpPurchLinePrice.RORAddInfoAction);
            ret.RORParmOpenPurchOrderForm(tmpPurchLinePrice.ROROpenPurchOrderForm);
            ret.RORParmForcePurchTypePurchase(tmpPurchLinePrice.RORForcePurchTypePurchase);

            if (object is PurchCreateFromSalesOrder)
            {
                PurchCreateFromSalesOrder purchCreateFromSalesOrder = object;

                ret.DNRParmBack2BackRentalDateTimes(purchCreateFromSalesOrder.DNRParmBack2BackRentalDateTimes());
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>create</Name>
				<Source><![CDATA[
    public void create()
    {
        next create();

        if (addInfoAction)
        {
            this.RORAddInfoAction();
        }

        if (openPurchOrderForm)
        {
            // Do not open form inside of tts transaction.
            this.ROROpenPurchOrderForm();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>RORAddInfoAction</Name>
				<Source><![CDATA[
    // extension methods

    private void RORAddInfoAction()
    {
        Query query = new Query();
        query.addDataSource(tableNum(PurchTable)).addRange(fieldNum(PurchTable, PurchId)).value(this.purchId());
    
        FormName formName = (this.purchType() == PurchaseType::RORSubRental ? formStr(RORPurchTable) : formStr(PurchTable));
        str message = strFmt("@DNR6397", this.purchType(), this.purchId());
        SysInfoAction infoAction = SysInfoAction_FormrunQuery::newFormnameQuery(formName, query);
        info(message, '', infoAction);
    }

]]></Source>
			</Method>
			<Method>
				<Name>ROROpenPurchOrderForm</Name>
				<Source><![CDATA[
    private void ROROpenPurchOrderForm()
    {
        PurchTable  purchTable;
        Args        args;
    
        if (this.purchType() == PurchaseType::RORSubRental)
        {
            purchTable = PurchTable::find(this.purchId());
    
            if (purchTable)
            {
                args = new Args();
                args.record(purchTable);
    
                new MenuFunction(menuitemDisplayStr(RORPurchTable), MenuItemType::Display).run(args);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>RORCreateRentalDatesForHeader</Name>
				<Source><![CDATA[
    public void RORCreateRentalDatesForHeader()
    {
        this.parmPurchAutoCreateStrategy().RORCreateRentalDatesForHeader();
    }

]]></Source>
			</Method>
			<Method>
				<Name>RORParmAddInfoAction</Name>
				<Source><![CDATA[
    public boolean RORParmAddInfoAction(boolean _addInfoAction = addInfoAction)
    {
        addInfoAction = _addInfoAction;
        return addInfoAction;
    }

]]></Source>
			</Method>
			<Method>
				<Name>RORParmForcePurchTypePurchase</Name>
				<Source><![CDATA[
    public boolean RORParmForcePurchTypePurchase(boolean _forcePurchTypePurchase = forcePurchTypePurchase)
    {
        forcePurchTypePurchase = _forcePurchTypePurchase;
        return forcePurchTypePurchase;
    }

]]></Source>
			</Method>
			<Method>
				<Name>RORParmOpenPurchOrderForm</Name>
				<Source><![CDATA[
    public boolean RORParmOpenPurchOrderForm(boolean _openPurchOrderForm = openPurchOrderForm)
    {
        openPurchOrderForm = _openPurchOrderForm;
        return openPurchOrderForm;
    }

]]></Source>
			</Method>
			<Method>
				<Name>RORUseExistingPurchTable</Name>
				<Source><![CDATA[
    [SysObsolete('Obsolete', false)]
    public boolean RORUseExistingPurchTable(boolean _searchForPurchTable)
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmPurchAutoCreateStrategy</Name>
				<Source><![CDATA[
    private DNRPurchAutoCreateStrategy parmPurchAutoCreateStrategy()
    {
        if (!strategy)
        {
            strategy = DNRPurchAutoCreateStrategy::construct(this);
        }

        return strategy;
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNRParmBack2BackRentalDateTimes</Name>
				<Source><![CDATA[
    public container DNRParmBack2BackRentalDateTimes(container _back2BackRentalDateTimes = back2BackRentalDateTimes)
    {
        back2BackRentalDateTimes = _back2BackRentalDateTimes;

        return back2BackRentalDateTimes;
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNRParmConINVInvoiceProfile</Name>
				<Source><![CDATA[
    public container DNRParmConINVInvoiceProfile(container _conInvoiceProfile = conInvoiceProfile)
    {
        conInvoiceProfile = _conInvoiceProfile;

        return conInvoiceProfile;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>