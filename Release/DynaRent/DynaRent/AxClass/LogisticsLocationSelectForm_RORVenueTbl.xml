<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LogisticsLocationSelectForm_RORVenueTbl</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class LogisticsLocationSelectForm_RORVenueTbl extends LogisticsLocationSelectForm
{
    RORVenueCode venueCode;
    LogisticsLocationSelectSourceType defaultSource;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>allowedSources</Name>
				<Source><![CDATA[
    public Set allowedSources()
    {
        Set allowedItems = super();

        // allow all sources
        allowedItems.add(LogisticsLocationSelectSourceType::BusinessRelation);
        allowedItems.add(LogisticsLocationSelectSourceType::RequestForQuote);
        allowedItems.add(LogisticsLocationSelectSourceType::RequestForQuoteLine);
        allowedItems.add(LogisticsLocationSelectSourceType::PurchReqLine);
        allowedItems.add(LogisticsLocationSelectSourceType::ServiceOrder);
        allowedItems.add(LogisticsLocationSelectSourceType::Project);
        allowedItems.add(LogisticsLocationSelectSourceType::PurchReqLineRequsterHCMWorker);
        allowedItems.add(LogisticsLocationSelectSourceType::SalesQuotation);
        allowedItems.add(LogisticsLocationSelectSourceType::SalesQuotationLine);
        allowedItems.add(LogisticsLocationSelectSourceType::SalesOrder);
        allowedItems.add(LogisticsLocationSelectSourceType::SalesLine);
        allowedItems.add(LogisticsLocationSelectSourceType::PurchaseOrder);
        allowedItems.add(LogisticsLocationSelectSourceType::PurchaseOrderLine);
        allowedItems.add(LogisticsLocationSelectSourceType::RORVenue);
    
        return allowedItems;
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyAddressToCaller</Name>
				<Source><![CDATA[
    public void copyAddressToCaller(Common _transferFromTable, LogisticsPostalAddressRecId _addressRecId)
    {
        Object callingFormObj = callingForm;
        callingFormObj.setAddress(_addressRecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultSource</Name>
				<Source><![CDATA[
    public LogisticsLocationSelectSourceType defaultSource(LogisticsLocationSelectSourceType _source = defaultSource)
    {
        return defaultSource;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLogisticsPostalAddressRecId</Name>
				<Source><![CDATA[
    public RecId getLogisticsPostalAddressRecId()
    {
        return (select firstonly PostalAddress from RORVenueTable
                where RORVenueTable.VenueCode == venueCode).PostalAddress;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLookupRecord</Name>
				<Source><![CDATA[
    public Common getLookupRecord()
    {
        LogisticsPostalAddress address;
        RORVenueTable venueTable;
        ;
    
        select firstOnly address
            exists join venueTable
            where venueTable.VenueCode == venueCode
            &&    venueTable.PostalAddress == address.RecId;
    
        return address;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    void new(RORVenueCode _venueCode)
    {
        venueCode = _venueCode;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmDefaultSource</Name>
				<Source><![CDATA[
    public void parmDefaultSource(LogisticsLocationSelectSourceType _source)
    {
        defaultSource = _source;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        Args                                    thisArgs;
        FormRun                                 formRun;
        LogisticsLocationSelectForm_RORVenueTbl selectForm;
        RORVenueTable                           callerBuffer;
        ;
    
        if(_args.dataset() == tableNum(RORVenueTable))
        {
            callerBuffer = _args.record();
    
            selectForm = new LogisticsLocationSelectForm_RORVenueTbl(callerBuffer.VenueCode);
            selectForm.parmCallingForm(_args.caller());
            selectForm.parmDefaultSource(_args.parmEnum());
    
            thisArgs = new Args();
            thisArgs.name(formStr(LogisticsLocationSelect));
            thisArgs.record(callerBuffer);
            thisArgs.parm(int642str(selectForm.getLogisticsPostalAddressRecId()));
            thisArgs.parmObject(selectForm);
    
            formRun = classFactory.formRunClass(thisArgs);
            formRun.init();
    
            if(formRun)
            {
                formRun.run();
                formRun.wait();
            }
        }
        else
        {
            throw error(strFmt("@SYS19096", tablePName(RORVenueTable)));
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>