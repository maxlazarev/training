<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRProdTableForm_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(ProdTable))]
public final class DNRProdTableForm_Extension
{
    // chain-of-command wrapper methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>projTable</Name>
				<Source><![CDATA[
    ProjTable projTable()
    {
        ProjTable ret = next projTable();

        if (this.args())
        {
            switch (this.args().dataset())
            {
                case tableNum(WPPWorkOrderLine):
                    ret = this.args().record().projTable();
                    break;

                case tableNum(WPPWorkOrderLineListPageView):
                    ret = WPPWorkOrderLineListPageView::getWorkOrderLine(this.args().record()).projTable();
                    break;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNRparmQueryBuildRangeProjId</Name>
				<Source><![CDATA[
    // extension methods

    public QueryBuildRange DNRparmQueryBuildRangeProjId(QueryBuildRange _queryBuildRangeProjId = queryBuildRangeProjId)
    {
        queryBuildRangeProjId = _queryBuildRangeProjId;
        return queryBuildRangeProjId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_ProdTableDS_OnInitialized</Name>
				<Source><![CDATA[
    // event handler methods

    [FormDataSourceEventHandler(formDataSourceStr(ProdTable, ProdTable), FormDataSourceEventType::Initialized)]
    public static void DNREvent_ProdTableDS_OnInitialized(FormDataSource _sender, FormDataSourceEventArgs _eventArgs)
    {
        FormRun formRun         = _sender.formRun();
        Object  formRunObject   = formRun as Object;

        if (formRun.args().dataset() != tableNum(ProjTable) && formRun.args().dataset() == tableNum(WPPWorkOrderLine))
        {
            formRunObject.DNRparmQueryBuildRangeProjId(_sender.query().dataSourceTable(tableNum(ProdTable)).addRange(fieldNum(ProdTable, ProjId)));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_ProdTableDS_OnPostLinkActive</Name>
				<Source><![CDATA[
    [FormDataSourceEventHandler(formDataSourceStr(ProdTable, ProdTable), FormDataSourceEventType::PostLinkActive)]
    public static void DNREvent_ProdTableDS_OnPostLinkActive(FormDataSource _sender, FormDataSourceEventArgs _eventArgs)
    {
        FormRun formRun         = _sender.formRun();
        Object  formRunObject   = formRun as Object;

        if(formRun.args().dataset() == tableNum(WPPWorkOrderLine))
        {
            WPPWorkOrderLine workOrderLine = formRun.args().record();
            formRunObject.DNRparmQueryBuildRangeProjId().value(workOrderLine.ProjId);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>