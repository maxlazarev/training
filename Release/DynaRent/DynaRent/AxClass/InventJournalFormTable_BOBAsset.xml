<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventJournalFormTable_BOBAsset</Name>
	<SourceCode>
		<Declaration><![CDATA[
class InventJournalFormTable_BOBAsset extends InventJournalFormTable
{
    BOBInventJournalType invJourType;
    QueryBuildRange qbrInvJourType;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>datasourceExecuteQueryPre</Name>
				<Source><![CDATA[
    public void datasourceExecuteQueryPre()
    {
        super();
    
        qbrInvJourType.value(SysQuery::value(invJourType));
    }

]]></Source>
			</Method>
			<Method>
				<Name>initJournalTypeFromCaller</Name>
				<Source><![CDATA[
    protected void initJournalTypeFromCaller()
    {
        super();
    
        switch(formRun.args().menuItemName())
        {
            case menuitemDisplayStr(BOBConvJourInventToAsset):
                invJourType = BOBInventJournalType::InventToAsset;
                break;
    
            case menuitemDisplayStr(BOBConvJourAssetToInvent):
                invJourType = BOBInventJournalType::AssetToInvent;
                break;
    
            case menuitemDisplayStr(BOBConvJourAssetToScrap):
                invJourType = BOBInventJournalType::AssetToScrap;
                break;

            default:
                invJourType = BOBInventJournalType::Standard;
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryAddRange</Name>
				<Source><![CDATA[
    public void queryAddRange()
    {
        super();
    
        qbrInvJourType = journalTable_ds.query().dataSourceTable(tableNum(InventJournalTable)).addRange(fieldNum(InventJournalTable, BOBInventJournalType));
        qbrInvJourType.status(RangeStatus::Hidden);
    }

]]></Source>
			</Method>
			<Method>
				<Name>datasourceCreatePost</Name>
				<Source><![CDATA[
    public void datasourceCreatePost()
    {
        if (!journalTable.JournalNameId)
        {
            switch (invJourType)
            {
                case BOBInventJournalType::AssetToInvent:
                    journalTable.JournalNameId = AssetParameters::find().BOBAssetJournalNameId;
                    break;
                default:
                    journalTable.JournalNameId = AssetParameters::find().InventJournalNameId;
                    break;
            }
        }

        if (journalTable.JournalNameId)
        {
            this.fieldModifiedJournalNameIdPost();
        }

        super();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>