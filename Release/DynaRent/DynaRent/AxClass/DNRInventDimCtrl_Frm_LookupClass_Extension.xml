<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRInventDimCtrl_Frm_LookupClass_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(classStr(InventDimCtrl_Frm_Lookup))]
public final class DNRInventDimCtrl_Frm_LookupClass_Extension
{
    // chain-of-command wrapper methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initInventSumQueryDatasource</Name>
				<Source><![CDATA[
    public void initInventSumQueryDatasource(FormDataSource _ds, boolean _linkItemId)
    {
        next initInventSumQueryDatasource(_ds, _linkItemId);

        RORTradeType    tradeType;
        BOBRentalTypeId rentalTypeId;
        FormDataSource  formDataSource  = this.callerInventDimFormDatasource();
    
        if (formDataSource)
        {
            formDataSource = formGetParentDatasource(formDataSource);
        }
        else
        {
            formDataSource = this.callerFormDatasource();
        }
    
        if (formDataSource)
        {
            switch (formDataSource.table())
            {
                case tableNum(PurchLine):
                    PurchLine purchLine = formDataSource.cursor();
                    tradeType = purchLine.RORTradeType;
                    rentalTypeId = purchLine.BOBRentalTypeId();
                    break;

                case tableNum(SalesQuotationLine):
                    SalesQuotationLine salesQuotationLine = formDataSource.cursor();
                    tradeType = salesQuotationLine.RORTradeType;
                    rentalTypeId = salesQuotationLine.BOBRentalTypeId();
                    break;

                case tableNum(SalesLine):
                    SalesLine salesLine = formDataSource.cursor();
                    tradeType = salesLine.RORTradeType;
                    rentalTypeId = salesLine.BOBRentalTypeId();
                    break;

                default:
                    break;
            }

            if (tradeType != RORTradeType::SalesPurch && rentalTypeId)
            {
                QueryBuildDataSource qbds = _ds.query().dataSourceNo(2);
                QueryBuildDataSource qbdsObjectTable = qbds.addDataSource(tableNum(BOBObjectTable));
                qbdsObjectTable.relations(true);
                qbdsObjectTable.joinMode(JoinMode::ExistsJoin);
                qbdsObjectTable.addRange(fieldNum(BOBObjectTable, RentalTypeId)).value(rentalTypeId);
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>