<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RORSubStatusController</Name>
	<SourceCode>
		<Declaration><![CDATA[
class RORSubStatusController extends TradeDocumentReportController
{
    FormletterJournalPrint  formletterJournalPrint;
    CustRORSubStatusJour    subStatusJour;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>documentTitle</Name>
				<Source><![CDATA[
    protected str documentTitle()
    {
        str documentTitle;
    
        if (printCopyOriginal == PrintCopyOriginal::Copy)
        {
            if (subStatusJour.isProforma())
            {
                documentTitle = literalStr("@DNR8169");
            }
            else
            {
                documentTitle = literalStr("@DNR8170");
            }
        }
        else
        {
            if (subStatusJour.isProforma())
            {
                documentTitle = literalStr("@DNR8171");
            }
            else
            {
                documentTitle = literalStr("@DNR1755");
            }
        }
    
        return documentTitle;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecordId</Name>
				<Source><![CDATA[
    protected RecId getRecordId()
    {
        return subStatusJour.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTableId</Name>
				<Source><![CDATA[
    protected TableId getTableId()
    {
        return subStatusJour.TableId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFormLetterReport</Name>
				<Source><![CDATA[
    protected void initFormLetterReport()
    {
        printCopyOriginal = this.parmArgs().parmEnum();
    
        if (classIdGet(this.parmArgs().caller()) == classNum(SalesFormletterRORSubStatusJournalPrint))
        {
            // Set the caller
            formletterJournalPrint = this.parmArgs().caller();
        }
    
        if (this.parmArgs().record())
        {
            journalList = FormLetter::createJournalListCopy(this.parmArgs().record());
        }
        else
        {
            journalList = this.parmArgs().object();
        }
    
        formLetterReport = FormLetterReport::construct(PrintMgmtDocumentType::RORSubStatus);
        formLetterReport.parmPrintType(printCopyOriginal);
    
        if (formletterJournalPrint)
        {
            // Get the print settings
            formLetterReport.parmDefaultCopyPrintJobSettings(new SRSPrintDestinationSettings(formletterJournalPrint.parmPrinterSettingsFormLetterCopy()));
            formLetterReport.parmDefaultOriginalPrintJobSettings(new SRSPrintDestinationSettings(formletterJournalPrint.parmPrinterSettingsFormLetter()));
            formLetterReport.parmUsePrintMgmtDestinations(formletterJournalPrint.parmUsePrintManagement());
        }
        else if (printCopyOriginal == PrintCopyOriginal::OriginalPrint)
        {
            // Always use the print mgmt destinations when reprinting for the OriginalPrint case.
            formLetterReport.parmUsePrintMgmtDestinations(true);
        }
    
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifyContract</Name>
				<Source><![CDATA[
    protected void modifyContract()
    {
        SRSTmpDataStore         srsTmpDataStore;
        RORSubStatusContract    subStatusContract = contract as RORSubStatusContract;
    
        if (formletterJournalPrint)
        {
            // Pack the class and insert into the temporary store.
            srsTmpDataStore.Value = formletterJournalPrint.pack();
            srsTmpDataStore.insert();
    
            // Set the rec id to contract parameter
            subStatusContract.parmFormLetterRecordId(srsTmpDataStore.RecId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFirstJournal</Name>
				<Source><![CDATA[
    protected boolean getFirstJournal()
    {
        return journalList.first(subStatusJour);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getNextJournal</Name>
				<Source><![CDATA[
    protected boolean getNextJournal()
    {
        return journalList.next(subStatusJour);
    }

]]></Source>
			</Method>
			<Method>
				<Name>output</Name>
				<Source><![CDATA[
    protected void output()
    {
        formLetterReport.loadPrintSettings(subStatusJour, SalesTable::find(subStatusJour.SalesId), subStatusJour.LanguageId);
        this.parmReportContract().parmRdlContract().parmLanguageId(subStatusJour.LanguageId);
        this.parmReportContract().parmRdlContract().parmLabelLanguageId(subStatusJour.LanguageId);

        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setDataContractRecord</Name>
				<Source><![CDATA[
    protected void setDataContractRecord(Common _common)
    {
        subStatusJour = args.record();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static RORSubStatusController construct()
    {
        return new RORSubStatusController();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        SrsReportRunController formLetterController = RORSubStatusController::construct();
    
        RORSubStatusController controller = formLetterController;
        controller.initArgs(_args, PrintMgmtDocType::construct(PrintMgmtDocumentType::RORSubStatus).getDefaultReportFormat());
    
        if (classIdGet(_args.caller()) == classNum(SalesFormLetterRORSubStatusJournalPrint))
        {
            formLetterController.renderingCompleted += eventhandler(SalesFormLetterRORSubStatusJournalPrint::renderingCompleted);
        }
    
        formLetterController.startOperation();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>