<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRVendPurchOrderJournalForm_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(VendPurchOrderJournal))]
public final class DNRVendPurchOrderJournalForm_Extension
{
    private BOBRentalSubStatus  rentalSubStatus;
    private QueryBuildRange     qbrInventTransId;

    // extension methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>DNRparmRentalSubStatus</Name>
				<Source><![CDATA[
    public BOBRentalSubStatus DNRparmRentalSubStatus(BOBRentalSubStatus _rentalSubStatus = rentalSubStatus)
    {
        rentalSubStatus = _rentalSubStatus;
        return rentalSubStatus;
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNRparmQbrInventTransId</Name>
				<Source><![CDATA[
    public QueryBuildRange DNRparmQbrInventTransId(QueryBuildRange _qbrInventTransId = qbrInventTransId)
    {
        qbrInventTransId = _qbrInventTransId;
        return qbrInventTransId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>RORUpdateDesign</Name>
				<Source><![CDATA[
    public void RORUpdateDesign()
    {
        boolean hasRentalDates = (isConfigurationkeyEnabled(configurationKeyNum(RORRentalDateTimes)) && VendPurchOrderJour.purchTable().PurchaseType == PurchaseType::RORSubRental);
    
        TabHeaderRentalDates.visible(hasRentalDates);
        TabLineRentalDates.visible(hasRentalDates);

        if(VendPurchOrderJour.purchTable().PurchaseType == PurchaseType::RORSubRental)
        {
            this.design().caption("@DNR6199");

            FormStringControl VendPurchOrderJour_PurchId = this.design().controlName(formControlStr(VendPurchOrderJournal, VendPurchOrderJour_PurchId)) as FormStringControl;
            FormStringControl VendPurchOrderTrans_origPurchId = this.design().controlName(formControlStr(VendPurchOrderJournal, VendPurchOrderTrans_origPurchId)) as FormStringControl;
            FormStringControl VendPurchOrderJour_PurchaseOrder = this.design().controlName(formControlStr(VendPurchOrderJournal, VendPurchOrderJour_PurchaseOrder)) as FormStringControl;

            VendPurchOrderJour_PurchId.label("@DNR1715");
            VendPurchOrderTrans_origPurchId.label("@DNR1715");
            VendPurchOrderJour_PurchaseOrder.label("@DNR:SubRentalJournal");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_element_OnInitializing</Name>
				<Source><![CDATA[
    // event handler methods

    [FormEventHandler(formStr(VendPurchOrderJournal), FormEventType::Initializing)]
    public static void DNREvent_element_OnInitializing(xFormRun _sender, FormEventArgs _eventArgs)
    {
        FormRun formRun = _sender as FormRun;
        Object  hostObj = formRun as Object;

        if (formRun.args().parmEnumType() == enumNum(BOBRentalSubStatus))
        {
            hostObj.DNRparmRentalSubStatus(formRun.args().parmEnum());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_VendPurchOrderJourDS_OnInitialized</Name>
				<Source><![CDATA[
    [FormDataSourceEventHandler(formDataSourceStr(VendPurchOrderJournal, VendPurchOrderJour), FormDataSourceEventType::Initialized)]
    public static void DNREvent_VendPurchOrderJourDS_OnInitialized(FormDataSource _sender, FormDataSourceEventArgs _eventArgs)
    {
        FormRun             formRun         = _sender.formRun() as FormRun;
        Object              hostObj         = formRun as Object;
        Args                args            = formRun.args();
        BOBRentalSubStatus  rentalSubStatus = hostObj.DNRparmRentalSubStatus();

        if (args.dataset() == tableNum(smmBusRelTable))
        {
            _sender.query().dataSourceTable(tablenum(VendPurchOrderJour)).clearDynalinks();
        
            QueryBuildDataSource queryDataSourceLink = _sender.query().dataSourceTable(tableNum(VendPurchOrderJour)).addDataSource(tableNum(VendTable));
            queryDataSourceLink.joinMode(JoinMode::ExistsJoin);
            queryDataSourceLink.addLink(fieldNum(VendPurchOrderJour, OrderAccount), fieldNum(VendTable, AccountNum));
            queryDataSourceLink.addDynalink(fieldnum(VendTable, Party), args.record(), fieldnum(smmBusRelTable, Party));
        }
        
        _sender.query().dataSourceTable(tablenum(VendPurchOrderJour)).addRange(fieldnum(VendPurchOrderJour, RORRentalSubStatus)).value(queryValue(rentalSubStatus));
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_VendPurchOrderJourDS_OnQueryExecuted</Name>
				<Source><![CDATA[
    [FormDataSourceEventHandler(formDataSourceStr(VendPurchOrderJournal, VendPurchOrderJour), FormDataSourceEventType::QueryExecuted)]
    public static void DNREvent_VendPurchOrderJourDS_OnQueryExecuted(FormDataSource _sender, FormDataSourceEventArgs _eventArgs)
    {
        Object formRunObj = _sender.formRun() as Object;
        formRunObj.RORUpdateDesign();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>