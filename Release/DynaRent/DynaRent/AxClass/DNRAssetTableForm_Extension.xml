<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRAssetTableForm_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(AssetTable))]
public final class DNRAssetTableForm_Extension
{
    // chain-of-command wrapper methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    void init()
    {
        next init();

        FINDimValueController::setEditableDimensionSet(tableNum(AssetTable), this.design().controlName(formControlStr(AssetTable, FINDimensionEntryControl)));

        FormStringControl ctrlBOBDisplayObjectId = this.design().controlName(formControlStr(AssetTable, BOBBusinessObject_M_BOBDisplayObjectId));
        FormStringControl ctrlBOBDisplayInventSerialId = this.design().controlName(formControlStr(AssetTable, BOBBusinessObject_M_BOBDisplayInventSerialId));
        //FormStringControl ctrlBOBDisplayItemId = this.design().controlName(formControlStr(AssetTable, BOBBusinessObject_M_BOBDisplayItemId));

        
        ctrlBOBDisplayObjectId.registerOverrideMethod(methodStr(FormStringControl, jumpRef), methodStr(DNRAssetTableForm_Extension, BOBBusinessObject_M_BOBDisplayObjectId_onJumpRef), this);
        ctrlBOBDisplayInventSerialId.registerOverrideMethod(methodStr(FormStringControl, jumpRef), methodStr(DNRAssetTableForm_Extension, BOBBusinessObject_M_BOBDisplayInventSerialId_onJumpRef), this);
        //ctrlBOBDisplayItemId.registerOverrideMethod(methodStr(FormStringControl, jumpRef), methodStr(DNRAssetTableForm_Extension, BOBBusinessObject_M_BOBDisplayItemId_onJumpRef), this);
        
        FormFunctionButtonControl fmbcLBAContractTable = this.design().controlName(formControlStr(AssetTable, LBAContractTable));
        fmbcLBAContractTable.registerOverrideMethod(methodStr(FormButtonControl, clicked), methodStr(DNRAssetTableForm_Extension, LBAContractTable_onClicked), this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDesign</Name>
				<Source><![CDATA[
    void updateDesign()
    {
        next updateDesign();

        boolean isBulkAsset = (assetTable.BOBIsBulkParent || assetTable.BOBIsBulkChild());
        BOBChangeAssetSerialNum.enabled(AssetGroup::find(assetTable.AssetGroup).BOBObjectCategory != BOBAssetObjectCategory::None);
        assetTable_DS.object(fieldNum(AssetTable, Quantity)).allowEdit(!isBulkAsset);
        assetTable_DS.object(fieldNum(AssetTable, UnitOfMeasure)).allowEdit(!isBulkAsset);        
        //assetTable_DS.object(fieldNum(AssetTable, MainAssetId)).allowEdit(isBulkAsset);
        LBAContractTable.enabled(assetTable.LBADisplayContractNumInt() != '');
    }

]]></Source>
			</Method>
			<Method>
				<Name>BOBBusinessObject_M_BOBDisplayObjectId_onJumpRef</Name>
				<Source><![CDATA[
    // extension methods

    public void BOBBusinessObject_M_BOBDisplayObjectId_onJumpRef(FormStringControl _callingControl)
    {
        this.BOBJumpRef(formStr(BOBObjectTable), fieldNum(BOBObjectTable, ObjectId), assetTable.BOBDisplayObjectId());
    }

]]></Source>
			</Method>
			<Method>
				<Name>BOBBusinessObject_M_BOBDisplayInventSerialId_onJumpRef</Name>
				<Source><![CDATA[
    public void BOBBusinessObject_M_BOBDisplayInventSerialId_onJumpRef(FormStringControl _callingControl)
    {
        this.BOBJumpRef(formStr(InventSerial), fieldNum(InventSerial, InventSerialId), assetTable.BOBDisplayInventSerialId());
    }

]]></Source>
			</Method>
			<Method>
				<Name>BOBBusinessObject_M_BOBDisplayItemId_onJumpRef</Name>
				<Source><![CDATA[
    [SysObsolete('Obsolete', false)]
    public void BOBBusinessObject_M_BOBDisplayItemId_onJumpRef(FormStringControl _callingControl)
    {
        this.BOBJumpRef(formStr(EcoResProductDetailsExtended), fieldNum(InventTable, ItemId), assetTable.BOBDisplayItemId());
    }

]]></Source>
			</Method>
			<Method>
				<Name>LBAContractTable_onClicked</Name>
				<Source><![CDATA[
    public void LBAContractTable_onClicked(FormFunctionButtonControl _callingControl)
    {
        Args args = new Args();
        
        args.record(BOBObjectTable::findByAssetId(assetTable.AssetId));
        args.caller(this);
        
        _callingControl.menufunction().run(args);
    }

]]></Source>
			</Method>
			<Method>
				<Name>BOBJumpRef</Name>
				<Source><![CDATA[
    void BOBJumpRef(FormName _formName, FieldId _lookupField, Num _lookupValue)
    {
        Args    args;
        FormRun formRun;
    
        args = new Args();
        args.caller(this);
        args.name(_formName);
        args.lookupField(_lookupField);
        args.lookupValue(_lookupValue);
    
        formRun = classFactory.formRunClass(args);
        formRun.init();
        formRun.run();
        formRun.wait();
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_element_OnInitialized</Name>
				<Source><![CDATA[
    // event handler methods

    [FormEventHandler(formStr(AssetTable), FormEventType::Initialized)]
    public static void DNREvent_element_OnInitialized(xFormRun _sender, FormEventArgs _eventArgs)
    {
        FormDataSource assetTable_ds = _sender.dataSource(formDataSourceStr(AssetTable, AssetTable));
        assetTable_ds.object(fieldNum(AssetTable, SerialNum)).visible(false);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>