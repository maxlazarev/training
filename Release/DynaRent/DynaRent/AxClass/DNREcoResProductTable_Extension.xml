<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNREcoResProductTable_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(EcoResProduct))]
public final class DNREcoResProductTable_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>BOBFindFromItemId</Name>
				<Source><![CDATA[
    // extension methods
    public static EcoResProduct BOBFindFromItemId(ItemId _itemId, boolean _forUpdate = false)
    {
        EcoResProduct ecoResProduct;
        InventTable inventTable;

        if (_itemId)
        {
            ecoResProduct.selectForUpdate(_forUpdate);

            select firstonly ecoResProduct
                exists join inventTable
                where inventTable.ItemId == _itemId
                &&    inventTable.Product == ecoResProduct.RecId;
        }
    
        return ecoResProduct;
    }

]]></Source>
			</Method>
			<Method>
				<Name>BOBMayRentalTypeBeModified</Name>
				<Source><![CDATA[
    public boolean BOBMayRentalTypeBeModified()
    {
        if(this.BOBIsProductReleased() || BOBRentalType::exist(this.DisplayProductNumber))
        {
            return false;
        }
        else
        {
            return true;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>BOBIsProductReleased</Name>
				<Source><![CDATA[
    public boolean BOBIsProductReleased()
    {
        InventTable inventTable;
        boolean     ret;
        
        select firstonly crosscompany RecId from inventTable
        where inventTable.Product == this.RecId;
        if(inventTable.RecId)
        {
            ret = true;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_onUpdating</Name>
				<Source><![CDATA[
    // event handler methods
    [DataEventHandler(tableStr(EcoResProduct), DataEventType::Updating)]
    public static void DNREvent_onUpdating(Common _sender, DataEventArgs _eventArgs)
    {
        EcoResProduct   hostRecord = _sender;
        EcoResProduct   origRecord = hostRecord.orig();
        InventTable     inventTable;
        
        if(origRecord.BOBRentalTypeId && origRecord.BOBRentalTypeId != hostRecord.BOBRentalTypeId)
        {
            if(!hostRecord.BOBMayRentalTypeBeModified())
            {
                throw error("@DNR10169");
            }
        }

    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>