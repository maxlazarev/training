<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRFormletterJournalCreateClass_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(classStr(FormletterJournalCreate))]
public final class DNRFormletterJournalCreateClass_Extension
{
    private BOBRentalSubStatus rentalSubStatus;

    // chain-of-command wrapper methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>constructFromFormLetterContract</Name>
				<Source><![CDATA[
    protected static FormletterJournalCreate constructFromFormLetterContract(PurchFormLetterContract _formletterContract)
    {
        FormletterJournalCreate ret = next constructFromFormLetterContract(_formletterContract);

        if(_formletterContract.getDocumentStatus() == DocumentStatus::RORSubStatus)
        {
            ret = purchRORSubStatusJournalCreate::construct();
            ret.RORparmRentalSubStatus(_formletterContract.RORparmRentalSubStatus());
        }
        else if(_formletterContract.getDocumentStatus() == DocumentStatus::RORReturnNote)
        {
            ret = purchRORReturnNoteJournalCreate::construct();
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newSalesJournalCreate</Name>
				<Source><![CDATA[
    public static FormletterJournalCreate newSalesJournalCreate(SalesFormLetterContract _formletterContract, SalesParmTable _salesParmTable)
    {
        FormletterJournalCreate ret = next newSalesJournalCreate(_formletterContract, _salesParmTable);

        if (ret is SalesRORSubStatusJournalCreate)
        {
            ret.RORparmRentalSubStatus(_formletterContract.RORparmRentalSubStatus());
        }
        else if (ret is SalesInvoiceJournalCreate && _formletterContract.parmCallerMenuItem() == menuItemActionStr(RORSalesFormletter_InvoiceProj_Action))
        {
            ret = null;

            RORSalesFormLetter_InvoiceProj salesFormLetter_InvoiceProj = RORSalesFormLetter_InvoiceProj::construct(_formletterContract, _salesParmTable);

            salesFormletter_InvoiceProj.transferToProjectInvoicing();
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>endCreate</Name>
				<Source><![CDATA[
    protected void endCreate()
    {
        next endCreate();

        new RORPopulateRentalLineParent(this.sourceTable(), this.getJournal()).run();
        new RORRentalDateTimesJournalizer(this.sourceTable(), this.getJournal()).run();
    }

]]></Source>
			</Method>
			<Method>
				<Name>RORparmRentalSubStatus</Name>
				<Source><![CDATA[
    // extension methods

    public BOBRentalSubStatus RORparmRentalSubStatus(BOBRentalSubStatus _rentalSubStatus = rentalSubStatus)
    {
        rentalSubStatus = _rentalSubStatus;
        return rentalSubStatus;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>