<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FINDimValueController_WPPWorkOrderLine</Name>
	<SourceCode>
		<Declaration><![CDATA[
class FINDimValueController_WPPWorkOrderLine extends FINDimValueController
{
    WPPWorkOrderLine callerWorkOrderLine;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>findSalesPurchId</Name>
				<Source><![CDATA[
    protected FINDimValueResult findSalesPurchId()
    {
        WPPWorkOrderTable workOrderTable = callerWorkOrderLine.workOrderTable();
        
        changecompany(workOrderTable.CompanyId)
        {
        
            switch (workOrderTable.OrderTableId)
            {
                case tableNum(ProjTable):
                    return FINDimValueResult::constructValue(ProjTable::findRecId(workOrderTable.OrderRecId).ProjId);
    
                case tableNum(PurchLine):
                    return FINDimValueResult::constructValue(PurchLine::findRecId(workOrderTable.OrderRecId).PurchId);
    
                case tableNum(SalesLine):
                    return FINDimValueResult::constructValue(SalesLine::findRecId(workOrderTable.OrderRecId).SalesId);
    
                default:
                    return FINDimValueResult::constructValue('');
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>findWorkOrderId</Name>
				<Source><![CDATA[
    protected FINDimValueResult findWorkOrderId()
    {
        return FINDimValueResult::constructValue(callerWorkOrderLine.WorkOrderId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDescription</Name>
				<Source><![CDATA[
    protected Description getDescription()
    {
        return strFmt("%1-%2", callerWorkOrderLine.WorkOrderId, callerWorkOrderLine.LineNumber);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDimensionFieldId</Name>
				<Source><![CDATA[
    protected FieldId getDimensionFieldId()
    {
        return fieldNum(WPPWorkOrderLine, FINDefaultDimension);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new(WPPWorkOrderLine _workOrderLine)
    {
        super(_workOrderLine);
        callerWorkOrderLine = _workOrderLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>refDepartmentId</Name>
				<Source><![CDATA[
    protected FINDimValueResult refDepartmentId()
    {
        return FINDimValueResult::constructReference(WPPExecutionGroup::find(callerWorkOrderLine.ExecutionGroupId));
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCleanupQuery</Name>
				<Source><![CDATA[
    public SysDaQueryObject getCleanupQuery()
    {
        SysDaQueryObject cleanupQuery = new SysDaQueryObject(callerWorkOrderLine);

        cleanupQuery.WhereClause(new SysDaEqualsExpression(
            new SysDaFieldExpression(callerWorkOrderLine, fieldStr(WPPWorkOrderLine, Status)),
            new SysDaValueExpression(WPPWorkOrderLineStatus::Closed)));

        return cleanupQuery;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>