<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRProjJournalTableForm_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(ProjJournalTable))]
public final class DNRProjJournalTableForm_Extension
{
    private WPPWorkOrderLine workOrderLine;

    // extension methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>DNRparmWorkOrderLine</Name>
				<Source><![CDATA[
    public WPPWorkOrderLine DNRparmWorkOrderLine(WPPWorkOrderLine _workOrderLine = workOrderLine)
    {
        workOrderLine = _workOrderLine;
        return workOrderLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNRparmJournalFormTable</Name>
				<Source><![CDATA[
    public ProjJournalFormTable DNRparmJournalFormTable(ProjJournalFormTable _journalFormTable = journalFormTable)
    {
        journalFormTable = _journalFormTable;
        return journalFormTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNRParmProjParameters</Name>
				<Source><![CDATA[
    public ProjParameters DNRParmProjParameters(ProjParameters _projParameters = projParameters)
    {
        projParameters = _projParameters;
        return projParameters;
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_element_OnInitializing</Name>
				<Source><![CDATA[
    // event handler methods

    [FormEventHandler(formStr(ProjJournalTable), FormEventType::Initializing)]
    public static void DNREvent_element_OnInitializing(xFormRun _sender, FormEventArgs _eventArgs)
    {
        FormRun formRun = _sender as FormRun;
        Object  hostObj = formRun as Object;
        WPPWorkOrderLine workOrderLine;

        if (formRun.args())
        {
            switch (formRun.args().dataset())
            {
                case tableNum(WPPWorkOrderLine):
                    workOrderLine = formRun.args().record();
                    break;

                case tableNum(WPPWorkOrderLineListPageView):
                    workOrderLine = WPPWorkOrderLineListPageView::getWorkOrderLine(formRun.args().record());
                    break;
            }
        }

        if(workOrderLine)
        {
            hostObj.DNRparmWorkOrderLine(workOrderLine);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>DNREvent_ProjJournalTableDS_OnWritten</Name>
				<Source><![CDATA[
    [FormDataSourceEventHandler(formDataSourceStr(ProjJournalTable, ProjJournalTable), FormDataSourceEventType::Written)]
    public static void DNREvent_ProjJournalTableDS_OnWritten(FormDataSource _sender, FormDataSourceEventArgs _eventArgs)
    {
        Object                  hostObj             = _sender.formRun() as Object;
        ProjJournalTable        projJournalTable    = _sender.cursor() as ProjJournalTable;
        ProjJournalFormTable    journalFormTable    = hostObj.DNRparmJournalFormTable();

        if(journalFormTable.journalTypeId() == ProjJournalType::Hour)
        {
            projJournalTable.WPPCreateProjJournalLine(hostObj.DNRparmWorkOrderLine());
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>