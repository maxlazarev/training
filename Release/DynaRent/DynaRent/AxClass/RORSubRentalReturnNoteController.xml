<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RORSubRentalReturnNoteController</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class RORSubRentalReturnNoteController extends RORReturnNoteController
{
    VendRORReturnNoteJour   returnNoteJourVend;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static RORSubRentalReturnNoteController construct()
    {
        return new RORSubRentalReturnNoteController();
    }

]]></Source>
			</Method>
			<Method>
				<Name>documentTitle</Name>
				<Source><![CDATA[
    protected str documentTitle()
    {
        str documentTitle;
    
        if (printCopyOriginal == PrintCopyOriginal::Copy)
        {
            if (!returnNoteJourVend.ReturnNoteId)
            {
                documentTitle = literalStr("@DNR7228");
            }
            else
            {
                documentTitle = literalStr("@DNR7229");
            }
        }
        else
        {
            if (!returnNoteJourVend.ReturnNoteId)
            {
                documentTitle = literalStr("@DNR7230");
            }
            else
            {
                documentTitle = literalStr("@DNR7231");
            }
        }
    
        return documentTitle;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecordId</Name>
				<Source><![CDATA[
    protected RecId getRecordId()
    {
        return returnNoteJourVend.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setDataContractRecord</Name>
				<Source><![CDATA[
    protected void setDataContractRecord(Common _common)
    {
        returnNoteJourVend = args.record();
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFormLetterReport</Name>
				<Source><![CDATA[
    protected void initFormLetterReport()
    {
        printCopyOriginal = this.parmArgs().parmEnum();
    
        if (classIdGet(this.parmArgs().caller()) == classNum(RORSubRentalReturnNoteFormLetterJournalPrint))
        {
            // Set the caller
            formletterJournalPrint = this.parmArgs().caller();
        }
    
        if (this.parmArgs().record())
        {
            journalList = FormLetter::createJournalListCopy(this.parmArgs().record());
        }
        else
        {
            journalList = this.parmArgs().object();
        }
    
        formLetterReport = FormLetterReport::construct(PrintMgmtDocumentType::RORReturnNoteSubRental);
        formLetterReport.parmPrintType(printCopyOriginal);
    
        if (formletterJournalPrint)
        {
            // Get the print settings
            formLetterReport.parmDefaultCopyPrintJobSettings(new SRSPrintDestinationSettings(formletterJournalPrint.parmPrinterSettingsFormLetterCopy()));
            formLetterReport.parmDefaultOriginalPrintJobSettings(new SRSPrintDestinationSettings(formletterJournalPrint.parmPrinterSettingsFormLetter()));
            formLetterReport.parmUsePrintMgmtDestinations(formletterJournalPrint.parmUsePrintManagement());
        }
        else if (printCopyOriginal == PrintCopyOriginal::OriginalPrint)
        {
            // Always use the print mgmt destinations when reprinting for the OriginalPrint case.
            formLetterReport.parmUsePrintMgmtDestinations(true);
        }
    
        formLetterReport.parmReportRunController(this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        RORSubRentalReturnNoteController controller = RORSubRentalReturnNoteController::construct();
        controller.initArgs(_args, PrintMgmtDocType::construct(PrintMgmtDocumentType::RORReturnNoteSubRental).getDefaultReportFormat());

        if (classIdGet(_args.caller()) == classNum(RORSubRentalReturnNoteFormLetterJournalPrint))
        {
            controller.renderingCompleted += eventhandler(RORSubRentalReturnNoteFormLetterJournalPrint::renderingCompleted);
        }

        controller.startOperation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTableId</Name>
				<Source><![CDATA[
    protected TableId getTableId()
    {
        return tableNum(VendRORReturnNoteJour);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFirstJournal</Name>
				<Source><![CDATA[
    protected boolean getFirstJournal()
    {
        return journalList.first(returnNoteJourVend);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getNextJournal</Name>
				<Source><![CDATA[
    protected boolean getNextJournal()
    {
        return journalList.next(returnNoteJourVend);
    }

]]></Source>
			</Method>
			<Method>
				<Name>output</Name>
				<Source><![CDATA[
    protected void output()
    {
        formLetterReport.loadPrintSettings(returnNoteJourVend, PurchTable::find(returnNoteJourVend.PurchId), returnNoteJourVend.LanguageId);
        this.parmReportContract().parmRdlContract().parmLanguageId(returnNoteJourVend.LanguageId);
        this.parmReportContract().parmRdlContract().parmLabelLanguageId(returnNoteJourVend.LanguageId);

        this.outputReports();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>