<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WPPTransportRouteFormHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
class WPPTransportRouteFormHandler
{
    String255               title;
    RefTableId              refTableId;
    RefRecId                refRecId;
    LogisticsDeliveryName   deliveryName;
    LogisticsPostalAddressRecId postalAddressRecId;
    boolean                 showVenue;
    RORVenueCode            venueCode;
    boolean                 isViewOnly;
    FieldId                 refFieldId;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmDeliveryName</Name>
				<Source><![CDATA[
    LogisticsDeliveryName    parmDeliveryName(LogisticsDeliveryName   _deliveryName = deliveryName)
    {
        deliveryName = _deliveryName;
        return _deliveryName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmIsViewOnly</Name>
				<Source><![CDATA[
    boolean parmIsViewOnly(boolean _isViewOnly = isViewOnly)
    {
        isViewOnly =_isViewOnly;
        return isViewOnly;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmPostalAddressRecId</Name>
				<Source><![CDATA[
    LogisticsPostalAddressRecId  parmPostalAddressRecId(LogisticsPostalAddressRecId _postalAddressRecId =postalAddressRecId)
    {
        postalAddressRecId = _postalAddressRecId;
        return postalAddressRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmRefFieldId</Name>
				<Source><![CDATA[
    FieldId  parmRefFieldId(FieldId  _refFieldId = refFieldId)
    {
        refFieldId = _refFieldId;
        return refFieldId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmRefRecId</Name>
				<Source><![CDATA[
    RefRecId  parmRefRecId(refRecId  _refRecId = refRecId)
    {
        refRecId = _refRecId;
        return refRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmRefTableId</Name>
				<Source><![CDATA[
    RefTableId  parmRefTableId(refTableId  _refTableId = refTableId)
    {
        refTableId = _refTableId;
        return refTableId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmShowVenue</Name>
				<Source><![CDATA[
    Boolean parmShowVenue(boolean   _showVenue = showVenue)
    {
        showVenue = _showVenue;
        return showVenue;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmTitle</Name>
				<Source><![CDATA[
    String255 parmTitle(String255 _title =title)
    {
        title = _title;
        return title;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmVenueCode</Name>
				<Source><![CDATA[
    RORVenueCode    parmVenueCode(RORVenueCode  _venueCode = venueCode)
    {
        venueCode   = _venueCode;
        return venueCode;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        WPPTransportRouteFormHandler    transportRouteFormHandler;
        Args                            args;
        FormRun                         addressForm;
        ;
    
        transportRouteFormHandler = WPPTransportRouteFormHandler::newParameters(_args.record(),_args.caller().name());
    
        args = new Args();
        args.name(formStr(WPPTransportRouteAddress));
        args.record(_args.record());
        args.parmObject(_args.parmObject(transportRouteFormHandler));
        args.caller(_args.caller());
    
        addressForm = classfactory.formRunClass(args);
        if (addressForm)
        {
            addressForm.init();
            addressForm.run();
            addressForm.wait();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>newParameters</Name>
				<Source><![CDATA[
    static WPPTransportRouteFormHandler newParameters(Common _callerRecord,formName _formName)
    {
        WPPTransportRouteFormHandler   transportRouteFormHandler = New WPPTransportRouteFormHandler();
        PurchLine   purchLine;
        PurchTable  purchTable;
        SalesLine   salesLine;
        SalesTable  salesTable;
        SalesQuotationLine  salesQuotationLine;
        SalesQuotationTable salesQuotationTable;
        WPPWorkOrderTable   workOrderTable;
        RefTableId  refTableId;
        refRecId    refRecid;
        str         titleName ='';
        ;
        transportRouteFormHandler.parmShowVenue(false);
        switch (_callerRecord.TableId)
        {
            case tableNum(salesLine):
                salesLine   = _callerRecord;
                refTableId  = salesLine.TableId;
                refRecid    = salesLine.RecId;
                if(_formName == formStr(RORSalesTable))
                {
                    titleName = "@DNR6238";
                    transportRouteFormHandler.parmShowVenue(true);
                    transportRouteFormHandler.parmVenueCode(salesLine.RORVenueCode);
                }
                else
                {
                    titleName = "@SYS80119";
                }
                salesTable = salesLine.salesTable();
                titleName += strFmt(': %1 : %2 - %3', salesTable.SalesId,
                                                salesTable.CustAccount,
                                                salesTable.customerName());
                transportRouteFormHandler.parmDeliveryName(salesLine.DeliveryName);
                transportRouteFormHandler.parmPostalAddressRecId(salesLine.DeliveryPostalAddress);
                transportRouteFormHandler.parmRefFieldId(fieldNum(salesLine, DeliveryPostalAddress));
                break;
    
    
            case tableNum(purchLine):
                purchLine   = _callerRecord;
                refTableId  = purchLine.TableId;
                refRecid    = purchLine.RecId;
                if(_formName == formStr(RORPurchTable))
                {
                    titleName = "@DNR1715";
                    transportRouteFormHandler.parmShowVenue(true);
                    transportRouteFormHandler.parmVenueCode(purchLine.RORVenueCode);
                }
                else
                {
                    titleName = "@SYS25545";
                }
                purchTable = purchLine.purchTable();
                titleName += strFmt(': %1 : %2 - %3', purchTable.PurchId,
                                                purchTable.InvoiceAccount,
                                                purchTable.vendTable_InvoiceAccount().name());
                transportRouteFormHandler.parmDeliveryName(purchLine.DeliveryName);
                transportRouteFormHandler.parmPostalAddressRecId(purchLine.DeliveryPostalAddress);
                transportRouteFormHandler.parmRefFieldId(fieldNum(purchLine, DeliveryPostalAddress));
                break;
    
            case tableNum(SalesQuotationLine):
                salesQuotationLine = _callerRecord;
                refTableId  = salesQuotationLine.TableId;
                refRecid    = salesQuotationLine.RecId;
                if(_formName == formStr(RORSalesQuotationTable) || _formName == formStr(RORSalesQuotationProjTable))
                {
                    titleName = _formName == formStr(RORSalesQuotationTable)? "@DNR5855":"@DNR1999";
                    transportRouteFormHandler.parmShowVenue(true);
                    transportRouteFormHandler.parmVenueCode(salesQuotationLine.RORVenueCode);
                }
                else if(_formName == formStr(SalesQuotationTable) || _formName == formStr(SalesQuotationProjTable))
                {
                    titleName = _formName == formStr(SalesQuotationTable)? "@SYS114606":"@SYS84513";
                }
                salesQuotationTable = salesQuotationLine.salesQuotationTable();
                if(salesQuotationTable.CustAccount)
                {
                    // Customer is defined...
                    titleName += strFmt(": %1 : %2 %3 - %4", salesQuotationTable.QuotationId,
                                                               "@SYS24704",
                                                               salesQuotationTable.CustAccount,
                                                               salesQuotationTable.customerName());
                }
                else
                {
                    // Business relation is defined...
                    titleName += strFmt(": %1 : %2 %3 - %4", salesQuotationTable.QuotationId,
                                                               "@SYS80138",
                                                               salesQuotationTable.BusRelAccount,
                                                               salesQuotationTable.busRelName());
                }
    
                if (salesQuotationTable && (!salesQuotationTable.isTemplate()               &&
                    salesQuotationTable.QuotationStatus != SalesQuotationStatus::Created)   &&
                    salesQuotationTable.QuotationStatus != SalesQuotationStatus::Sent       &&
                    salesQuotationTable.QuotationStatus != SalesQuotationStatus::Revised    &&
                    salesQuotationTable.QuotationStatus != SalesQuotationStatus::Modified)
                {
                    transportRouteFormHandler.parmIsViewOnly(true);
                }
    
                transportRouteFormHandler.parmDeliveryName(salesQuotationLine.DeliveryName);
                transportRouteFormHandler.parmPostalAddressRecId(salesQuotationLine.DeliveryPostalAddress);
                transportRouteFormHandler.parmRefFieldId(fieldNum(salesQuotationLine, DeliveryPostalAddress));
                break;
    
            case tableNum(WPPWorkOrderTable):
            case tableNum(WPPWorkOrderLine):
                if(_callerRecord.TableId == tableNum(WPPWorkOrderLine))
                {
                    workOrderTable = WPPWorkOrderLine::findRecId(_callerRecord.RecId).workOrderTable();
                }
                else
                {
                    workOrderTable =  _callerRecord;
                }

                titleName = "@DNR6464" + strFmt(': %1 : %2 - %3', WorkOrderTable.WorkOrderId, WorkOrderTable.custVendAccount(), WorkOrderTable.displayCustomerVendorName());

                if(workOrderTable.OrderTableId == tableNum(salesLine))
                {
                    select firstOnly salesLine where salesLine.RecId == workOrderTable.OrderRecId;
                    refTableId  = salesLine.TableId;
                    refRecid    = salesLine.RecId;
                    transportRouteFormHandler.parmDeliveryName(salesLine.DeliveryName);
                    transportRouteFormHandler.parmPostalAddressRecId(salesLine.DeliveryPostalAddress);
                    transportRouteFormHandler.parmRefFieldId(fieldNum(salesLine, DeliveryPostalAddress));
                    transportRouteFormHandler.parmVenueCode(salesLine.RORVenueCode);
                }
                else if(workOrderTable.OrderTableId == tableNum(purchLine))
                {
                    select firstOnly purchLine where purchLine.RecId == workOrderTable.OrderRecId;
                    refTableId  = purchLine.TableId;
                    refRecid    = purchLine.RecId;
                    transportRouteFormHandler.parmDeliveryName(purchLine.DeliveryName);
                    transportRouteFormHandler.parmPostalAddressRecId(purchLine.DeliveryPostalAddress);
                    transportRouteFormHandler.parmRefFieldId(fieldNum(purchLine, DeliveryPostalAddress));
                    transportRouteFormHandler.parmVenueCode(purchLine.RORVenueCode);
                }
                transportRouteFormHandler.parmIsViewOnly(true);
                transportRouteFormHandler.parmShowVenue(true);
                break;
        }
        transportRouteFormHandler.parmRefTableId(refTableId);
        transportRouteFormHandler.parmRefRecId(refRecid);
        transportRouteFormHandler.parmTitle(titleName);
        return transportRouteFormHandler;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>