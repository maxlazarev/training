<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRVendTableTable_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(VendTable))]
public final class DNRVendTableTable_Extension
{
    // chain-of-command wrapper methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insert</Name>
				<Source><![CDATA[
    void insert()
    {
        ttsbegin;

        next insert();
        this.IRMCreateOrUpdateProspect();

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromSmmBusRelTable</Name>
				<Source><![CDATA[
    public void initFromSmmBusRelTable(smmBusRelTable _smmBusRelTable)
    {
        LedgerDimensionValueSet tmpDefaultDimension = this.DefaultDimension;

        next initFromSmmBusRelTable(_smmBusRelTable);
        
        if(FINDimAttributeSetup::existTableId(tableNum(VendTable)))
        {
            this.DefaultDimension = tmpDefaultDimension;
        }

        this.RORInitFromSmmBusRelTable(_smmBusRelTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initValue</Name>
				<Source><![CDATA[
    void initValue()
    {
        next initValue();

        VendParameters parameters = VendParameters::find();

        this.INVInvoiceProfileId = parameters.INVInvoiceProfileId;

        this.DNRRentalCalendarId = parameters.DNRRentalCalendarId;
        this.RORPriceTimeUnit    = parameters.RORPriceTimeUnit;
        this.DlvMode             = parameters.RORVendDlvModeId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateField</Name>
				<Source><![CDATA[
    boolean validateField(FieldId p1)
    {
        boolean ret = next validateField(p1);

        if (ret)
        {
            switch (p1)
            {
                case fieldNum(VendTable, DNRRentalCalendarId):
                    if (this.DNRRentalCalendarId && DNRCalendarTable::isSpecialCalendar(this.DNRRentalCalendarId))
                    {
                        ret = checkFailed("@WAX3363");
                        ret = checkFailed(strFmt("@DNR8692", this.DNRRentalCalendarId));
                    }
                    break;
            }
        }
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>RORInitFromSmmBusRelTable</Name>
				<Source><![CDATA[
    // extension methods

    private void RORInitFromSmmBusRelTable(smmBusRelTable _smmBusRelTable)
    {
        this.INVInvoiceProfileId    = _smmBusRelTable.INVInvoiceProfileId;
        this.DNRRentalCalendarId    = _smmBusRelTable.DNRRentalCalendarId;
        this.RORPriceTimeUnit       = _smmBusRelTable.RORPriceTimeUnit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>IRMCreateOrUpdateProspect</Name>
				<Source><![CDATA[
    private void IRMCreateOrUpdateProspect()
    {
        if (smmBusRelTable::IRMExistByParty(this.Party))
        {
            smmBusRelTable::IRMUpdateProspect(this);
        }
        else
        {
            smmBusRelTable::IRMCreateProspect(this);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>