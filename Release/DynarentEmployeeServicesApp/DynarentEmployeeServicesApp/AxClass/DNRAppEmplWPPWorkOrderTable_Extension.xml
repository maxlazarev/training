<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRAppEmplWPPWorkOrderTable_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(WPPWorkOrderTable))]
public final class DNRAppEmplWPPWorkOrderTable_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>update</Name>
				<Source><![CDATA[
    public void update()
    {
        WPPWorkOrderTable workOrderTableOrig = this.orig();
    
        next update();

        if (isConfigurationkeyEnabled(configurationKeyNum(DNRAppEmpl))
        && (this.LogisticsLocationRecId != workOrderTableOrig.LogisticsLocationRecId))
        {
            WPPWorkOrderLine workOrderLine;
            WPPParameters parameters = WPPParameters::find();
            WPPWorkOrderLineResource workOrderLineResource;
            RecordInsertList recordInsertList = new RecordInsertList(tableNum(DNRAppEmplNotificationTable));

            while select workOrderLine
            where workOrderLine.WorkOrderId == this.WorkOrderId
            &&    workOrderLine.Status != WPPWorkOrderLineStatus::Completed
            &&    ((parameters.DNRAppEmplWorkOrderLineTriggerStatus == DNRAppEmplWorkOrderLineTriggerStatus::Planned && workOrderLine.Status >= WPPWorkOrderLineStatus::Planned)
                || (parameters.DNRAppEmplWorkOrderLineTriggerStatus == DNRAppEmplWorkOrderLineTriggerStatus::Released && workOrderLine.Status >= WPPWorkOrderLineStatus::Released))
                join workOrderLineResource
                where workOrderLineResource.WorkOrderLineRecId == workOrderLine.RecId
            {
                DNRAppEmplNotificationTable notification;
                notification.initValue();
                notification.initFromWorkOrderLine(workOrderLine);
                notification.initFromWorkOrderLineResource(workOrderLineResource);
                notification.setMessage(strFmt("@DNR:LocationWorkOrderXTaskYChanged", workOrderLine.WorkOrderId, workOrderLine.TaskId));
                recordInsertList.add(notification);
            }

            recordInsertList.insertDatabase();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>