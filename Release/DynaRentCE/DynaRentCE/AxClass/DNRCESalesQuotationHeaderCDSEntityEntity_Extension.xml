<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DNRCESalesQuotationHeaderCDSEntityEntity_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(SalesQuotationHeaderCDSEntity))]
final class DNRCESalesQuotationHeaderCDSEntityEntity_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initializeEntityDataSource</Name>
				<Source><![CDATA[
    public void initializeEntityDataSource(DataEntityRuntimeContext _entityCtx, DataEntityDataSourceRuntimeContext _dataSourceCtx)
    {
        switch (_dataSourceCtx.name())
        {
            case dataEntityDataSourceStr(SalesQuotationHeaderCDSEntity, SalesQuotationHeaderV2Entity):
                if (isConfigurationkeyEnabled(configurationKeyNum(DNRCEIntegration)))
                {
                    smmBusRelTable  prospects;
                    CustTable       custTable;

                    if (this.DNRCEProspectId)
                    {
                        select firstonly AccountNum,InvoiceAccount from custTable
                            join TableId from prospects
                            where prospects.Party == custTable.Party
                            &&    prospects.BusRelAccount == this.DNRCEProspectId;

                        if (custTable)
                        {
                            this.RequestingCustomerAccountNumber = custTable.AccountNum;
                            this.InvoiceCustomerAccountNumber = custTable.InvoiceAccount ? custTable.InvoiceAccount : custTable.AccountNum;
                        }
                        else
                        {
                            this.ProspectId = this.DNRCEProspectId;
                        }
                    }

                    if (this.DNRCEInvoiceAccountProspectId != this.DNRCEProspectId)
                    {
                        select firstonly AccountNum,InvoiceAccount from custTable
                            join TableId from prospects
                            where prospects.Party == custTable.Party
                            &&    prospects.BusRelAccount == this.DNRCEInvoiceAccountProspectId;

                        if (custTable)
                        {
                            this.InvoiceCustomerAccountNumber = custTable.AccountNum;
                        }
                    }
                }
           
                break;
        }

        next initializeEntityDataSource(_entityCtx, _dataSourceCtx);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postLoad</Name>
				<Source><![CDATA[
    public void postLoad()
    {
        if (isConfigurationkeyEnabled(configurationKeyNum(DNRCEIntegration))
        &&  this.SalesQuotationStatus != SalesQuotationStatus::Created)
        {
            CustQuotationJour       custQuotationJour;
            CustQuotationSalesLink  custQuotationSalesLink;
            
            select firstonly QuotationDocNum from custQuotationJour
            order by RecId desc
            join TableId from custQuotationSalesLink
            where custQuotationSalesLink.salesId == custQuotationJour.SalesId
            &&  custQuotationSalesLink.quotationId == custQuotationJour.QuotationId
            &&  custQuotationSalesLink.quotationDate == custQuotationJour.QuotationDate
            &&  custQuotationSalesLink.origQuotationId == this.SalesQuotationNumber;
            
            this.DNRCEQuotationDocNum = custQuotationJour.QuotationDocNum;
        }

        next postLoad();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>