<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RTPInventTransferOrderCopying</Name>
	<SourceCode>
		<Declaration><![CDATA[
class RTPInventTransferOrderCopying
{
    InventTransferTable copyFrom;
    InventTransferTable copyTo;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>copyServer</Name>
				<Source><![CDATA[
    public static void copyServer(InventTransferTable _copyFrom, InventTransferTable _copyTo)
    {
        RTPInventTransferOrderCopying   transferOrderCopying = new RTPInventTransferOrderCopying();
        transferOrderCopying.parmCopyFromBuffer(_copyFrom);
        transferOrderCopying.parmCopyToBuffer(_copyTo);
        transferOrderCopying.copy();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCopyFromBuffer</Name>
				<Source><![CDATA[
    public InventTransferTable parmCopyFromBuffer(InventTransferTable _copyFrom = copyFrom)
    {
        copyFrom = _copyFrom;
        return copyFrom;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCopyToBuffer</Name>
				<Source><![CDATA[
    public InventTransferTable parmCopyToBuffer(InventTransferTable _copyTo = copyTo)
    {
        copyTo = _copyTo;
        return copyTo;
    }

]]></Source>
			</Method>
			<Method>
				<Name>mayTransferLineBeCopied</Name>
				<Source><![CDATA[
    private boolean mayTransferLineBeCopied(InventTransferLine _transferLine)
    {
        InventDim           inventDimCopyFrom;
        InventTransferLine  inventTranferLine;
        InventDim           inventDim;
        boolean             ret = true;

        inventDimCopyFrom = _transferLine.inventDim();
        
        // This validation will ensure that the Serialized objects can only be copied only once.
        if(inventDimCopyFrom.inventSerialId)
        {
            select firstonly RecId from inventTranferLine
            where inventTranferLine.TransferId == copyTo.TransferId
                exists join inventDim
                where inventDim.inventDimId == inventTranferLine.InventDimId
                &&    inventDim.inventSerialId == inventDimCopyFrom.inventSerialId;

            if(inventTranferLine)
            {
                ret = false;
            }

        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>copy</Name>
				<Source><![CDATA[
    protected void copy()
    {
        InventTransferLine  transferLine;
        InventTransferLine  transferLineTo;
        InventDim           inventDim;

        ttsbegin;

        while select transferLine
        where transferLine.TransferId == copyFrom.TransferId
        {
            if(this.mayTransferLineBeCopied(transferLine))
            {
                transferLineTo.clear();
            
                buf2Buf(transferLine, transferLineTo);
            
                transferLineTo.TransferId   = copyTo.TransferId;
                transferLineTo.ShipDate     = copyTo.ShipDate;
                transferLineTo.ReceiveDate  = copyTo.ReceiveDate;

                inventDim = transferLine.inventDim();
                inventDim.InventLocationId = copyTo.InventLocationIdFrom;
                inventDim.InventSiteId     = InventLocation::find(copyTo.InventLocationIdFrom).InventSiteId;
                transferLineTo.InventDimId = InventDim::findOrCreate(inventDim).inventDimId;
            
                transferLineTo.QtyShipped = 0;
                transferLineTo.QtyShipNow = 0;
                transferLineTo.QtyRemainShip = transferLineTo.QtyTransfer;
                transferLineTo.RemainStatus = InventTransferRemainStatus::None;

                transferLineTo.QtyReceived = 0;
                transferLineTo.QtyReceiveNow = 0;
                transferLineTo.QtyRemainReceive = transferLineTo.QtyTransfer;
                transferLineTo.LineNum = InventTransferLine::lastLineNum(transferLineTo.TransferId) + 1;


                if(transferLineTo.validateWrite())
                {
                    transferLineTo.insert();
                }
            }
        }

        ttscommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>