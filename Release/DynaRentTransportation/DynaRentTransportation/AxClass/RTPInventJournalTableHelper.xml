<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RTPInventJournalTableHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
class RTPInventJournalTableHelper
{
    InventJournalTable  inventJournalTable;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createWorkOrder</Name>
				<Source><![CDATA[
    private WPPWorkOrderId createWorkOrder()
    {
        WPPWorkOrderSynchronizer    workOrderSynchroniser;
        ;
    
        workOrderSynchroniser = new WPPWorkOrderSynchronizer_RTPReturn(inventJournalTable);
    
        if (workOrderSynchroniser)
        {
            workOrderSynchroniser.run();
        }
    
        return WPPWorkOrderTable::findFromOrder(inventJournalTable.TableId, inventJournalTable.RecId).WorkOrderId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>inventJournalTable</Name>
				<Source><![CDATA[
    public InventJournalTable  inventJournalTable(InventJournalTable  _inventJournalTable = inventJournalTable)
    {
        if (!prmisDefault(_inventJournalTable))
        {
            inventJournalTable = _inventJournalTable;
        }
    
        return inventJournalTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>postCheckJournal</Name>
				<Source><![CDATA[
    private boolean postCheckJournal(JournalCheckPostType _journalCheckPostType)
    {
        InventJournalCheckPost_Movement journalCheckPost = InventJournalCheckPost::newJournalCheckPost(_journalCheckPostType, inventJournalTable);
        journalCheckPost.parmAutoBlock(true);
        journalCheckPost.parmShowInfoResult(true);
        journalCheckPost.runOperation();
        return journalCheckPost.parmCheckJournalResult();
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateJournalTable</Name>
				<Source><![CDATA[
    private void updateJournalTable(WPPWorkOrderId _workOrderId)
    {
        inventJournalTable = InventJournalTable::find(inventJournalTable.JournalId, true);
        inventJournalTable.RTPIsJournalConfirmed    = NoYes::Yes;
        inventJournalTable.RTPWorkOrderId           = _workOrderId;
        inventJournalTable.update();
    }

]]></Source>
			</Method>
			<Method>
				<Name>confirmJournal</Name>
				<Source><![CDATA[
    public client server static void confirmJournal(InventJournalTable _inventJournalTable)
    {
        RTPInventJournalTableHelper inventJournalTableHelper = new RTPInventJournalTableHelper();
        WPPWorkOrderId              workOrderId;
        FormDataSource              fds;
    
        ttsBegin;
        _inventJournalTable.selectForUpdate(true);
        _inventJournalTable.SessionId = 0;
        _inventJournalTable.update();
        ttsCommit;
        inventJournalTableHelper.inventJournalTable(_inventJournalTable);
    
        // 1. validate journal. If errors then throw error to stop procedure
        if (inventJournalTableHelper.postCheckJournal(JournalCheckPostType::Check))
        {
            ttsBegin;
    
            // 2. create work order
            workOrderId = inventJournalTableHelper.createWorkOrder();
    
            // 3. set journal as confirmed and stamp workOrderId
            inventJournalTableHelper.updateJournalTable(workOrderId);
    
            ttsCommit;
    
            if (workOrderId)
            {
                info(strFmt("@DNR8560", workOrderId));
            }
    
            if (FormDataUtil::isFormDataSource(_inventJournalTable))
            {
                fds = FormDataUtil::getFormDataSource(_inventJournalTable);
                fds.research(true);
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>