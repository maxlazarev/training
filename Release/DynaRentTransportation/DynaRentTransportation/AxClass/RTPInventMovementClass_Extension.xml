<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RTPInventMovementClass_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(classStr(InventMovement))]
public final class RTPInventMovementClass_Extension
{
    // chain-of-command wrapper methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>constructNoThrow</Name>
				<Source><![CDATA[
    public static InventMovement constructNoThrow(
        Common              _buffer,
        InventMovSubType    _subType,
        Common              _childBuffer)
    {
        InventMovement ret = next  constructNoThrow(_buffer, _subType, _childBuffer);

        if (_buffer.TableId == tableNum(InventJournalTrans))
        {
            InventJournalTrans inventJournalTrans = _buffer as InventJournalTrans;
            if (inventJournalTrans.JournalType == InventJournalType::RTPReturn)
            {
                ret = RTPInventMov_Jour_Return::newFromInventJournalTrans(inventJournalTrans, _subType);
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>mustUpdateInventTransFields</Name>
				<Source><![CDATA[
    public boolean mustUpdateInventTransFields(InventMovement _movement_orig)
    {
        boolean ret = next mustUpdateInventTransFields(_movement_orig);
        
        if (!this.parmCompanyCountryRegion().isCountryRegionRU())
        {
            ret = (ret || this.RTPTransTime() != _movement_orig.RTPTransTime());
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>mustBeBookedFinancially</Name>
				<Source><![CDATA[
    public boolean  mustBeBookedFinancially()
    {
        boolean ret = next mustBeBookedFinancially();

        if (ret && this is InventMov_Journal)
        {
            if (this.buffer() && this.buffer().TableId == tableNum(InventJournalTrans))
            {
                InventJournalTrans inventJournalTrans = this.buffer();

                if (inventJournalTrans.inventJournalTable().JournalType == InventJournalType::RTPReturn)
                {
                    ret = false;
                }
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>RTPTransTime</Name>
				<Source><![CDATA[
    // extension methods

    public TimeOfDay RTPTransTime()
    {
        return 0;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>