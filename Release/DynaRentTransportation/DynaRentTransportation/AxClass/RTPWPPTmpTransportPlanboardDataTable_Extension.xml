<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RTPWPPTmpTransportPlanboardDataTable_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(WPPTmpTransportPlanboardData))]
public final class RTPWPPTmpTransportPlanboardDataTable_Extension
{
    // extension methods

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>RTPBulkNotPicked</Name>
				<Source><![CDATA[
    public boolean RTPBulkNotPicked()
    {
        WHSShipmentTable    shipment;
        WHSLoadLine         loadLine;
        WPPWorkOrderTable   workOrderTable;
        InventTable         inventTable;
        boolean             ret;
            
        if (this.WorkOrderTableRecId)
        {
            while select inventTable
                join loadLine
                where loadLine.itemId == inventTable.ItemId
                    exists join shipment
                    where shipment.shipmentId == loadLine.shipmentId
                    // is not picked
            && (shipment.ShipmentStatus == WHSShipmentStatus::InProcess ||
                        shipment.ShipmentStatus == WHSShipmentStatus::Open)
                        join TableId from workOrderTable
                        where workOrderTable.OrderTableId   == shipment.TableId
                        &&    workOrderTable.OrderRecId     == shipment.RecId
                        &&    workOrderTable.RecId          == this.WorkOrderTableRecId
            {
                if (!inventTable.BOBIsSerializedRentalItem())
                {
                    ret = true;
                    break;
                }
            }
        }
    
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>RTPHasReturnJournal</Name>
				<Source><![CDATA[
    public boolean RTPHasReturnJournal()
    {
        InventJournalTable  inventJournalTable;
        WPPWorkOrderTable   workOrderTable;
            
        if (this.WorkOrderTableRecId)
        {
            select firstOnly RecId from inventJournalTable
                exists join workOrderTable
                where workOrderTable.OrderTableId   == inventJournalTable.TableId
                &&    workOrderTable.OrderRecId     == inventJournalTable.RecId
                &&    workOrderTable.RecId          == this.WorkOrderTableRecId;
        }
    
        return (inventJournalTable.RecId != 0);
    }

]]></Source>
			</Method>
			<Method>
				<Name>RTPHasShipment</Name>
				<Source><![CDATA[
    public boolean RTPHasShipment()
    {
        WHSShipmentTable    shipment;
        WPPWorkOrderTable   workOrderTable;
            
        if (this.WorkOrderTableRecId)
        {
            select firstOnly RecId from shipment
                exists join workOrderTable
                where workOrderTable.OrderTableId   == shipment.TableId
                &&    workOrderTable.OrderRecId     == shipment.RecId
                &&    workOrderTable.RecId          == this.WorkOrderTableRecId;
        }
    
        return (shipment.RecId != 0);
    }

]]></Source>
			</Method>
			<Method>
				<Name>RTPMissingObjectOnShipment</Name>
				<Source><![CDATA[
    public boolean RTPMissingObjectOnShipment()
    {
        WHSShipmentTable    shipment;
        WHSLoadLine         loadLine;
        WPPWorkOrderTable   workOrderTable;
        InventTable         inventTable;
        boolean             ret;
            
        if (this.WorkOrderTableRecId)
        {
            while select inventTable
                join InventDimId from loadLine
                where loadLine.itemId == inventTable.ItemId
                    exists join shipment
                    where shipment.shipmentId == loadLine.shipmentId
                        join TableId from workOrderTable
                        where workOrderTable.OrderTableId   == shipment.TableId
                        &&    workOrderTable.OrderRecId     == shipment.RecId
                        &&    workOrderTable.RecId          == this.WorkOrderTableRecId
            {
                if (inventTable.BOBIsSerializedRentalItem() && !loadLine.inventDim().inventSerialId)
                {
                    ret = true;
                    break;
                }
            }
        }
    
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>RTPShipment</Name>
				<Source><![CDATA[
    public WHSShipmentTable RTPShipment()
    {
        WHSShipmentTable         shipment;
        WPPWorkOrderTable   workOrderTable;
            
        if (this.WorkOrderTableRecId)
        {
            select firstOnly shipment
                exists join workOrderTable
                where workOrderTable.OrderTableId   == shipment.TableId
                &&    workOrderTable.OrderRecId     == shipment.RecId
                &&    workOrderTable.RecId          == this.WorkOrderTableRecId;
        }
    
        return shipment;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>